// Возвращает содержание переданного объекта
// Параметры
// ПредметЗадачи - объект, представление которого надо сформировать
// Возвращаемое значение:
//   Строка или MXL документ - содержание в виде HTML или MXL документа
Функция ПолучитьПредставлениеПредметаВнешнейЗадачи(ПредметЗадачи) Экспорт
	
	Если ПредметЗадачи = Неопределено Или ПредметЗадачи.Пустая() Тогда
		Возврат "";
	КонецЕсли;
	
	Представление = "";
	ВнешниеЗадачиВызовСервераПереопределяемый.ПолучитьПредставлениеПредметаВнешнейЗадачи(ПредметЗадачи, Представление);
	
	Возврат Представление;
	
КонецФункции

// Возвращает массив объектов типа ОписаниеПередаваемогоФайла
// Параметры
// Источник - объект информационной базы
// или Неопределено
// Возвращаемое значение:
//   Массив - массив ссылок на Файлы
Функция ПолучитьСписокФайлов(Источник) Экспорт
	
	МассивФайлов = Новый Массив;
	ВнешниеЗадачиВызовСервераПереопределяемый.ПолучитьСписокФайлов(Источник, МассивФайлов);
	Возврат МассивФайлов;
	
КонецФункции

// Возвращает Истина, если задача является внешней 
// Параметры
// ЗадачаСсылка - задача
// Возвращаемое значение:
//   Булево - Истина, если задача является внешней 
Функция ЭтоВнешняяЗадача(ЗадачаСсылка) Экспорт
	
	ВнешняяЗадача = ВнешниеЗадачиВызовСервераПереопределяемый.ЭтоВнешняяЗадача(ЗадачаСсылка);
	Если ВнешняяЗадача <> Неопределено Тогда
		Возврат ВнешняяЗадача;
	КонецЕсли;	
		
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьВнешниеЗадачи") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Проверяем бизнес-процесс
	БизнесПроцесс = ОбщегоНазначения.ПолучитьЗначениеРеквизита(ЗадачаСсылка, "БизнесПроцесс");
	Если ТипЗнч(БизнесПроцесс) = Тип("БизнесПроцессСсылка.Задание") Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		ВнешнееЗадание = ОбщегоНазначения.ПолучитьЗначениеРеквизита(БизнесПроцесс, "ВнешнееЗадание");
		УстановитьПривилегированныйРежим(Ложь);
		Если ВнешнееЗадание Тогда
			Возврат Истина;
		КонецЕсли;	
		
	КонецЕсли;	
	
	// Проверяем роль исполнителя
	Роль = ОбщегоНазначения.ПолучитьЗначениеРеквизита(ЗадачаСсылка, "РольИсполнителя");
	Если Роль <> Неопределено И Роль.ВнешняяРоль = Истина Тогда
		Возврат Истина;
	КонецЕсли;	
	
	Возврат Ложь;
	
КонецФункции

// Создает и стартует внешний бизнес-процесс Задание 
// при записи задачи другого бизнес-процесса, если эта задача 
// является внешней
// Параметры
// Источник - объект информационной базы
// Отказ - Булево  - Признак отказа от записи задачи
Процедура СтартВнешнегоЗаданияПриЗаписиЗадач(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда 
        Возврат;  
	КонецЕсли; 

	Если ТипЗнч(Источник.БизнесПроцесс) = Тип("БизнесПроцессСсылка.Задание") Тогда
		Возврат;
	КонецЕсли;	
	
	Если Источник.ДополнительныеСвойства.Свойство("ЗаписьНовойЗадачи") Тогда
		ВнешняяРоль = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Источник.РольИсполнителя, "ВнешняяРоль");
		Если Не Источник.РольИсполнителя.Пустая() И ВнешняяРоль = Истина Тогда
			// Создаем и стартуем внешний бизнес-процесс
			СтартоватьВнешнийБизнесПроцесс(Источник);
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры

// Стартует внешний бизнес-процесс Задание по переданной задаче
// Параметры
// Задача - ЗадачаСсылка.ЗадачаИсполнителя
Процедура СтартоватьВнешнийБизнесПроцесс(Задача)
	
	Если ВнешниеЗадачиВызовСервераПереопределяемый.СтартоватьВнешнийБизнесПроцесс(Задача) Тогда
		Возврат;
	КонецЕсли;	
	
	// Создаем бизнес-процесс задание
	Задание = БизнесПроцессы.Задание.СоздатьБизнесПроцесс();
	Задание.Дата = ТекущаяДатаСеанса();
	Задание.Автор = Задача.Автор;
	Задание.Важность = Задача.Важность;
	Задание.Исполнитель = Задача.РольИсполнителя;
	Задание.Наименование = Задача.Наименование;
	Задание.Предмет = Задача.Предмет;
	Задание.СрокИсполнения = Задача.СрокИсполнения;
	Задание.ЗадачаИсточник = Задача.Ссылка;
	
	Задание.Записать();
	Задание.Старт();
	
КонецПроцедуры

// Помечает задачу-источник как выполненную
// Параметры
// БизнесПроцесс - тип БизнесПроцессСсылка
Процедура ВыполнитьЗадачуИсточник(БизнесПроцесс) Экспорт
	
	Если ВнешниеЗадачиВызовСервераПереопределяемый.ВыполнитьЗадачуИсточник(БизнесПроцесс) Тогда
		Возврат;
	КонецЕсли;	
	
	Если ТипЗнч(БизнесПроцесс) = Тип("БизнесПроцессОбъект.Задание") Тогда
		Задача = БизнесПроцесс.ЗадачаИсточник.ПолучитьОбъект();
		Задача.РезультатВыполнения = БизнесПроцесс.РезультатВыполнения;
		Задача.ВыполнитьЗадачу();
	КонецЕсли;	
	
КонецПроцедуры

// Сохраняет сведения о записи новой задачи для последующей обработки
// в обработчике перед записью
// Параметры
// Источник - объект информационной базы
// Отказ - Булево  - Признак отказа от записи задачи
Процедура СтартВнешнегоЗаданияПередЗаписьюЗадач(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда 
        Возврат;  
	КонецЕсли; 
	
	Если Источник.ЭтоНовый() Тогда
		Источник.ДополнительныеСвойства.Вставить("ЗаписьНовойЗадачи");
	КонецЕсли;	
		
КонецПроцедуры
