///////////////////////////////////////////////////////////////////////////////////
// ПоставляемыеДанныеКлиентСервер: Механизм сервиса поставляемых данных.
//
///////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Методы обслуживания флага РучноеИзменение в формах разделенных
// поставляемых данных
//
// Единственным параметром всех процедур является 
// Форма - УправляемаяФорма элемента
// В форме должны присутствовать следующие реквизиты:
//  Объект - объект разделнный поставляемых данных
//  РучноеИзменение - Произвольный - состяние разделенного объекта
//   относительно общего. Реквизит не должен выводиться на форму.
//  ТекстРучногоИзменения - Строка, 0 - текст надписи отображающей
//   состояние разделенного объекта относительно общего.
// В форме должны присутствовать следующие кнопки:
//  ОбновитьИзКлассификатора
//  Изменить
//

///////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Задает текст состояние разделнного объекта, устанавливает доступность
// кнопок управления состоянием и флага ТолькоПросмотр формы
//
Процедура ОбработатьФлагРучногоИзменения(Знач Форма) Экспорт
	
	Элементы = Форма.Элементы;
	
	Если Форма.РучноеИзменение = Неопределено Тогда
		Форма.ТекстРучногоИзменения = НСтр("ru = 'Элемент создан вручную. Автоматическое обновление не возможно.'");
		
		Элементы.ОбновитьИзКлассификатора.Доступность = Ложь;
		Элементы.Изменить.Доступность = Ложь;
		Форма.ТолькоПросмотр = Ложь;
	ИначеЕсли Форма.РучноеИзменение = Истина Тогда
		Форма.ТекстРучногоИзменения = НСтр("ru = 'Автоматическое обновление элемента отключено.'");
		
		Элементы.ОбновитьИзКлассификатора.Доступность = Истина;
		Элементы.Изменить.Доступность = Ложь;
		Форма.ТолькоПросмотр = Ложь;
	Иначе
		Форма.ТекстРучногоИзменения = НСтр("ru = 'Элемент обновляется автоматически.'");
		
		Элементы.ОбновитьИзКлассификатора.Доступность = Ложь;
		Элементы.Изменить.Доступность = Истина;
		Форма.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

#Если Клиент Тогда
// Выдает пользователю вопрос об обновлении из общих данных.
// В случае утвердительного ответа, возвращает Истина.
//
Функция ОбновитьЭлементИзОбщихДанных(Знач Форма) Экспорт
	
	Текст = НСтр("ru = 'Данные элемента поставляемых данных будут заменены общими данными.
		|Все ручные изменения будут потеряны. Продолжить?'");
	Результат = Вопрос(Текст, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Форма.ЗаблокироватьДанныеФормыДляРедактирования();
		Форма.Модифицированность = Истина;
		
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Выдает пользователю вопрос об отказе от обновлении из общих данных.
// В случае утвердительного ответа, изменяет состояние объекта.
//
Процедура ИзменитьРазделенныйЭлемент(Знач Форма) Экспорт
	
	Текст = НСтр("ru = 'Поставляемые данные обновляются автоматически.
		|После ручного изменения автоматическое обновление этого элемента производиться не будет.
		|Продолжить с изменением?'");
	Результат = Вопрос(Текст, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Форма.ЗаблокироватьДанныеФормыДляРедактирования();
		Форма.Модифицированность = Истина;
		
		Форма.РучноеИзменение = Истина;
		
		ОбработатьФлагРучногоИзменения(Форма);
		
	КонецЕсли;
	
КонецПроцедуры
#КонецЕсли
