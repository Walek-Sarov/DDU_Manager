
// Открывает форму выбора дела
Процедура ВыбратьДело(ЭлементДело, РеквизитДело, ДатаРегистрации, Организация) Экспорт
	
	ПараметрыФормы = Новый Структура("ДатаРегистрации, ТекущаяСтрока, РежимВыбора, Организация", ДатаРегистрации, РеквизитДело, "ИзКарточкиДокумента", Организация);
	ОткрытьФорму("Справочник.ДелаХраненияДокументов.ФормаВыбора", ПараметрыФормы, ЭлементДело);
	
КонецПроцедуры	

// Открывает форму выбора получателя
Процедура ВыбратьПолучателя(ЭлементПолучатель, РеквизитПолучатель) Экспорт
	
	ПараметрыФормы = Новый Структура("Получатель", РеквизитПолучатель);
	ОткрытьФорму("ОбщаяФорма.ВыборПолучателя", ПараметрыФормы, ЭлементПолучатель);
	
КонецПроцедуры	

// Выполняет проверки перед закрытием документа 
Процедура ПередЗакрытиемДокумента(Объект, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Возврат;
	КонецЕсли;
	
	ТекущийПользователь = РаботаСФайламиКлиентПовтИсп.ПолучитьПерсональныеНастройкиРаботыСФайлами().ТекущийПользователь;
	ЗанятыеФайлы = РаботаСФайлами.ПолучитьЗанятыеФайлы(Объект.Ссылка, ТекущийПользователь);
	
	Если ЗанятыеФайлы.Количество() > 0 Тогда 
		
		ПараметрыФормы = Новый Структура;	
		ПараметрыФормы.Вставить("СообщениеВопрос", 		НСТР("ru = 'Закрыть карточку?'"));
		ПараметрыФормы.Вставить("СообщениеЗаголовок", 	НСТР("ru = 'Следующие файлы заняты вами для редактирования:'"));
		ПараметрыФормы.Вставить("Заголовок", 			Строка(Объект.Ссылка));
		ПараметрыФормы.Вставить("ВладелецФайла", 		Объект.Ссылка);
		ПараметрыФормы.Вставить("Редактирует", 			ТекущийПользователь);
		
		Результат = ОткрытьФормуМодально("Справочник.Файлы.Форма.СписокЗанятыхСВопросом", ПараметрыФормы);
		Если Результат <> КодВозвратаДиалога.Да Тогда 
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры	

// Открывает форму текущего состояния документа
Процедура ОткрытьТекущееСостояниеДокумента(Документ, Элемент) Экспорт
	
	Если Не ЗначениеЗаполнено(Документ) Тогда 
		Предупреждение(НСтр("ru = 'Для доступа к состоянию элемент необходимо записать!'"));
		Возврат;
	КонецЕсли;	
	
	Ключ = Делопроизводство.ПолучитьКлючСостоянияДокумента(Документ);
	Если Ключ = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	ПараметрыФормы = Новый Структура("Ключ", Ключ);
	Форма = ПолучитьФорму("РегистрСведений.СостоянияДокументов.ФормаЗаписи", ПараметрыФормы, Элемент);
	Форма.ОткрытьМодально();
	
КонецПроцедуры	

Функция ПредупредитьПриРегистрации() Экспорт
	
	ПоказыватьПредупреждениеПриРегистрации = ДелопроизводствоКлиентПовтИсп.ПолучитьПерсональныеНастройкиРаботыСДокументами().ПоказыватьПредупреждениеПриРегистрации;
	Если ПоказыватьПредупреждениеПриРегистрации = Истина Тогда 
		
		Диалог = ПолучитьФорму("ОбщаяФорма.ПредупреждениеПриРегистрации");
		Результат = Диалог.ОткрытьМодально();
		
		БольшеНеСпрашивать = Диалог.БольшеНеСпрашивать;
		Если БольшеНеСпрашивать = Истина Тогда
			РаботаСФайлами.ХранилищеОбщихНастроекСохранить("НастройкиРаботыСДокументами", "ПоказыватьПредупреждениеПриРегистрации", Ложь);
			ОбновитьПовторноИспользуемыеЗначения();
		КонецЕсли;
		
		Если Результат <> КодВозвратаДиалога.Да Тогда
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции	
