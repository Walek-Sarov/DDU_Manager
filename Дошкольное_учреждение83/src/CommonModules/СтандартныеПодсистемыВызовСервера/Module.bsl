////////////////////////////////////////////////////////////////////////////////
// Подсистема "Базовая функциональность".
//  
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЙ ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Записать настройку подтверждения завершения работы программы
// для текущего пользователя.
// 
// Параметры:
//   Значение - Булево   - значение настройки.
// 
Процедура СохранитьНастройкуПодтвержденияПриЗавершенииПрограммы(Значение) Экспорт
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ОбщиеНастройкиПользователя", "ЗапрашиватьПодтверждениеПриЗавершенииПрограммы", Значение);
	
КонецПроцедуры

// Возвращает настройку подтверждения завершения работы программы
// для текущего пользователя.
// 
Функция ЗагрузитьНастройкуПодтвержденияПриЗавершенииПрограммы() Экспорт
	
	Результат = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ОбщиеНастройкиПользователя", "ЗапрашиватьПодтверждениеПриЗавершенииПрограммы");
	Если Результат = Неопределено Тогда
		Результат = Истина;
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

// Возвращает структуру параметров, необходимых для работы клиентского кода
// при запуске конфигурации, т.е. в обработчиках событий
// - ПередНачаломРаботыСистемы,
// - ПриНачалеРаботыСистемы
//
// Подробнее см. ПараметрыРаботыКлиентаПриЗапуске в общем модуле СтандартныеПодсистемыВызовСервераПереопределяемый.
//
Функция ПараметрыРаботыКлиентаПриЗапуске() Экспорт
	
	Параметры = Новый Структура();
	
	Если НЕ СтандартныеПодсистемыВызовСервераПереопределяемый.ДобавитьПараметрыРаботыКлиентскойЛогикиСтандартныхПодсистемПриЗапуске(Параметры) Тогда
		Возврат Новый ФиксированнаяСтруктура(Параметры);
	КонецЕсли;
	
	ПрикладныеПараметры = ОбщегоНазначенияВызовСервераПереопределяемый.ПараметрыРаботыКлиентаПриЗапуске();
	Для Каждого Параметр Из ПрикладныеПараметры Цикл
		Параметры.Вставить(Параметр.Ключ, Параметр.Значение);
	КонецЦикла;
	
	Возврат Новый ФиксированнаяСтруктура(Параметры);
	
КонецФункции

// Возвращает структуру параметров, необходимых для работы клиентского кода
// конфигурации. 
//
// Подробнее см. ПараметрыРаботыКлиента в общем модуле СтандартныеПодсистемыВызовСервераПереопределяемый.
//
Функция ПараметрыРаботыКлиента() Экспорт
	
	Параметры = Новый Структура();
	СтандартныеПодсистемыВызовСервераПереопределяемый.ДобавитьПараметрыРаботыКлиентскойЛогикиСтандартныхПодсистем(Параметры);
	
	ПрикладныеПараметры = ОбщегоНазначенияВызовСервераПереопределяемый.ПараметрыРаботыКлиента();
	Для Каждого Параметр Из ПрикладныеПараметры Цикл
		Параметры.Вставить(Параметр.Ключ, Параметр.Значение);
	КонецЦикла;
	Возврат Новый ФиксированнаяСтруктура(Параметры);
	
КонецФункции

// Подбора поставляемых данных из классификатора.
// Запускается в фоне механизмом длительных операций.
//
// Параметры:
// Параметры - Структура
//	Ссылки - Массив - Ссылки на элементы справочников.
//	ИгнорироватьРучноеИзменение - Булево.
// АдресХранилища - Строка; Неопределено.
//
Процедура ПодобратьПоставляемыеДанныеИзКлассификатора(Знач Параметры, АдресХранилища = Неопределено) Экспорт

	СтандартныеПодсистемыВызовСервераПереопределяемый.ПодобратьПоставляемыеДанныеИзКлассификатора(
		Параметры.Ссылки, Параметры.ИгнорироватьРучноеИзменение);
	
КонецПроцедуры
