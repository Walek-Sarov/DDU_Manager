////////////////////////////////////////////////////////////////////////////////
// Подсистема "Версионирование объектов".
//
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Записывает версию объекта в информационную базу.
//
// Параметры
//  Источник - Объект - записываемый объект ИБ;
//  Отказ    - Булево - признак отказа от записи объекта.
//
Процедура ЗаписатьВерсиюОбъекта(Источник, Отказ) Экспорт
	
	Перем НомерПоследнейВерсии, Комментарий;
	
	Если Источник.ОбменДанными.Загрузка Тогда 
		Возврат; 
	КонецЕсли;	
	
	Если НЕ ОбъектВерсионируется(Источник, НомерПоследнейВерсии) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписьXML = Новый ЗаписьFastInfoset;
	ЗаписьXML.УстановитьДвоичныеДанные();
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	ЗаписатьXML(ЗаписьXML, Источник, НазначениеТипаXML.Явное);
	ДвоичныеДанные = ЗаписьXML.Закрыть();
	ХранилищеДанных = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(9));
	
	Если НЕ Источник.ДополнительныеСвойства.Свойство("ВерсионированиеОбъектовКомментарийКВерсии", Комментарий) Тогда
		Комментарий = "";
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписиВерсииОбъектов = РегистрыСведений.ВерсииОбъектов.СоздатьМенеджерЗаписи();
	МенеджерЗаписиВерсииОбъектов.Объект        = Источник.Ссылка;
	МенеджерЗаписиВерсииОбъектов.ДатаВерсии    = ТекущаяДатаСеанса();
	МенеджерЗаписиВерсииОбъектов.ВерсияОбъекта = ХранилищеДанных;
	МенеджерЗаписиВерсииОбъектов.НомерВерсии   = Число(НомерПоследнейВерсии) + 1;
	МенеджерЗаписиВерсииОбъектов.АвторВерсии   = Пользователи.АвторизованныйПользователь();
	МенеджерЗаписиВерсииОбъектов.Комментарий   = Комментарий;
	
	МенеджерЗаписиВерсииОбъектов.Записать();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Проверяет настройки версионирования по переданному объекту и
// и возвращает вариант версионирования. Если по объекту не настроено
// версионирование, то он версионируется в соответствии с правилами
// версионирования "по умолчанию".
//
Функция ОбъектВерсионируется(знач Источник, НомерПоследнейВерсии)
	
	ИспользоватьВерсионированиеОбъектов = ПолучитьФункциональнуюОпцию("ИспользоватьВерсионированиеОбъектов");
	// Проверяем, что подсистема версионирования включена
	Если НЕ ИспользоватьВерсионированиеОбъектов Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПолноеИмяОбъектаМетаданных = Источник.Метаданные().ПолноеИмя();
	ВариантВерсионирования = ВерсионированиеОбъектов.ВариантВерсионированияОбъекта(ПолноеИмяОбъектаМетаданных);
	Если ВариантВерсионирования = Ложь Тогда
		ВариантВерсионирования = ВерсионированиеОбъектов.НастройкаВерсионированияПоУмолчанию(ПолноеИмяОбъектаМетаданных);
	КонецЕсли;
	
	НомерПоследнейВерсии = ВерсионированиеОбъектов.НомерПоследнейВерсии(Источник.Ссылка);
	ВерсионироватьОбъект = Истина;
	Если ВариантВерсионирования = Перечисления.ВариантыВерсионированияОбъектов.НеВерсионировать Тогда
		ВерсионироватьОбъект = Ложь;
	ИначеЕсли ВариантВерсионирования = Перечисления.ВариантыВерсионированияОбъектов.ВерсионироватьПриПроведении Тогда
		Если НомерПоследнейВерсии = 0 И НЕ Источник.Проведен Тогда
			ВерсионироватьОбъект = Ложь;
		КонецЕсли;
	КонецЕсли;
		
	Возврат ВерсионироватьОбъект;
	
КонецФункции
