Функция ДатаКакМесяцПредставление(ДатаДата) Экспорт
	Возврат Формат(ДатаДата, "ДФ='ММММ гггг'");
КонецФункции

// Процедура обрабатывает событие начала выбора из списка в поле периода регистрации.
// Процедура исполняется только на клиенте
//
Процедура НачалоВыбораИзСпискаПредставленияПериодаРегистрации(Элемент, СтандартнаяОбработка, ПериодРегистрации, ЭтаФорма, НачальноеЗначение = Неопределено) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если НачальноеЗначение = Неопределено Тогда
		НачальноеЗначение = ПериодРегистрации;
	КонецЕсли; 
	
	СписокВыбора = Новый СписокЗначений;
	НачалоТекущегоГода = НачалоГода(НачальноеЗначение);
	НачалоПрошлогоГода = НачалоГода(НачалоТекущегоГода - 1);
	СписокВыбора.Добавить(НачалоПрошлогоГода, (Формат(НачалоПрошлогоГода, "ДФ='yyyy'") + "..."));
	НачалоМесяцаЗаполнения = НачалоТекущегоГода;
	ЭлементПоУмолчанию = Неопределено;
	Для а = 1 По 12 Цикл
		ДобавленныйЭлемент = СписокВыбора.Добавить(НачалоМесяцаЗаполнения, ДатаКакМесяцПредставление(НачалоМесяцаЗаполнения));
		Если НачальноеЗначение = НачалоМесяцаЗаполнения Тогда
			ЭлементПоУмолчанию = ДобавленныйЭлемент;
		КонецЕсли; 
		НачалоМесяцаЗаполнения = ДобавитьМесяц(НачалоМесяцаЗаполнения, 1);
	КонецЦикла;
	НачалоСледующегоГода = КонецГода(НачалоТекущегоГода) + 1;
	СписокВыбора.Добавить(НачалоСледующегоГода, (Формат(НачалоСледующегоГода, "ДФ='yyyy'") + "..."));
	
	ВыбранныйЭлемент = ЭтаФорма.ВыбратьИзСписка(СписокВыбора, Элемент, ЭлементПоУмолчанию);
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат ;
	ИначеЕсли Год(ВыбранныйЭлемент.Значение) <> Год(НачальноеЗначение) Тогда
		НачалоВыбораИзСпискаПредставленияПериодаРегистрации(Элемент, СтандартнаяОбработка, ПериодРегистрации, ЭтаФорма, ВыбранныйЭлемент.Значение);
		Возврат ;
	КонецЕсли;
	
	ПериодРегистрации = ВыбранныйЭлемент.Значение;
	
КонецПроцедуры

// Процедура открывает форму выбора ПланыВидовРасчета.удуОсновныеНачисленияОрганизаций.
// Отборы передаются в структуре СтруктураПараметровОтбора
//
// Параметры:
//	Владелец 			- владелец формы
//	НастройкаОтбора - параметры отбора
//	ТекущееЗначение 	- ссылка на элемент, на котором необходимо спозиционироваться при открытии формы	
//
Процедура ОткрытьФормуВыбораОсновныеНачисленияОрганизаций(Владелец, НастройкаОтбора, ТекущееЗначение = Неопределено) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	СтруктураПараметров=Новый Структура;
	СтруктураПараметров.Вставить("АдресНастройкиОтбора",ПоместитьВоВременноеХранилище(НастройкаОтбора));
	СтруктураПараметров.Вставить("ТекущееЗначение",ТекущееЗначение);
	
	ФормаВыбора = ПолучитьФорму("ПланВидовРасчета.удуОсновныеНачисленияОрганизаций.Форма.ФормаВыбора",СтруктураПараметров, Владелец);
		
		
	ФормаВыбора.Открыть();
	
КонецПроцедуры
