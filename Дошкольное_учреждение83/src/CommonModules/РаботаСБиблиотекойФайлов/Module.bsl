// Находит папку в библиотеке файлов, соответствующую указанному объекту
// Параметры
// Ссылка - объект, для которого надо найти папку
// 		тип значения - любая ссылка
// СоздатьПриОтсутствии - признак необходимости создания папки при ее отсутствии
// 		тип значения - булево
// Возвращаемое значение
// Папка
// 		Folder (http://www.1c.ru/docmng)
//
Функция ПолучитьПапкуФайлов(Ссылка, СоздатьПриОтсутствии) Экспорт
	
	Папка = РаботаСБиблиотекойФайловПереопределяемый.ПолучитьПапкуФайлов(Ссылка, Ложь);
	Если Папка <> Неопределено Тогда
		Возврат Папка;
	КонецЕсли;
	
	Прокси = ПолучитьПрокси();
	
	ПапкаТип = Прокси.ФабрикаXDTO.Тип("http://www.1c.ru/docmng", "Folder");
	КорневаяПапка = Прокси.ФабрикаXDTO.Создать(ПапкаТип);
	КорневаяПапка.Code = Константы.КодКорневойПапкиБиблиотекиФайлов.Получить();
	КорневаяПапка.Name = "";
	КорневаяПапка.Description = "";
	
	МетаданныеОбъекта = Метаданные.НайтиПоТипу(ТипЗнч(Ссылка));
	Если МетаданныеОбъекта = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;	
	
	ПапкаТипа = Прокси.FindFolder(КорневаяПапка, МетаданныеОбъекта.ПолноеИмя());
	Если ПустаяСтрока(ПапкаТипа.Code) Тогда
		Если СоздатьПриОтсутствии Тогда
			ИмяПапки = МетаданныеОбъекта.ПолноеИмя();
			ПапкаТипа = Прокси.AddFolder(КорневаяПапка, ИмяПапки, "");
		Иначе	
			Возврат Неопределено;
		КонецЕсли;	
	КонецЕсли;	
	
	ИДОбъекта = Строка(Ссылка.УникальныйИдентификатор());
	
	Папка = Прокси.FindFolder(ПапкаТипа, ИДОбъекта);
	Если ПустаяСтрока(Папка.Code) Тогда
		Если СоздатьПриОтсутствии Тогда
			Папка = Прокси.AddFolder(ПапкаТипа, ИДОбъекта, Строка(Ссылка));
		Иначе	
			Возврат Неопределено;
		КонецЕсли;	
	КонецЕсли;	
	
	Возврат Папка;
	
КонецФункции

// Возвращает прокси веб-сервиса
// Возвращаемое значение
// Прокси веб-сервиса
// 		WSПрокси
//
Функция ПолучитьПрокси() Экспорт
	
	АдресВебСервисаБиблиотекиФайлов = Константы.АдресВебСервисаБиблиотекиФайлов.Получить();
	ИмяПользователя = Константы.ИмяПользователяБиблиотекиФайлов.Получить();
	Пароль = Константы.ПарольПользователяБиблиотекиФайлов.Получить();
	
	Возврат ПолучитьПроксиПоПараметрам(АдресВебСервисаБиблиотекиФайлов, ИмяПользователя, Пароль);
	
КонецФункции	

// Возвращает прокси веб-сервиса
// Возвращаемое значение
// Прокси веб-сервиса
// 		WSПрокси
// Параметры
// АдресВебСервисаБиблиотекиФайлов - строка - адрес базы на веб сервере
// ИмяПользователя - строка-  Имя пользователя
// Пароль - строка - пароль пользователя
Функция ПолучитьПроксиПоПараметрам(АдресВебСервисаБиблиотекиФайлов, ИмяПользователя, Пароль) Экспорт
	
	МестоположениеWSDL = АдресВебСервисаБиблиотекиФайлов;
	Если ЗначениеЗаполнено(МестоположениеWSDL) И 
		Прав(МестоположениеWSDL, 1) <> "/" И Прав(МестоположениеWSDL, 1) <> "\" Тогда
		МестоположениеWSDL = МестоположениеWSDL + "/";
	КонецЕсли;	
	МестоположениеWSDL = МестоположениеWSDL + "ws/files.1cws?wsdl";
	
	Определение = Новый WSОпределения(
		МестоположениеWSDL, 
		ИмяПользователя,
		Пароль);
	
	Прокси = Новый WSПрокси(
		Определение,
		"http://www.1c.ru/docmng",
		"Files",
		"FilesSoap");
		
	Прокси.Пользователь = ИмяПользователя;
	Прокси.Пароль = Пароль;
	
	Возврат Прокси;
	
КонецФункции	
