
Функция ПолучитьОрганизациюПоУмолчанию() Экспорт
	
	ОрганизацияПоУмолчанию = Неопределено;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда 
		Возврат ОрганизацияПоУмолчанию;
	КонецЕсли;	
	
	ОрганизацияПоУмолчанию = ХранилищеОбщихНастроек.Загрузить("НастройкиРаботыСДокументами", "Организация");
	Если ЗначениеЗаполнено(ОрганизацияПоУмолчанию) Тогда 
		Возврат ОрганизацияПоУмолчанию;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	Организации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	(НЕ Организации.ПометкаУдаления)
	|	И ЛОЖЬ В
	|			(ВЫБРАТЬ
	|				ЛОЖЬ КАК ЗначениеЛожь
	|			ИЗ
	|				(ВЫБРАТЬ ПЕРВЫЕ 2
	|					ИСТИНА КАК ЗначениеИстина
	|				ИЗ
	|					Справочник.Организации КАК Организации
	|				ГДЕ
	|					(НЕ Организации.ПометкаУдаления)
	|				) КАК ВыбранныеОбъекты
	|			ИМЕЮЩИЕ
	|				КОЛИЧЕСТВО(ВыбранныеОбъекты.ЗначениеИстина) = 1)";
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда 
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ОрганизацияПоУмолчанию = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат ОрганизацияПоУмолчанию;
	
КонецФункции	

Функция ПолучитьОтветсвенноеЛицо(ОтветственноеЛицо, Организация, Период) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда 
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтветственныеЛицаОрганизацийСрезПоследних.Пользователь
		|ИЗ
		|	РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(
		|			&Период,
		|			Организация = &Организация
		|				И ОтветственноеЛицо = &ОтветственноеЛицо) КАК ОтветственныеЛицаОрганизацийСрезПоследних";
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("Период", Период);
		
		Если ОтветственноеЛицо = "Руководитель" Тогда
			Запрос.УстановитьПараметр("ОтветственноеЛицо", Перечисления.ОтветственныеЛицаОрганизаций.РуководительОрганизации);
		ИначеЕсли ОтветственноеЛицо = "РуководительАрхива" Тогда
			Запрос.УстановитьПараметр("ОтветственноеЛицо", Перечисления.ОтветственныеЛицаОрганизаций.РуководительАрхива);
		ИначеЕсли ОтветственноеЛицо = "РуководительСлужбыДОУ" Тогда	
			Запрос.УстановитьПараметр("ОтветственноеЛицо", Перечисления.ОтветственныеЛицаОрганизаций.РуководительСлужбыДОУ);
		КонецЕсли;
		
		Результат = Запрос.Выполнить();
		Если Результат.Пустой() Тогда 
			Возврат Справочники.Пользователи.ПустаяСсылка();
		КонецЕсли;	
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Пользователь;
		
	Иначе
		
		Если ОтветственноеЛицо = "Руководитель" Тогда
			Возврат Константы.РуководительПредприятия.Получить();
		ИначеЕсли ОтветственноеЛицо = "РуководительАрхива" Тогда
			Возврат Константы.РуководительАрхива.Получить();
		ИначеЕсли ОтветственноеЛицо = "РуководительСлужбыДОУ" Тогда	
			Возврат Константы.РуководительСлужбыДОУ.Получить();
		КонецЕсли;	
			
	КонецЕсли;
	
КонецФункции	

Функция ПолучитьНаименованиеОрганизации(Организация) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда 
		Возврат ?(ЗначениеЗаполнено(Организация.ПолноеНаименование), Организация.ПолноеНаименование, Организация.Наименование);
	Иначе
		Возврат Константы.НаименованиеПредприятия.Получить();
	КонецЕсли;
	
КонецФункции	