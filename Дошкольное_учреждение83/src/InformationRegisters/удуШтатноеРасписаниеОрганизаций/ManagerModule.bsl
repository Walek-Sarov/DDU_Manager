// Процедура предназначена для заполнения набора записей штатного расписания по текущему состоянию кадров.
// 
// Параметры:
//	Организация - СправочникСсылка.Организации,организация, для которой заполняется 
//					штатное расписание
//  Период - дата, дата по состоянию на которую осматривается кадровый состав
//
// Возвращаемое значение:
//	нет
//
Процедура Автозаполнение(ЭтотОбъект,Организация, Период, Сообщать = Ложь) Экспорт 
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Если Сообщать Тогда
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст="Не указана организация!";
			СообщениеПользователю.Сообщить();
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Период) Тогда
		Если Сообщать Тогда
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст="Не указана дата!";
			СообщениеПользователю.Сообщить();
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период", Период);

	МассивОтпускПоУходуЗаРебенком = Новый Массив;
	МассивОтпускПоУходуЗаРебенком.Добавить (ПланыВидовРасчета.удуОсновныеНачисленияОрганизаций.ОтпускПоУходуЗаРебенкомБезОплаты);
	МассивОтпускПоУходуЗаРебенком.Добавить (ПланыВидовРасчета.удуОсновныеНачисленияОрганизаций.ПособиеПоУходуЗаРебенкомДо1_5Лет);
	МассивОтпускПоУходуЗаРебенком.Добавить (ПланыВидовРасчета.удуОсновныеНачисленияОрганизаций.ПособиеПоУходуЗаРебенкомДо3Лет);
	Запрос.УстановитьПараметр("ОтпускПоУходуЗаРебенком", МассивОтпускПоУходуЗаРебенком);
	
	Запрос.Текст ="ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	              |	ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Сотрудник
	              |ПОМЕСТИТЬ СотрудникиПоУходуЗаРебенком
	              |ИЗ
	              |	РегистрСведений.удуПлановыеНачисленияРаботниковОрганизаций.СрезПоследних(
	              |			&Период,
	              |			Организация = &Организация
	              |				И ВидРасчета В (&ОтпускПоУходуЗаРебенком)) КАК ПлановыеНачисленияРаботниковОрганизацийСрезПоследних
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	              |	РаботникиОрганизацийСрезПоследних.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	              |	РаботникиОрганизацийСрезПоследних.Должность КАК Должность,
	              |	СУММА(РаботникиОрганизацийСрезПоследних.ЗанимаемыхСтавок) КАК КоличествоСтавок,
	              |	&Период КАК Период,
	              |	ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчета,
	              |	ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Показатель1 КАК МесячнаяСтавка
	              |ИЗ
	              |	РегистрСведений.удуРаботникиОрганизаций.СрезПоследних(
	              |			&Период,
	              |			Организация = &Организация
	              |				И (НЕ Сотрудник В
	              |						(ВЫБРАТЬ
	              |							СотрудникиПоУходуЗаРебенком.Сотрудник
	              |						ИЗ
	              |							СотрудникиПоУходуЗаРебенком КАК СотрудникиПоУходуЗаРебенком))) КАК РаботникиОрганизацийСрезПоследних
	              |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.удуПлановыеНачисленияРаботниковОрганизаций.СрезПоследних(
	              |				&Период,
	              |				ВидРасчетаИзмерение.Код ЕСТЬ NULL 
	              |					И Организация = &Организация
	              |					И (НЕ Сотрудник В
	              |							(ВЫБРАТЬ
	              |								СотрудникиПоУходуЗаРебенком.Сотрудник
	              |							ИЗ
	              |								СотрудникиПоУходуЗаРебенком КАК СотрудникиПоУходуЗаРебенком))) КАК ПлановыеНачисленияРаботниковОрганизацийСрезПоследних
	              |		ПО РаботникиОрганизацийСрезПоследних.Сотрудник = ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Сотрудник
	              |ГДЕ
	              |	РаботникиОрганизацийСрезПоследних.ПричинаИзмененияСостояния <> ЗНАЧЕНИЕ(Перечисление.удуПричиныИзмененияСостояния.Увольнение)
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	РаботникиОрганизацийСрезПоследних.ПодразделениеОрганизации,
	              |	РаботникиОрганизацийСрезПоследних.Должность,
	              |	ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчета,
	              |	ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Показатель1";
	
	// Собственно заполняем набор записей.
	ЭтотОбъект.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры // Автозаполнение()

