////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
Процедура СохранитьФайлСервер(МассивИменФайлов,Каталог)
	
	НаборЗаписей = РегистрыСведений.удуВнешниеКомпоненты.СоздатьНаборЗаписей();
	Для Каждого ИмяФайла Из МассивИменФайлов Цикл
		НаборЗаписей.Отбор.ИмяФайла.Установить(ИмяФайла);
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() > 0 Тогда
			Попытка 
				НаборЗаписей[0].ХранилищеФайла.Получить().Записать(Каталог + "\" + ИмяФайла);
			Исключение
				СообщениеПользователю = Новый СообщениеПользователю;
				СообщениеПользователю.Текст=ОписаниеОшибки();
				СообщениеПользователю.Сообщить();
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ДобавлениеВнешнейКомпонентыСервере(ИсходныйФайл)
	
	удуОбщегоНазначенияСервер.ДобавлениеВнешнейКомпоненты(ИсходныйФайл);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

&НаКлиенте
Процедура СохранитьФайл(Команда)
	
			
		ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
		Если ВыделенныеСтроки.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
		ДиалогВыбораФайла.Заголовок                   = "Выберите каталог, в который следует сохранить файлы";
		ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
		Если Не ДиалогВыбораФайла.Выбрать() Тогда
			Возврат;
		КонецЕсли;
		
		МассивИменФайлов=Новый Массив;
		Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
			МассивИменФайлов.Добавить(ВыделеннаяСтрока.ИмяФайла);
		КонецЦикла;
		
		СохранитьФайлСервер(МассивИменФайлов,ДиалогВыбораФайла.Каталог);

КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	Если Копирование Тогда
		Возврат;
	КонецЕсли;
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Заголовок                   = "Выбор добавляемых файлов";
	ДиалогВыбораФайла.МножественныйВыбор          = Истина;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
	ДиалогВыбораФайла.Фильтр                      = "dll файлы (*.dll)|*.dll|Все файлы (*.*)|*.*|";
		
	Если НЕ ДиалогВыбораФайла.Выбрать() Тогда
		Возврат;
	КонецЕсли;
		
	ИсходныеФайлы = ДиалогВыбораФайла.ВыбранныеФайлы;
		
	Для Каждого ИсходныйФайл Из ИсходныеФайлы Цикл
		ДобавлениеВнешнейКомпонентыСервере(ИсходныйФайл);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	ИмяФайла = Элементы.Список.ТекущиеДанные.ИмяФайла;
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Заголовок                   = "Выбор изменяемого файла";
	ДиалогВыбораФайла.МножественныйВыбор          = Ложь;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
	ДиалогВыбораФайла.Фильтр                      = ИмяФайла + "|" + ИмяФайла;
	
	Если НЕ ДиалогВыбораФайла.Выбрать() Тогда
		Возврат;
	КонецЕсли;
	
	ИсходныеФайлы = ДиалогВыбораФайла.ВыбранныеФайлы;
	
	Для каждого ИсходныйФайл Из ИсходныеФайлы Цикл
		ДобавлениеВнешнейКомпонентыСервере(ИсходныйФайл);
	КонецЦикла;

КонецПроцедуры
