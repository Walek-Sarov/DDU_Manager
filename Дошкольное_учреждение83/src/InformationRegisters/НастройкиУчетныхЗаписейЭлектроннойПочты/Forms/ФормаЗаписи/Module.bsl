
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	УчетнаяЗаписьЭлектроннойПочты = Неопределено;
	Если Параметры.Свойство("УчетнаяЗаписьЭлектроннойПочты", УчетнаяЗаписьЭлектроннойПочты) Тогда
		МенеджерЗаписи = РегистрыСведений.НастройкиУчетныхЗаписейЭлектроннойПочты.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.УчетнаяЗаписьЭлектроннойПочты = УчетнаяЗаписьЭлектроннойПочты;
		МенеджерЗаписи.Прочитать();
		ЗначениеВРеквизитФормы(МенеджерЗаписи, "Запись");
	
		Запись.УчетнаяЗаписьЭлектроннойПочты = УчетнаяЗаписьЭлектроннойПочты;
		Если УчетнаяЗаписьЭлектроннойПочты.Пустая() Тогда
			
			МетаданныеРесурсы = Метаданные.РегистрыСведений.НастройкиУчетныхЗаписейЭлектроннойПочты.Ресурсы;
			Запись.ВключатьПодписьДляНовыхСообщений  = МетаданныеРесурсы.ВключатьПодписьДляНовыхСообщений.ЗначениеЗаполнения;
			Запись.ФорматПодписиДляНовыхСообщений    = МетаданныеРесурсы.ФорматПодписиДляНовыхСообщений.ЗначениеЗаполнения;
			Запись.ВключатьПодписьПриОтветеПересылке = МетаданныеРесурсы.ВключатьПодписьПриОтветеПересылке.ЗначениеЗаполнения;
			Запись.ФорматПодписиПриОтветеПересылке   = МетаданныеРесурсы.ФорматПодписиПриОтветеПересылке.ЗначениеЗаполнения;
			
		ИначеЕсли Запись.ПерсональнаяУчетнаяЗапись Тогда
			ПользовательУчетнойЗаписи = Запись.ОтветственныйЗаВедениеПапок;
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеДоступностью();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ФорматПодписиДляНовыхСообщений = Перечисления.СпособыРедактированияЭлектронныхПисем.HTML Тогда
		
		ТекущийОбъект.ПодписьДляНовыхСообщенийПростойТекст =
			НовоеСообщениеФорматированныйДокумент.ПолучитьТекст();
		
		ТекущийОбъект.ПодписьДляНовыхСообщенийФорматированныйДокумент =
			Новый ХранилищеЗначения(НовоеСообщениеФорматированныйДокумент);
		
	Иначе
		
		ТекущийОбъект.ПодписьДляНовыхСообщенийФорматированныйДокумент = Неопределено;
		
	КонецЕсли;
	
	Если ТекущийОбъект.ФорматПодписиПриОтветеПересылке = Перечисления.СпособыРедактированияЭлектронныхПисем.HTML Тогда
		
		ТекущийОбъект.ПодписьПриОтветеПересылкеПростойТекст = ПриОтветеПересылкеФорматированныйДокумент.ПолучитьТекст();
		ТекущийОбъект.ПодписьПриОтветеПересылкеФорматированныйДокумент = 
			Новый ХранилищеЗначения(ПриОтветеПересылкеФорматированныйДокумент);
		
	Иначе
		
		ТекущийОбъект.ПодписьПриОтветеПересылкеФорматированныйДокумент = Неопределено;
		
	КонецЕсли;
	
	Если ТекущийОбъект.ПерсональнаяУчетнаяЗапись Тогда
		
		ТекущийОбъект.ОтветственныйЗаОбработкуПисем = ПользовательУчетнойЗаписи;
		ТекущийОбъект.ОтветственныйЗаВедениеПапок   = ПользовательУчетнойЗаписи;
		ТекущийОбъект.УдалятьПисьмаПослеОтправки    = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры 

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если ТекущийОбъект.ВключатьПодписьДляНовыхСообщений И 
			ТекущийОбъект.ФорматПодписиДляНовыхСообщений = Перечисления.СпособыРедактированияЭлектронныхПисем.HTML Тогда
		
		НовоеСообщениеФорматированныйДокумент = ТекущийОбъект.ПодписьДляНовыхСообщенийФорматированныйДокумент.Получить();
		
	КонецЕсли;
	
	Если ТекущийОбъект.ВключатьПодписьПриОтветеПересылке И 
			ТекущийОбъект.ФорматПодписиПриОтветеПересылке = Перечисления.СпособыРедактированияЭлектронныхПисем.HTML Тогда
		
		ПриОтветеПересылкеФорматированныйДокумент = 
			ТекущийОбъект.ПодписьПриОтветеПересылкеФорматированныйДокумент.Получить();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Запись.ПерсональнаяУчетнаяЗапись Тогда
		Если ПользовательУчетнойЗаписи.Пустая() Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан пользователь учетной записи'"),,
			                                                       "ПользовательУчетнойЗаписи");
		КонецЕсли;
	Иначе
		
		Если Запись.ОтветственныйЗаВедениеПапок.Пустая() Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан владелец ответственный за ведение папок'"),,
			                                                       "ОтветственныйЗаВедениеПапок",
			                                                       "Запись",Истина);
		КонецЕсли;
		
		Если Запись.ОтветственныйЗаОбработкуПисем.Пустая() Тогда
			 ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан владелец ответственный за обработку писем'"),,
			                                                       "ОтветственныйЗаОбработкуПисем",
			                                                       "Запись",Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ВключатьПодписьДляНовыйСообщенийПриИзменении(Элемент)
	
	УправлениеДоступностью();
	
КонецПроцедуры

&НаКлиенте
Процедура ВключатьПодписьПриОтветеПересылкеПриИзменении(Элемент)
	
	УправлениеДоступностью();
	
КонецПроцедуры

&НаКлиенте
Процедура ФорматПодписиДляНовыхСообщенийПриИзменении(Элемент)
	
	Если Запись.ФорматПодписиДляНовыхСообщений = ПредопределенноеЗначение(
		"Перечисление.СпособыРедактированияЭлектронныхПисем.HTML") И
		Элементы.СтраницыПодписьДляНовыхСообщений.ТекущаяСтраница = Элементы.СтраницаНовоеСообщениеФорматированныйТекст Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если Запись.ФорматПодписиДляНовыхСообщений = ПредопределенноеЗначение(
		"Перечисление.СпособыРедактированияЭлектронныхПисем.ОбычныйТекст") И
		Элементы.СтраницыПодписьДляНовыхСообщений.ТекущаяСтраница = Элементы.СтраницаНовоеСообщениеПростойТекст Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если Запись.ФорматПодписиДляНовыхСообщений = ПредопределенноеЗначение(
		"Перечисление.СпособыРедактированияЭлектронныхПисем.HTML") Тогда
		
		Если Не ПустаяСтрока(Запись.ПодписьДляНовыхСообщенийПростойТекст) Тогда
			НовоеСообщениеФорматированныйДокумент.Удалить();
			НовоеСообщениеФорматированныйДокумент.Добавить(Запись.ПодписьДляНовыхСообщенийПростойТекст);
		КонецЕсли;
		
	Иначе
		
		Если Не ВзаимодействияКлиент.ПриИзмененииФорматаСообщенияНаОбычныйТекст() Тогда
			
			Запись.ФорматПодписиДляНовыхСообщений = 
				ПредопределенноеЗначение("Перечисление.СпособыРедактированияЭлектронныхПисем.HTML");
				
			Возврат;
			
		КонецЕсли;
		
		Запись.ПодписьДляНовыхСообщенийПростойТекст = НовоеСообщениеФорматированныйДокумент.ПолучитьТекст();
		НовоеСообщениеФорматированныйДокумент.Удалить();
		
	КонецЕсли;
	
	УправлениеДоступностью();
	
КонецПроцедуры

&НаКлиенте
Процедура ФорматПодписиПриОтветеПересылкеПриИзменении(Элемент)
	
	Если Запись.ФорматПодписиПриОтветеПересылке = ПредопределенноеЗначение(
			"Перечисление.СпособыРедактированияЭлектронныхПисем.HTML") И
		Элементы.СтраницыПодписьПриОтветеПересылке.ТекущаяСтраница = Элементы.СтраницаПриОтветеПересылкеФорматированныйДокумент Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если Запись.ФорматПодписиПриОтветеПересылке = ПредопределенноеЗначение(
			"Перечисление.СпособыРедактированияЭлектронныхПисем.ОбычныйТекст") И
		Элементы.СтраницыПодписьПриОтветеПересылке.ТекущаяСтраница = Элементы.СтраницаПриОтветеПересылкеПростойТекст Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если Запись.ФорматПодписиПриОтветеПересылке = ПредопределенноеЗначение(
			"Перечисление.СпособыРедактированияЭлектронныхПисем.HTML") Тогда
		
		Если Не ПустаяСтрока(Запись.ПодписьПриОтветеПересылкеПростойТекст) Тогда
			ПриОтветеПересылкеФорматированныйДокумент.Удалить();
			ПриОтветеПересылкеФорматированныйДокумент.Добавить(Запись.ПодписьПриОтветеПересылкеПростойТекст);
		КонецЕсли;
		
	Иначе
		
		Если Не ВзаимодействияКлиент.ПриИзмененииФорматаСообщенияНаОбычныйТекст() Тогда
			
			Запись.ФорматПодписиПриОтветеПересылке = ПредопределенноеЗначение(
				"Перечисление.СпособыРедактированияЭлектронныхПисем.HTML");
				
			Возврат;
			
		КонецЕсли;
		
		Запись.ПодписьПриОтветеПересылкеПростойТекст = ПриОтветеПересылкеФорматированныйДокумент.ПолучитьТекст();
		ПриОтветеПересылкеФорматированныйДокумент.Удалить();
		
	КонецЕсли;
	
	УправлениеДоступностью();
	
КонецПроцедуры

&НаКлиенте
Процедура ПерсональнаяУчетнаяЗаписьПриИзменении(Элемент)
	
	Если Запись.ПерсональнаяУчетнаяЗапись Тогда
		ПользовательУчетнойЗаписи = Запись.ОтветственныйЗаВедениеПапок;
	Иначе
		Запись.ОтветственныйЗаВедениеПапок = ПользовательУчетнойЗаписи;
		Запись.ОтветственныйЗаОбработкуПисем =ПользовательУчетнойЗаписи;
	КонецЕсли;
	
	УправлениеДоступностью();
	
КонецПроцедуры 

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура УправлениеДоступностью()

	Если Запись.ФорматПодписиДляНовыхСообщений = ПредопределенноеЗначение(
		"Перечисление.СпособыРедактированияЭлектронныхПисем.HTML") Тогда
		
		Элементы.СтраницыПодписьДляНовыхСообщений.ТекущаяСтраница =
			Элементы.СтраницаНовоеСообщениеФорматированныйТекст;
		Элементы.НовоеСообщениеФорматированныйДокумент.Доступность =
			Запись.ВключатьПодписьДляНовыхСообщений;
		
	Иначе
		
		Элементы.СтраницыПодписьДляНовыхСообщений.ТекущаяСтраница =
			Элементы.СтраницаНовоеСообщениеПростойТекст;
		Элементы.ПодписьДляНовыхСообщенийПростойТекст.Доступность =
			Запись.ВключатьПодписьДляНовыхСообщений;
		
	КонецЕсли;
	
	Если Запись.ФорматПодписиПриОтветеПересылке = ПредопределенноеЗначение(
			"Перечисление.СпособыРедактированияЭлектронныхПисем.HTML") Тогда
		
		Элементы.СтраницыПодписьПриОтветеПересылке.ТекущаяСтраница = 
			Элементы.СтраницаПриОтветеПересылкеФорматированныйДокумент;
		Элементы.ПриОтветеПересылкеФорматированныйДокумент.Доступность = Запись.ВключатьПодписьПриОтветеПересылке;
		
	Иначе
		
		Элементы.СтраницыПодписьПриОтветеПересылке.ТекущаяСтраница = Элементы.СтраницаПриОтветеПересылкеПростойТекст;
		Элементы.ПодписьПриОтветеПересылкеПростойТекст.Доступность = Запись.ВключатьПодписьПриОтветеПересылке;
		
	КонецЕсли;
	
	Если Запись.ПерсональнаяУчетнаяЗапись Тогда
	
		Элементы.СтраницыОтветственный.ТекущаяСтраница = Элементы.СтраницаПерсональная;
		
	Иначе
		
		Элементы.СтраницыОтветственный.ТекущаяСтраница = Элементы.СтраницаКорпоративная;
	
	КонецЕсли;

КонецПроцедуры


