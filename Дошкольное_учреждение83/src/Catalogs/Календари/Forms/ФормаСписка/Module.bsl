///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.РежимВыбора Тогда
		Элементы.Список.РежимВыбора = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(РезультатВыбора, ИсточникВыбора)
	
	Если ТипЗнч(РезультатВыбора) = Тип("Структура") Тогда
		СоздатьКалендарьПоШаблону(РезультатВыбора);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
		
	Если ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
		Если ИмяСобытия = "ОбновитьПослеДобавления" Тогда
			Элементы.Список.Обновить();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура СоздатьПоШаблону(Команда)
	
	СоздатьКалендарь();

КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Группа Тогда
		Возврат;
	КонецЕсли;
	
	Если НастройкаНовогоКалендаряПоУмолчанию() <> Неопределено Тогда
		// Календарь по единственному шаблону уже создан, 
		// не нужно предлагать сделать это повторно
		Возврат;
	КонецЕсли;
	
	Текст = НСтр("ru = 'Есть возможность создать календарь по шаблону, на основе данных производственного календаря.
						|Создать по шаблону?'");
	Результат = Вопрос(Текст, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	Если Результат = КодВозвратаДиалога.Да Тогда
		Отказ = Истина;
		СоздатьКалендарь();
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура СоздатьКалендарь()
	
	НастройкаНовогоКалендаря = НастройкаНовогоКалендаряПоУмолчанию();
	
	Если НастройкаНовогоКалендаря <> Неопределено Тогда
		СоздатьКалендарьПоШаблону(НастройкаНовогоКалендаря);
	Иначе
		// Настройка неоднозначна, нужно уточнить ее у пользователя в форме мастера
		ОткрытьФорму("Справочник.Календари.Форма.НастройкаНовогоКалендаря", , ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКалендарьПоШаблону(ДанныеШаблона)
	
	Если ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
		СтандартныеПодсистемыВызовСервераПереопределяемый.ПодобратьПоставляемыеДанныеИзКлассификатора(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеШаблона.ПроизводственныйКалендарь));
		Оповестить("ОбновитьПослеДобавления");
	Иначе
		ОткрытьФорму("Справочник.Календари.ФормаОбъекта", ДанныеШаблона, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НастройкаНовогоКалендаряПоУмолчанию()
	
	// Если есть всего лишь один актуальный производственный календарь, 
	// и используется режим разделения данных, в котором временно не предусмотрено 
	// создание нескольких элементов на основании одного общего,
	// то настройку можно определить однозначно
	
	СписокПроизводственныхКалендарей = Справочники.ПроизводственныеКалендари.СписокПроизводственныхКалендарей();
	
	Если СписокПроизводственныхКалендарей.Количество() = 1 
		И ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
		Возврат Новый Структура("ПроизводственныйКалендарь, ВидКалендаря", 
			СписокПроизводственныхКалендарей[0],
			Перечисления.ВидыКалендарей.Пятидневка);
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции
