
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
    // Устанавливаем признак достпности печати по-комплектно
    ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
    Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Карточка") Тогда

        // Формируем табличный документ и добавляем его в коллекцию печатных форм
        УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
            "Карточка", "Карточка внутреннего документа", ПечатьКарточку(МассивОбъектов, ОбъектыПечати));

	КонецЕсли;
		
КонецПроцедуры

Функция ПечатьКарточку(МассивОбъектов, ОбъектыПечати)
	
	// Создаем табличный документ и устанавливаем имя параметров печати
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПараметрыПечати_КарточкаВнутреннегоДокумента";
	
	// Получаем запросом необходимые данные
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВнутреннийДокумент.ДатаРегистрации КАК ДатаРегистрации,
	|	ВнутреннийДокумент.ВидДокумента КАК ВидДокумента,
	|	ВнутреннийДокумент.Дело КАК Дело,
	|	ВнутреннийДокумент.Зарегистрировал КАК Зарегистрировал,
	|	ВнутреннийДокумент.Заголовок КАК Наименование,
	|	ВнутреннийДокумент.РегистрационныйНомер КАК РегистрационныйНомер,
	|	ВнутреннийДокумент.Папка КАК Папка,
	|	ВнутреннийДокумент.СрокИсполнения КАК ПлановыйСрокИсполнения,
	|	ВнутреннийДокумент.Подготовил КАК Подготовил,
	|	ВнутреннийДокумент.Содержание КАК Содержание,
	|	ВнутреннийДокумент.КоличествоЛистов КАК КоличествоЛистов,
	|	ВнутреннийДокумент.КоличествоПриложений КАК КоличествоПриложений,
	|	ВнутреннийДокумент.ЛистовВПриложениях КАК ЛистовВПриложениях,
	|	ВнутреннийДокумент.Подразделение КАК Подразделение,
	|	ВнутреннийДокумент.Утвердил КАК Утвердил,
	|	ВнутреннийДокумент.Представление КАК Представление,
	|	ВнутреннийДокумент.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(СостоянияДокументовУтверждение.Период, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаУтверждения,
	|	ЕСТЬNULL(СостоянияДокументовИсполнен.Период, ДАТАВРЕМЯ(1, 1, 1)) КАК ФактическийСрокИсполнения,
	|	ВЫБОР
	|		КОГДА СостоянияДокументовИсполнен.Период ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Исполнен
	|ИЗ
	|	Справочник.ВнутренниеДокументы КАК ВнутреннийДокумент
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			СостоянияДокументов.Документ КАК Документ,
	|			МАКСИМУМ(СостоянияДокументов.Период) КАК Период
	|		ИЗ
	|			РегистрСведений.СостоянияДокументов КАК СостоянияДокументов
	|		ГДЕ
	|			СостоянияДокументов.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияДОкументов.Утвержден)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			СостоянияДокументов.Документ) КАК СостоянияДокументовУтверждение
	|		ПО (СостоянияДокументовУтверждение.Документ = ВнутреннийДокумент.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			СостоянияДокументов.Документ КАК Документ,
	|			МАКСИМУМ(СостоянияДокументов.Период) КАК Период
	|		ИЗ
	|			РегистрСведений.СостоянияДокументов КАК СостоянияДокументов
	|		ГДЕ
	|			СостоянияДокументов.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияДОкументов.Исполнен)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			СостоянияДокументов.Документ) КАК СостоянияДокументовИсполнен
	|		ПО (СостоянияДокументовИсполнен.Документ = ВнутреннийДокумент.Ссылка)
	|ГДЕ
	|	ВнутреннийДокумент.Ссылка В(&МассивОбъектов)";

	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Шапка = Запрос.Выполнить().Выбрать();
	
	Макет = Справочники.ВнутренниеДокументы.ПолучитьМакет("Карточка");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	
	ПервыйДокумент = Истина;
	Пока Шапка.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		// Запомним номер строки с которой начали выводить текущий документ
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		
		ОбластьЗаголовок.Параметры.Заполнить(Шапка);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);

		ОбластьШапка.Параметры.Заполнить(Шапка);
		
		Если ЗначениеЗаполнено(Шапка.КоличествоЛистов) И ЗначениеЗаполнено(Шапка.КоличествоПриложений) И ЗначениеЗаполнено(Шапка.ЛистовВПриложениях) Тогда
			ОбластьШапка.Параметры.Состав = "Листов " + Строка(Шапка.КоличествоЛистов) + ", приложений " + Строка(Шапка.КоличествоПриложений) + ", листов в приложениях " + Строка(Шапка.ЛистовВПриложениях);
		ИначеЕсли ЗначениеЗаполнено(Шапка.КоличествоЛистов) И ЗначениеЗаполнено(Шапка.КоличествоПриложений) Тогда 
			ОбластьШапка.Параметры.Состав = "Листов " + Строка(Шапка.КоличествоЛистов) + ", приложений " + Строка(Шапка.КоличествоПриложений);
		ИначеЕсли ЗначениеЗаполнено(Шапка.КоличествоЛистов) Тогда 
			ОбластьШапка.Параметры.Состав = "Листов " + Строка(Шапка.КоличествоЛистов);	
		КонецЕсли;	
		
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		
		// В табличном документе зададим имя области в которую был 
		// выведен объект. Нужно для возможности печати по-комплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

