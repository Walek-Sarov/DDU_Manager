////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчик события ПриОткрытии формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Макет = Справочники.удуСтепениЗнанияЯзыка.ПолучитьМакет("КлассификаторСтепеньЗнанияЯзыка");

	Макет.Параметры.Расшифровка = Истина; // чтобы работала расшифровка

	ТабличныйДокумент = Классификатор;
	
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(Макет);

	ТабличныйДокумент.ФиксацияСверху      = 3;

	ТабличныйДокумент.ОтображатьЗаголовки = Ложь;
	ТабличныйДокумент.ОтображатьСетку     = Ложь;
	ТабличныйДокумент.ТолькоПросмотр      = Истина;
	
КонецПроцедуры

&НаСервере
Функция НайтиЭлемент(Наименование)
	
	 Возврат Справочники.удуСтепениЗнанияЯзыка.НайтиПоНаименованию(Наименование);
	 
КонецФункции

&НаКлиенте
Процедура КлассификаторОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	// Получение значений полей выбранной строки.
	ТабличныйДокумент = Классификатор;
	ТекущаяОбласть    = ТабличныйДокумент.ТекущаяОбласть;

	ОбластьКод          = ТабличныйДокумент.Области.Код;
	ОбластьНаименование = ТабличныйДокумент.Области.Наименование;
	
	Если ТекущаяОбласть.Низ = ТекущаяОбласть.Верх Тогда
		
		Код          = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьКод.         Лево, ТекущаяОбласть.Низ, ОбластьКод.         Право).Текст;
		Наименование = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименование.Лево, ТекущаяОбласть.Низ, ОбластьНаименование.Право).Текст;
		
		// Проверка наличия выбранного элемента.
		Ссылка=НайтиЭлемент(Наименование);

		Если НЕ Ссылка.Пустая() Тогда
			
			Вопрос = "В справочнике ""Степени знания языков"" уже существует элемент с наименованием """ + Наименование + """! Открыть существующий?";
			Ответ  = Вопрос(Вопрос, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Отмена, );
			
			Если      Ответ = КодВозвратаДиалога.Да Тогда
				
				ФормаЭлемента=ПолучитьФорму("Справочник.удуСтепениЗнанияЯзыка.Форма.ФормаЭлемента",Новый Структура("Ключ",Ссылка),ВладелецФормы, );
				ФормаЭлемента.Открыть();
				Возврат;

			ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
				Возврат;
			КонецЕсли;
			
		КонецЕсли;

		// Создание нового элемента справочника.
		ФормаНовогоЭлемента = ПолучитьФорму("Справочник.удуСтепениЗнанияЯзыка.Форма.ФормаЭлемента", ,ВладелецФормы, );
		ФормаНовогоЭлемента.Объект.Наименование = Наименование;
		ФормаНовогоЭлемента.Объект.Код	      	 = Код;
		ФормаНовогоЭлемента.Открыть();
	КонецЕсли;	
	
	
	
КонецПроцедуры
