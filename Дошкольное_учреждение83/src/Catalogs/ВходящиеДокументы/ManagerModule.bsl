
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
    // Устанавливаем признак достпности печати по-комплектно
    ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
    Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Карточка") Тогда

        // Формируем табличный документ и добавляем его в коллекцию печатных форм
        УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
            "Карточка", "Карточка внутреннего документа", ПечатьКарточку(МассивОбъектов, ОбъектыПечати));

	КонецЕсли;
		
КонецПроцедуры

Функция ПечатьКарточку(МассивОбъектов, ОбъектыПечати)
	
	// Создаем табличный документ и устанавливаем имя параметров печати
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПараметрыПечати_КарточкаВходящегоДокумента";
	
	// Получаем запросом необходимые данные
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВходящийДокумент.АвторРезолюции КАК АвторРезолюции,
	|	ВходящийДокумент.Адресат КАК Адресат,
	|	ВходящийДокумент.ВидДокумента КАК ВидДокумента,
	|	ВходящийДокумент.ВОтветНа.ДатаРегистрации КАК ВОтветНаДата,
	|	ВходящийДокумент.ВОтветНа.РегистрационныйНомер КАК ВОтветНаНомер,
	|	ВходящийДокумент.ДатаРегистрации КАК ДатаРегистрации,
	|	ВходящийДокумент.ДатаРезолюции КАК ДатаРезолюции,
	|	ВходящийДокумент.Дело КАК Дело,
	|	ВходящийДокумент.Зарегистрировал КАК Зарегистрировал,
	|	ВходящийДокумент.ИсходящаяДата КАК ИсходящаяДата,
	|	ВходящийДокумент.ИсходящийНомер КАК ИсходящийНомер,
	|	ВходящийДокумент.Заголовок КАК Наименование,
	|	ВходящийДокумент.РегистрационныйНомер КАК РегистрационныйНомер,
	|	ВходящийДокумент.Отправитель КАК Отправитель,
	|	ВходящийДокумент.ОтправленОтвет.ДатаРегистрации КАК ОтправленОтветДата,
	|	ВходящийДокумент.ОтправленОтвет.РегистрационныйНомер КАК ОтправленОтветНомер,
	|	ВходящийДокумент.СрокИсполнения КАК ПлановыйСрокИсполнения,
	|	ВходящийДокумент.Подписал КАК Подписал,
	|	ВходящийДокумент.Резолюция КАК Резолюция,
	|	ВходящийДокумент.Содержание КАК Содержание,
	|	ВходящийДокумент.КоличествоЛистов КАК КоличествоЛистов,
	|	ВходящийДокумент.КоличествоПриложений КАК КоличествоПриложений,
	|	ВходящийДокумент.ЛистовВПриложениях КАК ЛистовВПриложениях,
	|	ВходящийДокумент.СпособПолучения,
	|	ВходящийДокумент.Подразделение КАК ПодразделениеАдресата,
	|	ВходящийДокумент.Представление КАК Представление,
	|	ВходящийДокумент.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(СостоянияДокументовИсполнен.Период, ДАТАВРЕМЯ(1, 1, 1)) КАК ФактическийСрокИсполнения,
	|	ВЫБОР
	|		КОГДА СостоянияДокументовИсполнен.Период ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Исполнен
	|ИЗ
	|	Справочник.ВходящиеДокументы КАК ВходящийДокумент
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			СостоянияДокументов.Документ КАК Документ,
	|			МАКСИМУМ(СостоянияДокументов.Период) КАК Период
	|		ИЗ
	|			РегистрСведений.СостоянияДокументов КАК СостоянияДокументов
	|		ГДЕ
	|			СостоянияДокументов.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияДОкументов.Исполнен)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			СостоянияДокументов.Документ) КАК СостоянияДокументовИсполнен
	|		ПО (СостоянияДокументовИсполнен.Документ = ВходящийДокумент.Ссылка)
	|ГДЕ
	|	ВходящийДокумент.Ссылка В(&МассивОбъектов)";

	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Шапка = Запрос.Выполнить().Выбрать();
	
	Макет = Справочники.ВходящиеДокументы.ПолучитьМакет("Карточка");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	
	ПервыйДокумент = Истина;
	Пока Шапка.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		// Запомним номер строки с которой начали выводить текущий документ
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		
		ОбластьЗаголовок.Параметры.Заполнить(Шапка);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);

		ОбластьШапка.Параметры.Заполнить(Шапка);
		
		Если ЗначениеЗаполнено(Шапка.ОтправленОтветНомер) Или ЗначениеЗаполнено(Шапка.ОтправленОтветДата) Тогда
			ОбластьШапка.Параметры.ОтправленОтвет = "№ " + Шапка.ОтправленОтветНомер + " от " + Формат(Шапка.ОтправленОтветДата, "ДФ=dd.MM.yyyy");
		КонецЕсли;
		Если ЗначениеЗаполнено(Шапка.ВОтветНаНомер) Или ЗначениеЗаполнено(Шапка.ВОтветНаДата) Тогда
			ОбластьШапка.Параметры.ВОтветНаДокумент = "№ " + Шапка.ВОтветНаНомер + " от " + Формат(Шапка.ВОтветНаДата, "ДФ=dd.MM.yyyy");
		КонецЕсли;
		Если ЗначениеЗаполнено(Шапка.ИсходящийНомер) Или ЗначениеЗаполнено(Шапка.ИсходящаяДата) Тогда
			ОбластьШапка.Параметры.ИсходящийДокумент = "№ " + Шапка.ИсходящийНомер + " от " + Формат(Шапка.ИсходящаяДата, "ДФ=dd.MM.yyyy");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Шапка.КоличествоЛистов) И ЗначениеЗаполнено(Шапка.КоличествоПриложений) И ЗначениеЗаполнено(Шапка.ЛистовВПриложениях) Тогда
			ОбластьШапка.Параметры.Состав = "Листов " + Строка(Шапка.КоличествоЛистов) + ", приложений " + Строка(Шапка.КоличествоПриложений) + ", листов в приложениях " + Строка(Шапка.ЛистовВПриложениях);
		ИначеЕсли ЗначениеЗаполнено(Шапка.КоличествоЛистов) И ЗначениеЗаполнено(Шапка.КоличествоПриложений) Тогда 
			ОбластьШапка.Параметры.Состав = "Листов " + Строка(Шапка.КоличествоЛистов) + ", приложений " + Строка(Шапка.КоличествоПриложений);
		ИначеЕсли ЗначениеЗаполнено(Шапка.КоличествоЛистов) Тогда 
			ОбластьШапка.Параметры.Состав = "Листов " + Строка(Шапка.КоличествоЛистов);	
		КонецЕсли;	
		
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		
		// В табличном документе зададим имя области в которую был 
		// выведен объект. Нужно для возможности печати по-комплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции
