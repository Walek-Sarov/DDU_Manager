
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЭтоГруппа Тогда 
		Возврат;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(Раздел) И (Год <> Раздел.Год) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(	
			НСтр("ru = 'Год элемента номенклатуры дел не совпадает с годом раздела'"),
			ЭтотОбъект,
			"Год",,
			Отказ);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда 
		Если ЗначениеЗаполнено(Раздел) И Организация <> Раздел.Организация Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(	
				НСтр("ru = 'Организация элемента номенклатуры дел не совпадает с организацией раздела'"),
				ЭтотОбъект,
				"Организация",,
				Отказ);
		КонецЕсли;
	КонецЕсли;	
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НоменклатураДел.Ссылка
	|ИЗ
	|	Справочник.НоменклатураДел КАК НоменклатураДел
	|ГДЕ
	|	НоменклатураДел.Индекс = &Индекс
	|	И НоменклатураДел.Год = &Год
	|	И НоменклатураДел.Ссылка <> &Ссылка";
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда
		Запрос.Текст = Запрос.Текст + " И (Организация = &Организация) ";
		Запрос.УстановитьПараметр("Организация", Организация);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Индекс", Индекс);
	Запрос.УстановитьПараметр("Год", Год);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(	
			НСтр("ru = 'Элемент номенклатуры дел с указанным индексом уже зарегистрирован'"),
			ЭтотОбъект,
			"Индекс",,
			Отказ);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЭтоНовый() Тогда 
		
		Запрос = новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Дела.Ссылка
		|ИЗ
		|	Справочник.ДелаХраненияДокументов КАК Дела
		|ГДЕ
		|	Дела.НоменклатураДел = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ДелоОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ЗаблокироватьДанныеДляРедактирования(ДелоОбъект.Ссылка);
			ДелоОбъект.Записать();
		КонецЦикла;	
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Наименование = Делопроизводство.НаименованиеНоменклатурыДел(ЭтотОбъект);
	
КонецПроцедуры
