
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВременноеДо10 = Перечисления.КатегорииДел.ВременноеДо10;
	ВременноеСвыше10 = Перечисления.КатегорииДел.ВременноеСвыше10;
	Постоянное = Перечисления.КатегорииДел.Постоянное;
	
	Если Объект.Ссылка.Пустая() Тогда
		Если Не ЗначениеЗаполнено(Объект.Год) Тогда
			Объект.Год = Год(ТекущаяДата());
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Объект.СрокХранения) Тогда 
			Объект.СрокХранения = 0;
		КонецЕсли;	
		
		Если Не ЗначениеЗаполнено(Объект.Организация) Тогда 
			Объект.Организация = РаботаСОрганизациями.ПолучитьОрганизациюПоУмолчанию();
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаполнитьПодписьЛет();
	
КонецПроцедуры

&НаКлиенте
Процедура СрокХраненияНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	СписокВыбора = Новый СписокЗначений;
	
	Если Объект.КатегорияДела = ВременноеДо10 Тогда
		СписокВыбора.Добавить("1 год");
		СписокВыбора.Добавить("3 года");
		СписокВыбора.Добавить("5 лет");
		СписокВыбора.Добавить("10 лет");
		СписокВыбора.Добавить("До минования надобности");
		СписокВыбора.Добавить("До замены новыми");
		СписокВыбора.Добавить("<Другое>");
		
	ИначеЕсли Объект.КатегорияДела = ВременноеСвыше10 Тогда
		СписокВыбора.Добавить("15 лет");
		СписокВыбора.Добавить("50 лет");
		СписокВыбора.Добавить("75 лет");
		СписокВыбора.Добавить("До замены новыми");
		СписокВыбора.Добавить("<Другое>");
		
	ИначеЕсли Объект.КатегорияДела = Постоянное Тогда
		СписокВыбора.Добавить("Постоянно");
		
	КонецЕсли;	
	
	Элемент.СписокВыбора.ЗагрузитьЗначения(СписокВыбора.ВыгрузитьЗначения());
	
КонецПроцедуры

&НаКлиенте
Процедура СрокХраненияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = "<Другое>" Тогда 
		СтандартнаяОбработка = Ложь;
		Объект.СрокХранения = "";
		
	ИначеЕсли ВыбранноеЗначение = "1 год" Или ВыбранноеЗначение = "3 года" 
		Или ВыбранноеЗначение = "5 лет" Или ВыбранноеЗначение = "10 лет" 
		Или ВыбранноеЗначение = "15 лет" Или ВыбранноеЗначение = "50 лет" 
		Или ВыбранноеЗначение = "75 лет" Тогда 
		
		СтандартнаяОбработка = Ложь;
		Объект.СрокХранения = Число(Лев(ВыбранноеЗначение, 2));
		ЗаполнитьПодписьЛет();
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура КатегорияДелаПриИзменении(Элемент)
	
	Если Объект.КатегорияДела = ВременноеДо10 Тогда
		Объект.СрокХранения = "";
		
	ИначеЕсли Объект.КатегорияДела = ВременноеСвыше10 Тогда
		Объект.СрокХранения = "";	
		
	ИначеЕсли Объект.КатегорияДела = Постоянное Тогда
		Объект.СрокХранения = "Постоянно";
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура РазделНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура("ПоказыватьДелаЗа, Организация", Объект.Год, Объект.Организация);
	ОткрытьФорму("Справочник.РазделыНоменклатурыДел.ФормаВыбора", ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СрокХраненияПриИзменении(Элемент)
	
	ЗаполнитьПодписьЛет();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПодписьЛет()
	
	Если ТипЗнч(Объект.СрокХранения) = Тип("Число") Тогда 
		Элементы.ДекорацияЛет.Заголовок = "(" + ДелопроизводствоКлиентСервер.ПодписьЛет(Объект.СрокХранения) + ")";
	Иначе
		Элементы.ДекорацияЛет.Заголовок = "";
	КонецЕсли;	
	
КонецПроцедуры	
