
&НаКлиенте
Перем ПерваяАктивизация;

//////////////////////////////////////////////////////////////////////////////// 
// ПРОЦЕДУРЫ И ФУНКЦИИ 
// 

&НаСервереБезКонтекста
Процедура ПечатьНоменклатурыДел(ТабДок, ПараметрыПечати)
	
	Справочники.НоменклатураДел.ПечатьНоменклатурыДел(ТабДок, ПараметрыПечати);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПечатьИтоговойЗаписи(ТабДок, ПараметрыПечати)
	
	Справочники.НоменклатураДел.ПечатьИтоговойЗаписи(ТабДок, ПараметрыПечати);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОжидания()
	
	Если Элементы.Разделы.ТекущаяСтрока <> Неопределено Тогда 
		Список.Параметры.УстановитьЗначениеПараметра("Раздел", Элементы.Разделы.ТекущаяСтрока);
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборСписка(ПараметрыОтбора)

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда
		ПараметрыОтбора["Организация"] = Неопределено;
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("Год", ПараметрыОтбора["ПоказыватьДелаЗа"]);
	Разделы.Параметры.УстановитьЗначениеПараметра("Год", ПараметрыОтбора["ПоказыватьДелаЗа"]);
	
	Если ЗначениеЗаполнено(ПараметрыОтбора["Организация"]) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьОтборУСпискаНаРавенство(Список.Отбор,
			Новый ПолеКомпоновкиДанных("Организация"),
			ПараметрыОтбора["Организация"]);
			
		ОбщегоНазначенияКлиентСервер.УстановитьОтборУСпискаНаРавенство(Разделы.Отбор,
			Новый ПолеКомпоновкиДанных("Организация"),
			ПараметрыОтбора["Организация"]);	
	Иначе
		ОбщегоНазначенияКлиентСервер.УдалитьОтборУСписка(Список.Отбор,
			Новый ПолеКомпоновкиДанных("Организация"));
			
		ОбщегоНазначенияКлиентСервер.УдалитьОтборУСписка(Разделы.Отбор,
			Новый ПолеКомпоновкиДанных("Организация"));	
	КонецЕсли;	
	
КонецПроцедуры	

&НаСервере
Процедура УстановитьОтбор()

	ПараметрыОтбора = Новый Соответствие();
	ПараметрыОтбора.Вставить("ПоказыватьДелаЗа", ПоказыватьДелаЗа);	
	ПараметрыОтбора.Вставить("Организация",      Организация);	
	УстановитьОтборСписка(ПараметрыОтбора);
	
КонецПроцедуры	

//////////////////////////////////////////////////////////////////////////////// 
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
// 

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПапкаПриОткрытии = Справочники.РазделыНоменклатурыДел.ПустаяСсылка();
	Список.Параметры.УстановитьЗначениеПараметра("Раздел", ПапкаПриОткрытии);
	
	ПоказыватьДелаЗа = Год(ТекущаяДата());
	Организация = РаботаСОрганизациями.ПолучитьОрганизациюПоУмолчанию();
	УстановитьОтбор();
	
	ИспользоватьУчетПоОрганизациям = ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям");
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	УстановитьОтборСписка(Настройки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьРазделыНоменклатурыДел" И Параметр = ПоказыватьДелаЗа Тогда 
		Элементы.Разделы.Обновить();
	КонецЕсли;	
	
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////// 
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
// 

&НаКлиенте
Процедура ПечатьНоменклатурыДелВыполнить(Команда)
	
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("Год", ПоказыватьДелаЗа);
	ПараметрыПечати.Вставить("Организация", Организация);
	
	ТабДок = Новый ТабличныйДокумент;
	ПечатьНоменклатурыДел(ТабДок, ПараметрыПечати);

	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_НоменклатураДел";
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.Показать("Номенклатура дел");
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьИтоговойЗаписиВыполнить(Команда)
	
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("Год", ПоказыватьДелаЗа);
	ПараметрыПечати.Вставить("Организация", Организация);
	
	ТабДок = Новый ТабличныйДокумент;
	ПечатьИтоговойЗаписи(ТабДок, ПараметрыПечати);

	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ИтоговаяЗапись";
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.Показать("Итоговая запись о категориях и количестве дел");
	
КонецПроцедуры

&НаКлиенте
Процедура Скопировать(Команда)
	
	ПараметрыФормы = Новый Структура("Год, Организация", ПоказыватьДелаЗа, Организация);
	ОткрытьФорму("Справочник.НоменклатураДел.Форма.ФормаКопирования", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура РазделыПриАктивизацииСтроки(Элемент)
	
	Если ПерваяАктивизация = Истина Или ПерваяАктивизация = Неопределено Тогда
		ПерваяАктивизация = Ложь;
		Возврат;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ОбработкаОжидания", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЭлемент(Команда)
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Раздел", Элементы.Разделы.ТекущаяСтрока);
	ЗначенияЗаполнения.Вставить("Год", ПоказыватьДелаЗа);
	Если ИспользоватьУчетПоОрганизациям Тогда 
		ЗначенияЗаполнения.Вставить("Организация", Организация);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	Открытьформу("Справочник.НоменклатураДел.ФормаОбъекта", ПараметрыФормы, Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьРаздел(Команда)
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Родитель", Элементы.Разделы.ТекущаяСтрока);
	ЗначенияЗаполнения.Вставить("Год", ПоказыватьДелаЗа);
	Если ИспользоватьУчетПоОрганизациям Тогда 
		ЗначенияЗаполнения.Вставить("Организация", Организация);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ОткрытьФорму("Справочник.РазделыНоменклатурыДел.ФормаОбъекта", ПараметрыФормы, Элементы.Разделы);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРазделы(Команда)
	
	ПараметрыФормы = Новый Структура("Год, Организация", ПоказыватьДелаЗа, Организация);
	ОткрытьФорму("Справочник.НоменклатураДел.Форма.ФормаЗаполненияРазделов", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	УстановитьОтбор();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьДелаЗаПриИзменении(Элемент)
	
	УстановитьОтбор();
	
КонецПроцедуры
