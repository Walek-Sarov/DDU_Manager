
Процедура ПечатьНоменклатурыДел(ТабДок, ПараметрыПечати) Экспорт
	
	Макет = Справочники.НоменклатураДел.ПолучитьМакет("НоменклатураДел");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НоменклатураДел.Индекс КАК Индекс,
	|	НоменклатураДел.ПолноеНаименование КАК Наименование,
	|	НоменклатураДел.Раздел КАК Раздел,
	|	НоменклатураДел.ОтметкаЭПК КАК ОтметкаЭПК,
	|	НоменклатураДел.НомераСтатей КАК НомераСтатей,
	|	НоменклатураДел.СрокХранения КАК СрокХранения,
	|	НоменклатураДел.Примечание КАК Примечание
	|ИЗ
	|	Справочник.НоменклатураДел КАК НоменклатураДел
	|ГДЕ
	|	НоменклатураДел.Год = &Год
	|	И (НоменклатураДел.Организация = &Организация
	|			ИЛИ &ИспользоватьУчетПоОрганизациям = ЛОЖЬ)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Индекс
	|ИТОГИ ПО
	|	Раздел ИЕРАРХИЯ";
	Запрос.УстановитьПараметр("Год", ПараметрыПечати.Год);
	Запрос.УстановитьПараметр("Организация", ПараметрыПечати.Организация);
	Запрос.УстановитьПараметр("ИспользоватьУчетПоОрганизациям", ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям"));
	
	ВыборкаРазделы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка 	 = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока 	 = Макет.ПолучитьОбласть("Строка");
	ОбластьРаздел 	 = Макет.ПолучитьОбласть("Раздел");
	ТабДок.Очистить();

	// заголовок
	ОбластьЗаголовок.Параметры.Год = Формат(ПараметрыПечати.Год, "ЧГ=");
	ОбластьЗаголовок.Параметры.НаименованиеПредприятия = РаботаСОрганизациями.ПолучитьНаименованиеОрганизации(ПараметрыПечати.Организация);
	ОбластьЗаголовок.Параметры.Руководитель = РаботаСОрганизациями.ПолучитьОтветсвенноеЛицо("Руководитель", ПараметрыПечати.Организация, Дата(ПараметрыПечати.Год, 1, 1));
	ТабДок.Вывести(ОбластьЗаголовок);
	
	// шапка
	ТабДок.Вывести(ОбластьШапка);
	
	// разделы
	Пока ВыборкаРазделы.Следующий() Цикл
		
		ОбластьРаздел.Параметры.Индекс = ВыборкаРазделы.Раздел.Индекс;
		ОбластьРаздел.Параметры.Наименование = ВыборкаРазделы.Раздел.Наименование;
		ТабДок.Вывести(ОбластьРаздел);
		
		// строки
		ВыборкаСтроки = ВыборкаРазделы.Выбрать();
		Пока ВыборкаСтроки.Следующий() Цикл
			ОбластьСтрока.Параметры.Заполнить(ВыборкаСтроки);
			
			НомераСтатей = ВыборкаСтроки.НомераСтатей;
			СрокХранения = ВыборкаСтроки.СрокХранения;
			ОтметкаЭПК 	 = ВыборкаСтроки.ОтметкаЭПК;
			
			Если ТипЗнч(СрокХранения) = Тип("Число") Тогда 
				ОбластьСтрока.Параметры.СрокХраненияНомераСтатей = Строка(СрокХранения) + " "
					+ ДелопроизводствоКлиентСервер.ПодписьЛет(СрокХранения) 
					+ ?(ОтметкаЭПК, " ЭПК", "") 
					+ ?(ЗначениеЗаполнено(НомераСтатей), ", " + НомераСтатей, "");
			Иначе
				ОбластьСтрока.Параметры.СрокХраненияНомераСтатей = СрокХранения 
					+ ?(ОтметкаЭПК, " ЭПК", "")
					+ ?(ЗначениеЗаполнено(НомераСтатей), ", " + НомераСтатей, "");
			КонецЕсли;
			
			ТабДок.Вывести(ОбластьСтрока);
		КонецЦикла;	
	КонецЦикла;
	
КонецПроцедуры

Процедура ПечатьИтоговойЗаписи(ТабДок, ПараметрыПечати) Экспорт
	
	Макет = Справочники.НоменклатураДел.ПолучитьМакет("ИтоговаяЗапись");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Дела.НоменклатураДел.КатегорияДела КАК КатегорияДела,
	|	СУММА(1) КАК Всего,
	|	СУММА(ВЫБОР
	|			КОГДА Дела.НоменклатураДел.ОтметкаЭПК
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СОтметкойЭПК,
	|	СУММА(ВЫБОР
	|			КОГДА Дела.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1)
	|					И ГОД(Дела.ДатаОкончания) <> &Год
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Переходящих
	|ИЗ
	|	Справочник.ДелаХраненияДокументов КАК Дела
	|ГДЕ
	|	Дела.НоменклатураДел.Год = &Год
	|	И (Дела.НоменклатураДел.Организация = &Организация
	|			ИЛИ &ИспользоватьУчетПоОрганизациям = ЛОЖЬ)
	|
	|СГРУППИРОВАТЬ ПО
	|	Дела.НоменклатураДел.КатегорияДела";
	Запрос.УстановитьПараметр("Год", ПараметрыПечати.Год);
	Запрос.УстановитьПараметр("Организация", ПараметрыПечати.Организация);
	Запрос.УстановитьПараметр("ИспользоватьУчетПоОрганизациям", ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям"));
	
	Выборка = Запрос.Выполнить().Выгрузить();

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка 	 = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока 	 = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал 	 = Макет.ПолучитьОбласть("Подвал");
	ТабДок.Очистить();

	// заголовок
	ОбластьЗаголовок.Параметры.Год = Формат(ПараметрыПечати.Год, "ЧГ=");
	ТабДок.Вывести(ОбластьЗаголовок);
	
	// шапка
	ТабДок.Вывести(ОбластьШапка);
	
	// строки
	ОбластьСтрока.Параметры.КатегорияДелаСтр = "Постоянного";
	НайденнаяСтрока = Выборка.Найти(Перечисления.КатегорииДел.Постоянное, "КатегорияДела");
	Если НайденнаяСтрока <> Неопределено Тогда 
		ОбластьСтрока.Параметры.Заполнить(НайденнаяСтрока);
	КонецЕсли;	
	ТабДок.Вывести(ОбластьСтрока);
	
	ОбластьСтрока.Параметры.КатегорияДелаСтр = "Временного (свыше 10 лет)";
	НайденнаяСтрока = Выборка.Найти(Перечисления.КатегорииДел.ВременноеСвыше10, "КатегорияДела");
	Если НайденнаяСтрока <> Неопределено Тогда 
		ОбластьСтрока.Параметры.Заполнить(НайденнаяСтрока);
	КонецЕсли;	
	ТабДок.Вывести(ОбластьСтрока);
		
	ОбластьСтрока.Параметры.КатегорияДелаСтр = "Временного (до 10 лет включительно)";
	НайденнаяСтрока = Выборка.Найти(Перечисления.КатегорииДел.ВременноеДо10, "КатегорияДела");
	Если НайденнаяСтрока <> Неопределено Тогда 
		ОбластьСтрока.Параметры.Заполнить(НайденнаяСтрока);
	КонецЕсли;	
	ТабДок.Вывести(ОбластьСтрока);	
		
	// подвал
	ДатаПодписи = ТекущаяДата();
	РуководительСлужбыДОУ = РаботаСОрганизациями.ПолучитьОтветсвенноеЛицо("РуководительСлужбыДОУ", ПараметрыПечати.Организация, ДатаПодписи);
	
	ОбластьПодвал.Параметры.РуководительДОУ = РуководительСлужбыДОУ;
	ОбластьПодвал.Параметры.ДолжностьРуководителяДОУ = РаботаСПользователями.ПолучитьДолжность(РуководительСлужбыДОУ);
	ОбластьПодвал.Параметры.ДатаПодписи = ДатаПодписи;
	ТабДок.Вывести(ОбластьПодвал);
	
КонецПроцедуры
