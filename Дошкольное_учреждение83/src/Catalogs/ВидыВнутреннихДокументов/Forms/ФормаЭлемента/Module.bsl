
&НаСервере
Процедура УстановитьДоступность()
	
	НумераторДоступен = (Объект.СпособНумерации = Перечисления.СпособыНумерации.Автоматически);
	Элементы.Нумератор.Доступность = НумераторДоступен;
	Элементы.Нумератор.АвтоОтметкаНезаполненного = НумераторДоступен;
		
КонецПроцедуры	

&НаСервере
Процедура ПриИзмененииСпособаНумерации()
	
	Если (Объект.СпособНумерации = Перечисления.СпособыНумерации.Вручную) Тогда 
		Объект.Нумератор = Справочники.Нумераторы.ПустаяСсылка();
		Элементы.Нумератор.ОтметкаНезаполненного = Ложь;
	КонецЕсли;
	
	УстановитьДоступность();
	
КонецПроцедуры	

&НаСервере
Функция ИзменениеПараметровНумерации()
	
	Возврат ЗначениеЗаполнено(Объект.Ссылка) 
		И Объект.Нумератор <> Объект.Ссылка.Нумератор
		И Делопроизводство.КоличествоДокументовПоВидуДокумента(Объект.Ссылка) > 0;
	
КонецФункции	

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура СпособНумерацииПриИзменении(Элемент)
	
	ПриИзмененииСпособаНумерации();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНаборСвойств(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда
		
		ТекстВопроса = "Для перехода к набору свойств элемент необходимо записать." + Символы.ПС + "Записать?";
		Результат = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
		Если Результат <> КодВозвратаДиалога.Да Тогда 
			Возврат;
		КонецЕсли;	
		
		Если Не Записать() Тогда 
			Возврат;
		КонецЕсли;	
		
	КонецЕсли;	
	
	ОткрытьЗначение(Объект.НаборСвойств);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ИзменениеПараметровНумерации() Тогда 
		ТекстВопроса = НСТР("ru = 'Для данного вида документа имеются зарегистрированные документы.
							|Вы действительно хотите изменить параметры нумерации?'"); 
		
		Результат = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет);
		Если Результат <> КодВозвратаДиалога.Да Тогда 
			Отказ = Истина;
			Возврат;
		КонецЕсли;	
	КонецЕсли;
	
		
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ИзмененУчетПоДетям", Делопроизводство.удуИзмененПризнакУчетаПоДетям(Объект));

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.ИзмененУчетПоДетям Тогда
		Оповестить("ИзмененУчетПоДетямДляВнутреннихДокументов", Объект.Ссылка);
	КонецЕсли; 

КонецПроцедуры

			

	
		
		
