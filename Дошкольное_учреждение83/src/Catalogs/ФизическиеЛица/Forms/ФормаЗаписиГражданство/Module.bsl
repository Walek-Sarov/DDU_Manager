  &НаСервере
 Процедура ОбновитьПредставлениеЭлемента(ИмяОбновляемогоЭлемента = "Все")
	
	Если ИмяОбновляемогоЭлемента = "РасшифровкаНДФЛ" или ИмяОбновляемогоЭлемента = "Все" Тогда
		
		Если НеЯвляетсяНалоговымРезидентомРФ Тогда
			Элементы.РасшифровкаНДФЛ.Заголовок = "Физическое лицо за прошедшие 12 месяцев пробыло на территории РФ менее 183 дней. При исчислении НДФЛ с оплаты труда будет применяться ставка 30%.";
		Иначе
			Элементы.РасшифровкаНДФЛ.Заголовок = "Физическое лицо постоянно находится на территории РФ (за прошедшие 12 месяцев - более 183 дней). При исчислении НДФЛ с оплаты труда будет применяться ставка 13%";
		КонецЕсли;
		
	КонецЕсли;
	
	Если ИмяОбновляемогоЭлемента = "РасшифровкаПФР" или ИмяОбновляемогоЭлемента = "Все" Тогда
		
		Если НеИмеетПравоНаПенсию Тогда
			Элементы.РасшифровкаПФР.Заголовок = "На физическое лицо не распространяется обязательное пенсионное, медицинское и социальное страхование в РФ. Таковыми являются иностранные граждане и лица без гражданства, временно пребывающие на территории РФ.";
		Иначе
			Элементы.РасшифровкаПФР.Заголовок = "На физическое лицо распространяется обязательное пенсионное, медицинское и социальное страхование в РФ. Таковыми являются граждане РФ, а также постоянно или временно проживающие на территории РФ иностранные граждане и лица без гражданства.";
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтработатьПереключательГражданство()
	
	Если ПереключательГражданство=1 Тогда
		мСтрана = Страна;
		Страна = Справочники.СтраныМира.ПустаяСсылка();
	Иначе
		Страна = мСтрана;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключательГражданствоПриИзменении(Элемент)
	
	ОтработатьПереключательГражданство();
	
	Элементы.Страна.ТолькоПросмотр = Булево(ПереключательГражданство);
	
КонецПроцедуры

&НаКлиенте
Процедура НеЯвляетсяНалоговымРезидентомРФПриИзменении(Элемент)
	ОбновитьПредставлениеЭлемента("РасшифровкаНДФЛ");
КонецПроцедуры

&НаКлиенте
Процедура НеИмеетПравоНаПенсиюПриИзменении(Элемент)
	ОбновитьПредставлениеЭлемента("РасшифровкаПФР");
КонецПроцедуры

&НаКлиенте
Процедура История(Команда)
	
	ФормаИстории= ПолучитьФорму("РегистрСведений.удуГражданствоФизЛиц.ФормаСписка",,ЭтаФорма);
	ЭлементОтбора=ФормаИстории.Список.Отбор.элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование=Истина;
	ЭлементОтбора.ВидСравнения=ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение=ФизЛицо;
	ЭлементОтбора.ЛевоеЗначение=Новый ПолеКомпоновкиДанных("ФизЛицо");
	ФормаИстории.открыть();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтруктураДанных=ПолучитьИзВременногоХранилища(Параметры.АдресНачальныхДанных);
	ЗаполнитьЗначенияСвойств(ЭтаФорма,СтруктураДанных);
	АдресНачальныхДанных=Параметры.АдресНачальныхДанных;
	
	Если ФизЛицо.Пустая() тогда
		Элементы.ФормаКоманднаяПанель.ПодчиненныеЭлементы.История.Видимость=Ложь;
		Элементы.ФормаКоманднаяПанель.ПодчиненныеЭлементы.История.Доступность=Ложь;
	КонецЕсли;
	
	Если не ЗначениеЗаполнено(Период) тогда
		 Период=Дата(1900,01,01);
	КонецЕсли;
	
	
	// установим переключатель в форме в соответствии с выбранным гражданством
	ЛицоБезГражданства = Страна = Справочники.СтраныМира.ЛицоБезГражданства;
	Элементы.Страна.ТолькоПросмотр = ЛицоБезГражданства;
	Если ЛицоБезГражданства Тогда
		Страна = Справочники.СтраныМира.ПустаяСсылка();
		ПереключательГражданство=1;
	КонецЕсли;	
	
	ОбновитьПредставлениеЭлемента();

КонецПроцедуры

&НаСервере
Функция СравнитьДанные()
	
	Перем НачальноеЗначение, КонечноеЗначение;
	
	СтруктураДанных=ПолучитьИзВременногоХранилища(АдресНачальныхДанных);
	СтруктураДанныхНовая=Новый Структура;
	Для Каждого ЭлементСтруктуры из СтруктураДанных цикл
		СтруктураДанныхНовая.Вставить(ЭлементСтруктуры.Ключ,ЭлементСтруктуры.Значение);
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(СтруктураДанныхНовая,ЭтаФорма);
	Если не ЗначениеЗаполнено(Страна) тогда
		  СтруктураДанныхНовая.Страна=Справочники.СтраныМира.ЛицоБезГражданства;
	КонецЕсли;
	
	
	
	ПризнакИзменения=Ложь;
	МассивРеквизитов=ПолучитьРеквизиты();
	
	Для Каждого ЭлементМассива из МассивРеквизитов цикл
		СтруктураДанных.Свойство(ЭлементМассива.Имя, НачальноеЗначение); 
		СтруктураДанныхНовая.Свойство(ЭлементМассива.Имя, КонечноеЗначение);
		Если не НачальноеЗначение=КонечноеЗначение тогда
			 ПризнакИзменения=Истина;
			 Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ПризнакИзменения тогда
         Возврат СтруктураДанныхНовая;
	 Иначе 
		 Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПодтвердитьИзменения(Кнопка)
	
	Закрыть(СравнитьДанные());
	
КонецПроцедуры

