
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.РежимВыбора = "ИзКарточкиДокумента" Тогда 
		
		ОбщегоНазначенияКлиентСервер.УстановитьОтборУСпискаНаРавенство(Список.Отбор,
			Новый ПолеКомпоновкиДанных("ДелоЗакрыто"), 
			Ложь);
		
		Список.ТекстЗапроса = Список.ТекстЗапроса + 
			" ГДЕ (&ДатаРегистрации >= НАЧАЛОПЕРИОДА(ДатаНачала, ДЕНЬ) ИЛИ ДатаНачала = ДАТАВРЕМЯ(1,1,1)) И
			|(&ДатаРегистрации <= КОНЕЦПЕРИОДА(ДатаОкончания, ДЕНЬ) ИЛИ ДатаОкончания = ДАТАВРЕМЯ(1,1,1)) ";
		
		Если ЗначениеЗаполнено(Параметры.ДатаРегистрации) Тогда 
			Список.Параметры.УстановитьЗначениеПараметра("ДатаРегистрации", Параметры.ДатаРегистрации);	
		Иначе	
			Список.Параметры.УстановитьЗначениеПараметра("ДатаРегистрации", ТекущаяДата());	
		КонецЕсли;	
	
	ИначеЕсли Параметры.РежимВыбора = "ИзПередачиДелВАрхив" 
		  Или Параметры.РежимВыбора = "ИзУничтоженияДел" Тогда 
		  
		Список.ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, 
		  	"РегистрСведений.СостоянияДелХраненияДокументов.СрезПоследних(", 
			"РегистрСведений.СостоянияДелХраненияДокументов.СрезПоследних(&Период");
		  
		Период = Новый Граница(Параметры.СостояниеНаДату, ВидГраницы.Исключая);
		Список.Параметры.УстановитьЗначениеПараметра("Период", Период);
			
		ОбщегоНазначенияКлиентСервер.УстановитьОтборУСпискаНаРавенство(Список.Отбор,
			Новый ПолеКомпоновкиДанных("ДелоЗакрыто"), 
			Истина);
		
		ОбщегоНазначенияКлиентСервер.УстановитьОтборУСпискаНаРавенство(Список.Отбор,
			Новый ПолеКомпоновкиДанных("Состояние"), 
			Перечисления.СостоянияДелХраненияДокументов.ПустаяСсылка());
			
		АдресВременногоХранилища = Параметры.АдресВременногоХранилища;
		Если ЗначениеЗаполнено(АдресВременногоХранилища) Тогда 
			Выбранные.Загрузить( ПолучитьИзВременногоХранилища(АдресВременногоХранилища) );
			ОбновитьУсловноеОформление(Список, Выбранные);
		КонецЕсли;	
			
	КонецЕсли;	
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") И ЗначениеЗаполнено(Параметры.Организация) Тогда 
		ОбщегоНазначенияКлиентСервер.УстановитьОтборУСпискаНаРавенство(Список.Отбор,
			Новый ПолеКомпоновкиДанных("Организация"), 
			Параметры.Организация);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	НоваяСтрока = Выбранные.Добавить();
	НоваяСтрока.ДелоХраненияДокументов = ВыбраннаяСтрока;
	
	ОбновитьУсловноеОформление(Список, Выбранные);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьУсловноеОформление(Список, Выбранные)
	
	МассивВыбранных = Новый Массив;
	Для Каждого Строка Из Выбранные Цикл
		МассивВыбранных.Добавить(Строка.ДелоХраненияДокументов);
	КонецЦикла;	
	
	ЭлементОформления = Список.УсловноеОформление.Элементы[0];
	ЭлементОформления.Отбор.Элементы[0].ПравоеЗначение = МассивВыбранных;
	
КонецПроцедуры