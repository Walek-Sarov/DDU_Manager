
Процедура ПечатьОбложки(ТабДок, Ссылки) Экспорт
	
	Макет = Справочники.ДелаХраненияДокументов.ПолучитьМакет("Обложка");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Дела.НомерТома КАК НомерТома,
	|	Дела.НоменклатураДел.Индекс КАК НомерДела,
	|	Дела.НоменклатураДел.ПолноеНаименование КАК ЗаголовокДела,
	|	Дела.НоменклатураДел.СрокХранения КАК СрокХранения,
	|	Дела.ДатаНачала КАК ДатаНачала,
	|	Дела.ДатаОкончания КАК ДатаОкончания,
	|	Дела.КоличествоЛистов КАК КоличествоЛистов,
	|	Дела.Организация КАК Организация
	|ИЗ
	|	Справочник.ДелаХраненияДокументов КАК Дела
	|ГДЕ
	|	Дела.Ссылка В(&Ссылки)";
	
	Запрос.УстановитьПараметр("Ссылки", Ссылки);
	Выборка = Запрос.Выполнить().Выбрать();

	ОбластьОбложка = Макет.ПолучитьОбласть("Обложка");
	ТабДок.Очистить();

	
	ПервыйДокумент = Истина;
	Пока Выборка.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		ЗаголовокДела1 = "";
		ЗаголовокДела2 = "";
		ЗаголовокДела3 = "";
		ЗаголовокДела4 = "";
		ЗаголовокДела5 = "";
		
		ДлинаСтроки = 50;
		КоличествоСтрок = 5;
		
		ЗаголовокДела = СтрЗаменить(Выборка.ЗаголовокДела, Символы.ПС, " ");
		Для Инд = 1 По КоличествоСтрок Цикл
			
			Если СтрДлина(ЗаголовокДела) > ДлинаСтроки Тогда
				врЗаголовокДела = СокрЛП(Лев(ЗаголовокДела, ДлинаСтроки));
				
				ПозицияПробела = 0;
				Для Поз = 1 По ДлинаСтроки Цикл
					Если Сред(врЗаголовокДела, ДлинаСтроки - Поз + 1, 1) = " " Тогда 
						ПозицияПробела = ДлинаСтроки - Поз + 1;
						Прервать;
					КонецЕсли;	
				КонецЦикла;
				
				Если ПозицияПробела > 0 Тогда 
					врЗаголовокДела = Лев(врЗаголовокДела, ПозицияПробела);
					Выполнить("ЗаголовокДела" + Инд + " = врЗаголовокДела");
					ЗаголовокДела = Сред(ЗаголовокДела, ПозицияПробела + 1)
				Иначе
					Выполнить("ЗаголовокДела" + Инд + " = врЗаголовокДела");
					ЗаголовокДела = Сред(ЗаголовокДела, ДлинаСтроки + 1)
				КонецЕсли;	
				
			Иначе	
				Выполнить("ЗаголовокДела" + Инд + " = ЗаголовокДела");
				Прервать;
			КонецЕсли;	
			
		КонецЦикла;	
		
		ОбластьОбложка.Параметры.Заполнить(Выборка);
		ОбластьОбложка.Параметры.ЗаголовокДела1 = ЗаголовокДела1;
		ОбластьОбложка.Параметры.ЗаголовокДела2 = ЗаголовокДела2;
		ОбластьОбложка.Параметры.ЗаголовокДела3 = ЗаголовокДела3;
		ОбластьОбложка.Параметры.ЗаголовокДела4 = ЗаголовокДела4;
		ОбластьОбложка.Параметры.ЗаголовокДела5 = ЗаголовокДела5;
		
		Если ЗначениеЗаполнено(Выборка.ДатаОкончания) Тогда 
			ОбластьОбложка.Параметры.ПериодДела = ПредставлениеПериода(Выборка.ДатаНачала, КонецДня(Выборка.ДатаОкончания));
		Иначе
			ОбластьОбложка.Параметры.ПериодДела = Формат(Выборка.ДатаНачала, "ДФ='dd MMMM yyyy'") + " - ";
		КонецЕсли;	
		
		ОбластьОбложка.Параметры.КоличествоЛистов = Выборка.КоличествоЛистов;
		
		ОбластьОбложка.Параметры.НаименованиеПредприятия = РаботаСОрганизациями.ПолучитьНаименованиеОрганизации(Выборка.Организация);
		
		СрокХранения = Выборка.СрокХранения;
		Если ТипЗнч(СрокХранения) = Тип("Число") Тогда 
			ОбластьОбложка.Параметры.СрокХранения = Строка(СрокХранения) + " " + ДелопроизводствоКлиентСервер.ПодписьЛет(СрокХранения);
		Иначе
			ОбластьОбложка.Параметры.СрокХранения = СрокХранения;
		КонецЕсли;
		
		ТабДок.Вывести(ОбластьОбложка);
	КонецЦикла;	
		
КонецПроцедуры

Процедура ПечатьВнутреннейОписи(ТабДок, Ссылка) Экспорт 
	
	Макет = Справочники.ДелаХраненияДокументов.ПолучитьМакет("ВнутренняяОпись");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДокументыВДелеТоме.Ссылка.РегистрационныйНомер КАК ИндексДокумента,
	|	ДокументыВДелеТоме.Ссылка.ДатаРегистрации КАК ДатаДокумента,
	|	ДокументыВДелеТоме.Ссылка.Наименование КАК ЗаголовокДокумента
	|ИЗ
	|	КритерийОтбора.ДокументыВДелеТоме(&Значение) КАК ДокументыВДелеТоме";
	
	Запрос.УстановитьПараметр("Значение", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка 	 = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока 	 = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал 	 = Макет.ПолучитьОбласть("Подвал");
	ТабДок.Очистить();

	// заголовок
	ОбластьЗаголовок.Параметры.ИндексДела = Ссылка.НоменклатураДел.Индекс;
	ТабДок.Вывести(ОбластьЗаголовок);
	
	// шапка
	ТабДок.Вывести(ОбластьШапка);
	
	// строки
	Номер = 0;
	Пока Выборка.Следующий() Цикл
		Номер = Номер + 1;
		
		ОбластьСтрока.Параметры.Заполнить(Выборка);
		ОбластьСтрока.Параметры.Номер = Номер;
		ТабДок.Вывести(ОбластьСтрока);
	КонецЦикла;
	
	ОбластьПодвал.Параметры.КоличествоДокументов = ?(Номер = 0, "", Строка(Номер) + ", " + ЧислоПрописью(Номер, , ",,,,,,,,0"));
	ОбластьПодвал.Параметры.Дата = ТекущаяДата();
	ОбластьПодвал.Параметры.Расшифровка = Строка(ОбщегоНазначения.ТекущийПользователь());
	ОбластьПодвал.Параметры.Должность = РаботаСПользователями.ПолучитьДолжность(ОбщегоНазначения.ТекущийПользователь());
	ТабДок.Вывести(ОбластьПодвал);
	
КонецПроцедуры	
	
Процедура ПечатьЛистаЗаверителя(ТабДок, Ссылки) Экспорт	
	
	Макет = Справочники.ДелаХраненияДокументов.ПолучитьМакет("ЛистЗаверитель");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Дела.НоменклатураДел.Индекс КАК НомерДела,
	|	Дела.КоличествоЛистов КАК КоличествоЛистов
	|ИЗ
	|	Справочник.ДелаХраненияДокументов КАК Дела
	|ГДЕ
	|	Дела.Ссылка В(&Ссылки)";
	
	Запрос.УстановитьПараметр("Ссылки", Ссылки);
	Выборка = Запрос.Выполнить().Выбрать();

	ОбластьЛистЗаверитель = Макет.ПолучитьОбласть("ЛистЗаверитель");
	ТабДок.Очистить();

	
	ПервыйДокумент = Истина;
	Пока Выборка.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		ОбластьЛистЗаверитель.Параметры.Заполнить(Выборка);
		ОбластьЛистЗаверитель.Параметры.КоличествоЛистов = ?(Выборка.КоличествоЛистов = 0, "", Строка(Выборка.КоличествоЛистов) + ", " + ЧислоПрописью(Выборка.КоличествоЛистов, , ",,,,,,,,0"));
		
		ОбластьЛистЗаверитель.Параметры.Дата = ТекущаяДата();
		ОбластьЛистЗаверитель.Параметры.Расшифровка = Строка(ОбщегоНазначения.ТекущийПользователь());
		ОбластьЛистЗаверитель.Параметры.Должность = РаботаСПользователями.ПолучитьДолжность(ОбщегоНазначения.ТекущийПользователь());
		
		ТабДок.Вывести(ОбластьЛистЗаверитель);
	КонецЦикла;	
		
КонецПроцедуры
	
Процедура ПечатьКартыЗаместителя(ТабДок, Ссылки) Экспорт
	
	Макет = Справочники.ДелаХраненияДокументов.ПолучитьМакет("КартаЗаместитель");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Дела.НомерТома КАК НомерТома,
	|	Дела.НоменклатураДел.Индекс КАК НомерДела,
	|	Дела.НоменклатураДел.Наименование КАК ЗаголовокДела,
	|	Дела.Организация КАК Организация
	|ИЗ
	|	Справочник.ДелаХраненияДокументов КАК Дела
	
	|ГДЕ
	|	Дела.Ссылка В(&Ссылки)";
	
	Запрос.УстановитьПараметр("Ссылки", Ссылки);
	Выборка = Запрос.Выполнить().Выбрать();

	ОбластьКартаЗаместитель = Макет.ПолучитьОбласть("КартаЗаместитель");
	ТабДок.Очистить();

	
	ПервыйДокумент = Истина;
	Пока Выборка.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		ОбластьКартаЗаместитель.Параметры.Заполнить(Выборка);
		
		ОбластьКартаЗаместитель.Параметры.НаименованиеПредприятия = РаботаСОрганизациями.ПолучитьНаименованиеОрганизации(Выборка.Организация);
		
		ТабДок.Вывести(ОбластьКартаЗаместитель);
	КонецЦикла;	
		
КонецПроцедуры
	