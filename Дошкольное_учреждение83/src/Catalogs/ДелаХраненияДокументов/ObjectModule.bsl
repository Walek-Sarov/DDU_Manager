
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Дела.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДелаХраненияДокументов КАК Дела
	|ГДЕ
	|	Дела.НомерТома = &НомерТома
	|	И Дела.НоменклатураДел = &НоменклатураДел
	|	И Дела.Ссылка <> &Ссылка";
	
	Запрос.УстановитьПараметр("НомерТома", НомерТома);
	Запрос.УстановитьПараметр("НоменклатураДел", НоменклатураДел);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Дело с указанным номером тома уже существует!'"), 
			ЭтотОбъект, 
			"НомерТома",, 
			Отказ);
		Возврат;	
	КонецЕсли;	
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ДелоЗакрыто Тогда 
		ПроверяемыеРеквизиты.Добавить("ДатаНачала");
		ПроверяемыеРеквизиты.Добавить("ДатаОкончания");
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Наименование = НоменклатураДел.Наименование + " (том №" + Формат(НомерТома, "ЧГ=") + ")";
	Организация = НоменклатураДел.Организация;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ДелоЗакрыто = Ложь;
	КоличествоЛистов = 0;
	ДатаОкончания = '00010101';
	
КонецПроцедуры
