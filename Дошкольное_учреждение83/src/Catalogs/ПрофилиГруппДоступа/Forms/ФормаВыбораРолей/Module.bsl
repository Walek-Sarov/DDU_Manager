
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.ЗакрыватьПриВыборе = Ложь Тогда
		Заголовок = НСтр("ru = 'Подбор разрешенных действий'");
		Элементы.Роли.МножественныйВыбор = Истина;
		Элементы.Роли.РежимВыделения = РежимВыделенияТаблицы.Множественный;
	Иначе
		Заголовок = НСтр("ru = 'Выбор разрешенного действия'");
	КонецЕсли;
	
	ВсеРоли = УправлениеДоступомСерверПовтИсп.ВсеРоли();
	Для каждого Строка Из ВсеРоли Цикл
		Если ВРег(Строка.Имя) <> ВРег("ПолныеПрава") Тогда
			НоваяСтрока = Роли.Добавить();
			НоваяСтрока.Имя               = Строка.Имя;
			НоваяСтрока.Синоним           = Строка.Синоним;
			НоваяСтрока.ИменаПодсистем    = Строка.ИменаПодсистем;
			НоваяСтрока.СинонимыПодсистем = Строка.СинонимыПодсистем;
			
		КонецЕсли;
	КонецЦикла;
	Роли.Сортировать("Синоним Возр");
	
	Если Параметры.Свойство("ТекущаяСтрока") Тогда
		НайденныеСтроки = Роли.НайтиСтроки(Новый Структура("Имя", Параметры.ТекущаяСтрока));
		Если НайденныеСтроки.Количество() > 0 Тогда
			Элементы.Роли.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РолиВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(Значение) = Тип("Массив") Тогда
		ЗначениеВыбора = Новый Массив;
		Для каждого Идентификатор Из Значение Цикл
			ЗначениеВыбора.Добавить(Роли.НайтиПоИдентификатору(Идентификатор).Имя);
		КонецЦикла;
		
	Иначе
		ЗначениеВыбора = Роли.НайтиПоИдентификатору(Значение).Имя;
	КонецЕсли;
	
	ОповеститьОВыборе(ЗначениеВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьТекстПолностью(Команда)
	
	Если Элементы.Роли.ТекущиеДанные <> Неопределено Тогда
		ОткрытьЗначение(Элементы.Роли.ТекущиеДанные[Элементы.Роли.ТекущийЭлемент.Имя]);
	КонецЕсли;
	
КонецПроцедуры
