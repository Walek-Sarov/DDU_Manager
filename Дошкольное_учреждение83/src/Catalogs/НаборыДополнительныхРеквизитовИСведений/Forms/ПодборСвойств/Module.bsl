////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропуск инициализации, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранныеСвойства.ЗагрузитьЗначения(Параметры.ВыбранныеСвойства);
	
	ЭтоДополнительноеСведение = Параметры.ЭтоДополнительноеСведение;
	
	Если ЭтоДополнительноеСведение Тогда
		Заголовок = НСтр("ru = 'Подбор дополнительных сведений'");
	Иначе
		Заголовок = НСтр("ru = 'Подбор дополнительных реквизитов'");
	КонецЕсли;
	
	ДеревоСвойств.Параметры.УстановитьЗначениеПараметра("ВыбранныеСвойства", Параметры.ВыбранныеСвойства);
	
	ЭлементОтбора = ДеревоСвойств.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтоДополнительноеСведение");
	
	Если ЭтоДополнительноеСведение Тогда
		ЭлементОтбора.ПравоеЗначение = Истина;
	Иначе
		ЭлементОтбора.ПравоеЗначение = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьКомментарий();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ДополнительныеРеквизитыИСведения" Тогда
		
		Элементы.ДеревоСвойств.Обновить();
		
		ТекущиеДанные = Элементы.ДеревоСвойств.ТекущиеДанные;
		
		Если ТекущиеДанные <> Неопределено И ТекущиеДанные.Ссылка = Источник Тогда
			Комментарий = Параметр.Комментарий;
		КонецЕсли;
		
		ДеревоСвойств.Параметры.УстановитьЗначениеПараметра("ВыбранныеСвойства", ВыбранныеСвойства.ВыгрузитьЗначения());
		
	ИначеЕсли ИмяСобытия = "ИзменилсяНаборВыбранных" И Источник = ЭтаФорма.ВладелецФормы Тогда
		
		Для Каждого Элемент Из Параметр.МассивСвойств Цикл
			Найденное = ВыбранныеСвойства.НайтиПоЗначению(Элемент);
			
			Если Параметр.Операция = "Добавление" И Найденное = Неопределено Тогда
				ВыбранныеСвойства.Добавить(Элемент);
				
			ИначеЕсли Параметр.Операция = "Удаление" И Найденное <> Неопределено Тогда
				ВыбранныеСвойства.Удалить(Найденное);
			КонецЕсли;
		КонецЦикла;
		
		ДеревоСвойств.Параметры.УстановитьЗначениеПараметра("ВыбранныеСвойства", ВыбранныеСвойства.ВыгрузитьЗначения());
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ ДеревоСвойств

&НаКлиенте
Процедура ДеревоСвойствПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	// Для групп используется стандартная обработка.
	Если Группа = Истина Тогда
		Возврат;
	ИначеЕсли Копирование И (Элементы.ДеревоСвойств.ТекущиеДанные.НомерКартинкиСведения % 3 = 0) Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("ОткрываетсяИзНабораСвойств", Истина);
	ПараметрыФормы.Вставить("ЭтоДополнительноеСведение", ЭтоДополнительноеСведение);
	
	Если Копирование Тогда
		ПараметрыФормы.Вставить("Копируемый", Элементы.ДеревоСвойств.ТекущаяСтрока);
	Иначе
		ПараметрыФормы.Вставить("Родитель", Родитель);
	КонецЕсли;
	
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.Форма.ФормаЭлемента", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвойствПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	Если Элементы.ДеревоСвойств.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если (Элементы.ДеревоСвойств.ТекущиеДанные.НомерКартинкиСведения % 3 = 0) Тогда
		ИмяФормыПВХ = "ФормаГруппы";
	Иначе
		ИмяФормыПВХ = "ФормаЭлемента";
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", Элементы.ДеревоСвойств.ТекущаяСтрока);
	ПараметрыФормы.Вставить("ОткрываетсяИзНабораСвойств", Истина);
	
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.Форма." + ИмяФормыПВХ, ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвойствПриАктивизацииСтроки(Элемент)
	
	УстановитьКомментарий();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвойствВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыполнитьПодборСвойств();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ДобавитьСвойствоВНабор(Команда)
	
	ВыполнитьПодборСвойств();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура УстановитьКомментарий()
	
	Если Элементы.ДеревоСвойств.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Комментарий = Элементы.ДеревоСвойств.ТекущиеДанные.Комментарий;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПодборСвойств()
	
	Если Элементы.ДеревоСвойств.ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДобавляемыеСвойства = Новый Массив;
	
	Для Каждого ВыделенаяСтрока ИЗ Элементы.ДеревоСвойств.ВыделенныеСтроки Цикл
		Свойство = Элементы.ДеревоСвойств.ДанныеСтроки(ВыделенаяСтрока).Ссылка;
		ДобавляемыеСвойства.Добавить(Свойство);
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("ЭтоДополнительноеСведение", ЭтоДополнительноеСведение);
	Результат.Вставить("ДобавляемыеСвойства",       ДобавляемыеСвойства);
	
	Оповестить("ПодборСвойствВНабор", Результат);
	
КонецПроцедуры

