

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;	
	
	Если ПометкаУдаления <> Ссылка.ПометкаУдаления Тогда 
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НоменклатураДел.Ссылка
		|ИЗ
		|	Справочник.НоменклатураДел КАК НоменклатураДел
		|ГДЕ
		|	НоменклатураДел.Раздел = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Выборка.Ссылка.ПолучитьОбъект().УстановитьПометкуУдаления(ПометкаУдаления);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры


Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(Родитель) И (Год <> Родитель.Год) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(	
			НСтр("ru = 'Год раздела не совпадает с годом вышестоящего раздела'"),
			ЭтотОбъект,
			"Год",,
			Отказ);
	КонецЕсли;
	
КонецПроцедуры

