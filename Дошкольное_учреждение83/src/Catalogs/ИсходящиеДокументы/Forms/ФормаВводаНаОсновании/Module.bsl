
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Файлы.Ссылка КАК Ссылка,
	|	Файлы.ПолноеНаименование КАК Наименование,
	|	Файлы.Описание КАК Описание,
	|	Файлы.ИндексКартинки КАК ИндексКартинки,
	|	ИСТИНА КАК Пометка
	|ИЗ
	|	Справочник.Файлы КАК Файлы
	|ГДЕ
	|	Файлы.ВладелецФайла = &ВладелецФайла";
	Запрос.УстановитьПараметр("ВладелецФайла", Параметры.Основание);
	
	ЗначениеВРеквизитФормы(Запрос.Выполнить().Выгрузить(), "Файлы");
	
	Получатель = Справочники.Корреспонденты.ПустаяСсылка();
	Если ТипЗнч(Параметры.Основание) = Тип("СправочникСсылка.ВнутренниеДокументы") Тогда 
		Если Параметры.Основание.ВидДокумента.УчетПоКорреспондентам Тогда 
			Получатель = Параметры.Основание.Корреспондент;
		КонецЕсли;	
	КонецЕсли;	
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВидыИсходящихДокументов") Тогда 
		ВидДокумента = Делопроизводство.ПолучитьВидДокументаПоУмолчанию(Справочники.ИсходящиеДокументы.ПустаяСсылка());
	КонецЕсли;	
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрифыДоступаКВходящимИИсходящимДокументам") Тогда
		ГрифДоступа = Константы.ГрифДоступаПоУмолчанию.Получить();
	КонецЕсли;
	
	// выбор вида документа
	БыстрыйВыборВидаДокумента = Делопроизводство.ПолучитьРежимВыборавидаДокумента("ИсходящийДокумент");
	Элементы.ВидДокумента.БыстрыйВыбор = БыстрыйВыборВидаДокумента;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрифыДоступаКВходящимИИсходящимДокументам") Тогда 
		ПроверяемыеРеквизиты.Добавить("ГрифДоступа");
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВидыИсходящихДокументов") Тогда 
		ПроверяемыеРеквизиты.Добавить("ВидДокумента");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отметить(Команда)
	
	Для Каждого Строка Из Файлы Цикл
		Строка.Пометка = Истина;
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметку(Команда)
	
	Для Каждого Строка Из Файлы Цикл
		Строка.Пометка = Ложь;
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайлВыполнить()
	
	Если Элементы.Файлы.ТекущаяСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ДанныеФайла = РаботаСФайлами.ПолучитьДанныеФайлаДляОткрытия(Элементы.Файлы.ТекущиеДанные.Ссылка, Неопределено, ЭтаФорма.УникальныйИдентификатор);
	КомандыРаботыСФайлами.Открыть(ДанныеФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДелопроизводствоКлиент.ВыбратьПолучателя(Элемент, Получатель);
	
КонецПроцедуры
