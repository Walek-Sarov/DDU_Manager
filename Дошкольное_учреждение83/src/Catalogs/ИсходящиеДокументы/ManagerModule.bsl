
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
    // Устанавливаем признак достпности печати по-комплектно
    ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
    Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Карточка") Тогда

        // Формируем табличный документ и добавляем его в коллекцию печатных форм
        УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
            "Карточка", "Карточка внутреннего документа", ПечатьКарточку(МассивОбъектов, ОбъектыПечати));

    КонецЕсли;
КонецПроцедуры

Функция ПечатьКарточку(МассивОбъектов, ОбъектыПечати)
	
	// Создаем табличный документ и устанавливаем имя параметров печати
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПараметрыПечати_КарточкаВнутреннегоДокумента";
	
	// Получаем запросом необходимые данные
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИсходящийДокумент.Адресат КАК Адресат,
	|	ИсходящийДокумент.ВидДокумента КАК ВидДокумента,
	|	ИсходящийДокумент.ВОтветНа.ИсходящаяДата КАК ВОтветНаДата,
	|	ИсходящийДокумент.ВОтветНа.ИсходящийНомер КАК ВОтветНаНомер,
	|	ИсходящийДокумент.ДатаРегистрации КАК ДатаРегистрации,
	|	ИсходящийДокумент.Дело КАК Дело,
	|	ИсходящийДокумент.Зарегистрировал КАК Зарегистрировал,
	|	ИсходящийДокумент.Наименование КАК Наименование,
	|	ИсходящийДокумент.РегистрационныйНомер КАК РегистрационныйНомер,
	|	ИсходящийДокумент.Подготовил КАК Подготовил,
	|	ИсходящийДокумент.Подписал КАК Подписал,
	|	ИсходящийДокумент.Получатель КАК Получатель,
	|	ИсходящийДокумент.ПолученОтвет.ДатаРегистрации КАК ПолученОтветДата,
	|	ИсходящийДокумент.ПолученОтвет.РегистрационныйНомер КАК ПолученОтветНомер,
	|	ИсходящийДокумент.Содержание КАК Содержание,
	|	ИсходящийДокумент.КоличествоЛистов КАК КоличествоЛистов,
	|	ИсходящийДокумент.КоличествоПриложений КАК КоличествоПриложений,
	|	ИсходящийДокумент.ЛистовВПриложениях КАК ЛистовВПриложениях,
	|	ИсходящийДокумент.СпособОтправки КАК СпособОтправки,
	|	ИсходящийДокумент.Представление КАК Представление,
	|	ИсходящийДокумент.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ИсходящиеДокументы КАК ИсходящийДокумент
	|ГДЕ
	|	ИсходящийДокумент.Ссылка В(&МассивОбъектов)";

	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Шапка = Запрос.Выполнить().Выбрать();
	
	Макет = Справочники.ИсходящиеДокументы.ПолучитьМакет("Карточка");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	
	ПервыйДокумент = Истина;
	Пока Шапка.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		// Запомним номер строки с которой начали выводить текущий документ
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		
		ОбластьЗаголовок.Параметры.Заполнить(Шапка);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);

		ОбластьШапка.Параметры.Заполнить(Шапка);
		
		Если ЗначениеЗаполнено(Шапка.ВОтветНаНомер) Или ЗначениеЗаполнено(Шапка.ВОтветНаДата) Тогда
			ОбластьШапка.Параметры.ВОтветНаДокумент = "№ " + Шапка.ВОтветНаНомер + " от " + Формат(Шапка.ВОтветНаДата, "ДФ=dd.MM.yyyy");
		КонецЕсли;
		Если ЗначениеЗаполнено(Шапка.ПолученОтветНомер) Или ЗначениеЗаполнено(Шапка.ПолученОтветДата) Тогда
			ОбластьШапка.Параметры.ПолученОтвет = "№ " + Шапка.ПолученОтветНомер + " от " + Формат(Шапка.ПолученОтветДата, "ДФ=dd.MM.yyyy");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Шапка.КоличествоЛистов) И ЗначениеЗаполнено(Шапка.КоличествоПриложений) И ЗначениеЗаполнено(Шапка.ЛистовВПриложениях) Тогда
			ОбластьШапка.Параметры.Состав = "Листов " + Строка(Шапка.КоличествоЛистов) + ", приложений " + Строка(Шапка.КоличествоПриложений) + ", листов в приложениях " + Строка(Шапка.ЛистовВПриложениях);
		ИначеЕсли ЗначениеЗаполнено(Шапка.КоличествоЛистов) И ЗначениеЗаполнено(Шапка.КоличествоПриложений) Тогда 
			ОбластьШапка.Параметры.Состав = "Листов " + Строка(Шапка.КоличествоЛистов) + ", приложений " + Строка(Шапка.КоличествоПриложений);
		ИначеЕсли ЗначениеЗаполнено(Шапка.КоличествоЛистов) Тогда 
			ОбластьШапка.Параметры.Состав = "Листов " + Строка(Шапка.КоличествоЛистов);	
		КонецЕсли;	
		
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		
		// В табличном документе зададим имя области в которую был 
		// выведен объект. Нужно для возможности печати по-комплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции
