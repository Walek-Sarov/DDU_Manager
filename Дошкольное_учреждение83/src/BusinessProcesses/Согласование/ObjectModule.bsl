
////////////////////////////////////////////////////////////////////////////////
// Обработчики событий бизнес-процесса

// Процедура ЗаполнитьНаборыЗначенийДоступа заполняет таблицу
// НаборыЗначений(НомерНабора, ВидДоступа, ЗначениеДоступа, Чтение, Добавление, Изменение, Удаление)
// по проверяемому объекту.
//
//  Вызывается из процедуры УправлениеДоступом.ЗаписатьНаборыЗначенийДоступа(),
// если объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьНаборыЗначенийДоступа" и
// из таких же процедур объектов, у которых наборы значений доступа зависят от наборов этого
// объекта (в этом случае объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьЗависимыеНаборыЗначенийДоступа").
//
// Параметры:
//  Таблица      - ТабличнаяЧасть,
//                 РегистрСведенийНаборЗаписей.НаборыЗначенийДоступа,
//                 ТаблицаЗначений, возвращаемая УправлениеДоступом.ТаблицаНаборыЗначенийДоступа().
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	// Логика ограничения:
	// Чтения:                Автор ИЛИ Исполнитель ИЛИ <исполнитель по адресации>
	// Добавления, Изменения: Автор
	
	// Реквизит ГруппаДоступаИсполнителей содержит либо "Исполнителя" либо 
	// "СправочникСсылка.ГруппыДоступаИсполнителей"
	
	// Чтение, Добавление, Изменение: набор №1.
	Строка = Таблица.Добавить();
	Строка.НомерНабора     = 1;
	Строка.Чтение          = Истина;
	Строка.Добавление      = Истина;
	Строка.Изменение       = Истина;
	Строка.ВидДоступа      = ПланыВидовХарактеристик.ВидыДоступа.Пользователи;
	Строка.ЗначениеДоступа = Автор;
	
	// Чтение: набор №2.
	ТекущийНомерНабора = 2;
	Для каждого Элемент Из Исполнители Цикл
		Строка = Таблица.Добавить();
		Строка.НомерНабора     = ТекущийНомерНабора;
		Строка.Чтение          = Истина;
		Строка.ВидДоступа      = ПланыВидовХарактеристик.ВидыДоступа.Пользователи;
		Строка.ЗначениеДоступа = Элемент.ГруппаДоступаИсполнителей;
		
		ТекущийНомерНабора = ТекущийНомерНабора + 1;
	КонецЦикла;
	
	// Чтение:
	Для каждого Элемент Из ДополнительныеИсполнители Цикл
		Строка = Таблица.Добавить();
		Строка.НомерНабора     = ТекущийНомерНабора;
		Строка.Чтение          = Истина;
		Строка.ВидДоступа      = ПланыВидовХарактеристик.ВидыДоступа.Пользователи;
		Строка.ЗначениеДоступа = Элемент.ГруппаДоступаИсполнителей;
		
		ТекущийНомерНабора = ТекущийНомерНабора + 1;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = ТекущаяДата();
	Автор = ОбщегоНазначения.ТекущийПользователь();
	НомерИтерации = 0;
	РезультатыСогласования.Очистить();
	РезультатыОзнакомлений.Очистить();
	ДатаНачала = '00010101';
	ДатаЗавершения = '00010101';
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда
		Дата = ТекущаяДата();
		Автор = ОбщегоНазначения.ТекущийПользователь();
		Важность = Перечисления.ВариантыВажностиЗадачи.Обычная;
		НомерИтерации = 0;
	КонецЕсли;	
	
	Если ДанныеЗаполнения <> Неопределено И ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") Тогда
		Предмет = ДанныеЗаполнения;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Предмет) Тогда 
		Наименование = "Согласовать """ + Строка(Предмет) + """";
	Иначе
		Наименование = "Согласовать ";
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Для Каждого Строка Из Исполнители Цикл
		Если Не БизнесПроцессыИЗадачиПереопределяемый.ЕстьПравоДоступаУчастникаБизнесПроцесса(ЭтотОбъект, Предмет, "ЧтениеПапокИФайлов",  Строка.Исполнитель, Строка.ОсновнойОбъектАдресации, Строка.ДополнительныйОбъектАдресации, "Исполнители", "Исполнитель", Строка.НомерСтроки - 1) Тогда
			Отказ = Истина;
		КонецЕсли;	
	КонецЦикла;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПометкаУдаления <> Ссылка.ПометкаУдаления Тогда
		Если ПометкаУдаления Тогда 
			РаботаСБизнесПроцессами.УдалитьСостоянияБизнесПроцесса(Ссылка);
		Иначе
			ВосстановитьСостоянияБизнесПроцесса();
		КонецЕсли;	
	КонецЕсли;
	
	// Исполнители (табличная часть)
	Для каждого Элемент Из Исполнители Цикл
		Если ТипЗнч(Элемент.Исполнитель) = Тип("СправочникСсылка.РолиИсполнителей") Тогда
			Элемент.ГруппаДоступаИсполнителей = РегистрыСведений.ИсполнителиЗадач.ГруппаДоступаИсполнителей(
				Элемент.Исполнитель, Элемент.ОсновнойОбъектАдресации, Элемент.ДополнительныйОбъектАдресации);
		Иначе		
			Элемент.ГруппаДоступаИсполнителей = Элемент.Исполнитель;
		КонецЕсли;		
	КонецЦикла;	
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// Обработчики событий элементов карты маршрута

Процедура СтартПередСтартом(ТочкаМаршрутаБизнесПроцесса, Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	ДатаНачала = ТекущаяДата();
	Записать();
	
КонецПроцедуры

Процедура ПодготовкаОбработка(ТочкаМаршрутаБизнесПроцесса)
	
	УстановитьПривилегированныйРежим(Истина);
	Если ЗначениеЗаполнено(Предмет) И ДелопроизводствоКлиентСервер.ЭтоСсылкаНаДокумент(Предмет) Тогда 
		Делопроизводство.ЗаписатьСостояниеДокумента(
			Предмет, 
			ТекущаяДата(), 
			Перечисления.СостоянияДокументов.НаСогласовании, 
			Ссылка);
	КонецЕсли;
	
	НомерИтерации = НомерИтерации + 1;
	ПовторитьСогласование = Ложь;
	Если ВариантСогласования = Перечисления.ВариантыСогласования.Последовательно Тогда 
		Для Каждого Строка Из Исполнители Цикл
			Строка.Пройден = Ложь;
		КонецЦикла;	
	КонецЕсли;	
	Записать();
	
КонецПроцедуры

Процедура НаправлятьВсемСразуПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	
	Результат = (ВариантСогласования = Перечисления.ВариантыСогласования.Параллельно);
	
КонецПроцедуры

Процедура СогласоватьПараллельноПередСозданиемЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Для Каждого Строка Из Исполнители Цикл
		Задача = Задачи.ЗадачаИсполнителя.СоздатьЗадачу();
		Задача.Дата  	= ТекущаяДата();
		Задача.Автор 	= Автор;
		Задача.Описание = Описание;
		Задача.Предмет 	= Предмет;
		Задача.Важность = Важность;
		
		Задача.Наименование  = Наименование;
		Задача.БизнесПроцесс = ЭтотОбъект.Ссылка;
		Задача.ТочкаМаршрута = ТочкаМаршрутаБизнесПроцесса;
		
		Если ЗначениеЗаполнено(СрокИсполнения) Тогда 
			Задача.СрокИсполнения = Задача.Дата + СрокИсполнения * 24 * 3600;
		Иначе	
			Задача.СрокИсполнения = '00010101';
		КонецЕсли;	
		
		Если ТипЗнч(Строка.Исполнитель) = Тип("СправочникСсылка.Пользователи") Тогда
			Задача.Исполнитель = Строка.Исполнитель;
		Иначе	
			Задача.РольИсполнителя = Строка.Исполнитель;
			Задача.ОсновнойОбъектАдресации = Строка.ОсновнойОбъектАдресации;
			Задача.ДополнительныйОбъектАдресации = Строка.ДополнительныйОбъектАдресации;
		КонецЕсли;	
		
		Задача.Записать();
		ФормируемыеЗадачи.Добавить(Задача);
		
		НоваяСтрока = РезультатыСогласования.Добавить();
		НоваяСтрока.НомерИтерации 	  = НомерИтерации;
		НоваяСтрока.ЗадачаИсполнителя = Задача.Ссылка;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Истина);
	Записать();
	
КонецПроцедуры

Процедура СогласоватьПоследовательноПередСозданиемЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Для Каждого Строка Из Исполнители Цикл
		
		Если Строка.Пройден Тогда
			Продолжить;
		КонецЕсли;	
			
		Задача = Задачи.ЗадачаИсполнителя.СоздатьЗадачу();
		Задача.Дата 	= ТекущаяДата();
		Задача.Автор 	= Автор;
		Задача.Описание = Описание;
		Задача.Предмет 	= Предмет;
		Задача.Важность = Важность;
		
		Задача.Наименование  = Наименование;
		Задача.БизнесПроцесс = ЭтотОбъект.Ссылка;
		Задача.ТочкаМаршрута = ТочкаМаршрутаБизнесПроцесса;
		
		Если ЗначениеЗаполнено(СрокИсполнения) Тогда 
			Задача.СрокИсполнения = Задача.Дата + СрокИсполнения * 24 * 3600;
		Иначе	
			Задача.СрокИсполнения = '00010101';
		КонецЕсли;	
		
		Если ТипЗнч(Строка.Исполнитель) = Тип("СправочникСсылка.Пользователи") Тогда
			Задача.Исполнитель = Строка.Исполнитель;
		Иначе	
			Задача.РольИсполнителя = Строка.Исполнитель;
			Задача.ОсновнойОбъектАдресации = Строка.ОсновнойОбъектАдресации;
			Задача.ДополнительныйОбъектАдресации = Строка.ДополнительныйОбъектАдресации;
		КонецЕсли;	
		
		Задача.Записать();
		ФормируемыеЗадачи.Добавить(Задача);
		
		УстановитьПривилегированныйРежим(Истина);
		НоваяСтрока = РезультатыСогласования.Добавить();
		НоваяСтрока.НомерИтерации 	  = НомерИтерации;
		НоваяСтрока.ЗадачаИсполнителя = Задача.Ссылка;
		Записать();
		
		Прервать;
	КонецЦикла;
	
КонецПроцедуры

Процедура СогласоватьПоследовательноПередВыполнением(ТочкаМаршрутаБизнесПроцесса, Задача, Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	Для Каждого Строка Из Исполнители Цикл
		Если Не Строка.Пройден Тогда
			Если Строка.Исполнитель = Задача.Исполнитель ИЛИ 
				(Строка.Исполнитель = Задача.РольИсполнителя И
				Строка.ОсновнойОбъектАдресации = Задача.ОсновнойОбъектАдресации И
				Строка.ДополнительныйОбъектАдресации = Задача.ДополнительныйОбъектАдресации) Тогда
				
				Строка.Пройден = Истина;
				Записать();
				Прервать;
				
			КонецЕсли;	
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры

Процедура УсловиеОбходЗавершен(ТочкаМаршрутаБизнесПроцесса, Результат)
	
	ПараметрыОтбора = Новый Структура("НомерИтерации, РезультатСогласования", НомерИтерации, Перечисления.РезультатыСогласования.НеСогласовано);
	РезультатНеСогласовано = РезультатыСогласования.НайтиСтроки(ПараметрыОтбора);
	
	Если (Исполнители.Найти(Ложь, "Пройден") = Неопределено) Или (РезультатНеСогласовано.Количество() > 0) Тогда 
		Результат = Истина;
	Иначе
		Результат = Ложь;
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбработкаРезультатаОбработка(ТочкаМаршрутаБизнесПроцесса)
	
	УстановитьПривилегированныйРежим(Истина);
	
	// результат согласования
	Если РезультатыСогласования.Количество() > 0 Тогда
		
		РезультатСогласования = Перечисления.РезультатыСогласования.Согласовано;
		Для Каждого Элемент Из РезультатыСогласования Цикл
			Если Элемент.НомерИтерации = НомерИтерации Тогда
				
				Если Элемент.РезультатСогласования = Перечисления.РезультатыСогласования.НеСогласовано Тогда 
					РезультатСогласования = Перечисления.РезультатыСогласования.НеСогласовано;
					Прервать;
				КонецЕсли;	
				
				Если Элемент.РезультатСогласования = Перечисления.РезультатыСогласования.СогласованоСЗамечаниями Тогда 
					РезультатСогласования = Перечисления.РезультатыСогласования.СогласованоСЗамечаниями;
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;	
		
	КонецЕсли;	
	Записать();
	
	Если ЗначениеЗаполнено(Предмет) И ДелопроизводствоКлиентСервер.ЭтоСсылкаНаДокумент(Предмет) Тогда 
		Если РезультатСогласования = Перечисления.РезультатыСогласования.НеСогласовано Тогда 
			Состояние = Перечисления.СостоянияДокументов.НеСогласован;
		Иначе	
			Состояние = Перечисления.СостоянияДокументов.Согласован;
		КонецЕсли;	
		
		Делопроизводство.ЗаписатьСостояниеДокумента(
			Предмет, 
			ТекущаяДата(), 
			Состояние, 
			Ссылка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОзнакомитьсяПередСозданиемЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	// для вложенного бп
	Если ЗначениеЗаполнено(ВедущаяЗадача) Тогда
		Если РезультатСогласования <> Перечисления.РезультатыСогласования.НеСогласовано Тогда 
			Возврат;
		КонецЕсли;
	КонецЕсли;	
	
	Задача = Задачи.ЗадачаИсполнителя.СоздатьЗадачу();
	Задача.Дата  	= ТекущаяДата();
	Задача.Автор 	= Автор;
	Задача.Описание = Описание;
	Задача.Предмет 	= Предмет;
	Задача.Важность = Важность;
	
	Задача.Исполнитель 	  = Автор;
	Задача.СрокИсполнения = '00010101'; 
	Задача.БизнесПроцесс  = Ссылка;
	Задача.ТочкаМаршрута  = ТочкаМаршрутаБизнесПроцесса;
	
	Если ЗначениеЗаполнено(Предмет) Тогда 
		Задача.Наименование = НСтр("ru = 'Ознакомиться с результатом согласования ""'") + Строка(Предмет) + """";
	Иначе
		Задача.Наименование = НСтр("ru = 'Ознакомиться с результатом согласования: '") + Наименование;
	КонецЕсли;	
	
	Задача.Записать();
	ФормируемыеЗадачи.Добавить(Задача);
	
	УстановитьПривилегированныйРежим(Истина);
	НоваяСтрока = РезультатыОзнакомлений.Добавить();
	НоваяСтрока.НомерИтерации 	  = НомерИтерации;
	НоваяСтрока.ЗадачаИсполнителя = Задача.Ссылка;
	Записать();
	
КонецПроцедуры

Процедура ПовторитьСогласованиеПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	
	Результат = ПовторитьСогласование;
	
КонецПроцедуры

Процедура ОбработкаПроверкиВыполнения(ТочкаМаршрутаБизнесПроцесса, Задача, Результат)
	
	Результат = Истина;
	
КонецПроцедуры

Процедура ЗавершениеПриЗавершении(ТочкаМаршрутаБизнесПроцесса, Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	ДатаЗавершения = ТекущаяДата();
	Записать();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры

Процедура ИзменитьРеквизитыНевыполненныхЗадач() Экспорт 

	НачатьТранзакцию();
	Попытка
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Задачи.Ссылка,
		|	Задачи.ТочкаМаршрута
		|ИЗ
		|	Задача.ЗадачаИсполнителя КАК Задачи
		|ГДЕ
		|	Задачи.БизнесПроцесс = &БизнесПроцесс
		|	И Задачи.ПометкаУдаления = ЛОЖЬ
		|	И Задачи.Выполнена = ЛОЖЬ";
		Запрос.УстановитьПараметр("БизнесПроцесс", Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			ЗадачаОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ЗаблокироватьДанныеДляРедактирования(ЗадачаОбъект.Ссылка);
			
			Если ЗадачаОбъект.ТочкаМаршрута = БизнесПроцессы.Согласование.ТочкиМаршрута.Ознакомиться Тогда 
				Если ЗначениеЗаполнено(Предмет) Тогда 
					ЗадачаОбъект.Наименование = "Ознакомиться с результатом согласования """ + Строка(Предмет) + """";
				Иначе
					ЗадачаОбъект.Наименование = "Ознакомиться с результатом согласования: " + Наименование;
				КонецЕсли;
			Иначе	
				ЗадачаОбъект.Наименование = Наименование;
				
				Если ЗначениеЗаполнено(СрокИсполнения) Тогда 
					ЗадачаОбъект.СрокИсполнения = ЗадачаОбъект.Дата + СрокИсполнения * 24 * 3600;
				Иначе
					ЗадачаОбъект.СрокИсполнения = '00010101';
				КонецЕсли;	
			КонецЕсли;
			
			ЗадачаОбъект.Важность = Важность;
			ЗадачаОбъект.Описание = Описание;
			ЗадачаОбъект.Автор 	  = Автор;
			ЗадачаОбъект.Записать();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры 

Процедура ВосстановитьСостоянияБизнесПроцесса()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СогласованиеРезультатыСогласования.НомерИтерации,
	|	МИНИМУМ(СогласованиеРезультатыСогласования.ЗадачаИсполнителя.Дата) КАК ПерваяЗадача,
	|	МАКСИМУМ(СогласованиеРезультатыСогласования.ЗадачаИсполнителя.ДатаИсполнения) КАК ПоследняяЗадача
	|ИЗ
	|	БизнесПроцесс.Согласование.РезультатыСогласования КАК СогласованиеРезультатыСогласования
	|ГДЕ
	|	СогласованиеРезультатыСогласования.Ссылка = &ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	СогласованиеРезультатыСогласования.НомерИтерации";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	ДатыЗадач = Запрос.Выполнить().Выгрузить();
	
	
	Для Инд = 1 По НомерИтерации Цикл
		СтрокаДаты = ДатыЗадач.Найти(Инд, "НомерИтерации");
		
		Если ЗначениеЗаполнено(Предмет) И ДелопроизводствоКлиентСервер.ЭтоСсылкаНаДокумент(Предмет) Тогда 
			Делопроизводство.ЗаписатьСостояниеДокумента(
				Предмет, 
				СтрокаДаты.ПерваяЗадача, 
				Перечисления.СостоянияДокументов.НаСогласовании, 
				Ссылка);
		КонецЕсли;
		
		ЗадачиИтерации = РезультатыСогласования.НайтиСтроки(Новый Структура("НомерИтерации", Инд));
		
		ИтерацияЗавершена = Истина;
		Для Каждого Строка Из ЗадачиИтерации Цикл
			Если Не Строка.ЗадачаИсполнителя.Выполнена Тогда 
				ИтерацияЗавершена = Ложь;
				Прервать;
			КонецЕсли;	
		КонецЦикла;	
		
		Если ИтерацияЗавершена Тогда 
			
			СостояниеИтерации = Перечисления.СостоянияДокументов.Согласован;
			Для Каждого Строка Из ЗадачиИтерации Цикл
				Если Строка.РезультатСогласования = Перечисления.РезультатыСогласования.НеСогласовано Тогда 
					СостояниеИтерации = Перечисления.СостоянияДокументов.НеСогласован;
					Прервать;
				КонецЕсли;	
			КонецЦикла;	
			
			Если ЗначениеЗаполнено(Предмет) И ДелопроизводствоКлиентСервер.ЭтоСсылкаНаДокумент(Предмет) Тогда 
				Делопроизводство.ЗаписатьСостояниеДокумента(
					Предмет, 
					СтрокаДаты.ПоследняяЗадача, 
					СостояниеИтерации, 
					Ссылка);
			КонецЕсли;
			
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры	