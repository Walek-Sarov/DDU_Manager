////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ

// Формирует запрос по документу
//
// Параметры: 
//  Режим - режим проведения
//
// Возвращаемое значение:
//  Результат запроса
//
Функция СформироватьЗапросДляПечати(Режим,Док)

	Запрос = Новый Запрос;
	Если не ЗначениеЗаполнено(Док) тогда
		 Возврат Неопределено;
	КонецЕсли;
	 
	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",	Док);
	Запрос.УстановитьПараметр("ДатаДокумента",	Док.Дата);
	
	Если Режим = "ПоРеквизитамДокумента" Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОтветственныеЛицаСрезПоследних.ФизЛицо.Наименование КАК НаименованиеФизлица,
		|	ОтветственныеЛицаСрезПоследних.Должность.Наименование КАК Должность,
		|	ОтветственныеЛицаСрезПоследних.ОтветственноеЛицо,
		|	ОтветственныеЛицаСрезПоследних.ФизЛицо КАК Физлицо,
		|	ОтветственныеЛицаСрезПоследних.Учреждение КАК Организация
		|ПОМЕСТИТЬ ВТОтветственныеЛица
		|ИЗ
		|	РегистрСведений.удуОтветственныеЛицаУчреждения.СрезПоследних(&ДатаДокумента, ОтветственноеЛицо В (ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.РуководительОрганизации), ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.РуководительКадровойСлужбы))) КАК ОтветственныеЛицаСрезПоследних
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Физлицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЕСТЬNULL(ВЫБОР
		|			КОГДА ПОДСТРОКА(ФИОФизЛицСрезПоследних.Имя, 1, 1) <> """"
		|				ТОГДА ПОДСТРОКА(ФИОФизЛицСрезПоследних.Имя, 1, 1) + "". ""
		|			ИНАЧЕ """"
		|		КОНЕЦ + ВЫБОР
		|			КОГДА ПОДСТРОКА(ФИОФизЛицСрезПоследних.Отчество, 1, 1) <> """"
		|				ТОГДА ПОДСТРОКА(ФИОФизЛицСрезПоследних.Отчество, 1, 1) + "". ""
		|			ИНАЧЕ """"
		|		КОНЕЦ + ФИОФизЛицСрезПоследних.Фамилия, ОтветственныеЛицаСрезПоследних.НаименованиеФизлица) КАК НаименованиеОтветственногоЛица,
		|	ОтветственныеЛицаСрезПоследних.Должность,
		|	ОтветственныеЛицаСрезПоследних.ОтветственноеЛицо,
		|	ОтветственныеЛицаСрезПоследних.Физлицо,
		|	ОтветственныеЛицаСрезПоследних.Организация КАК Организация
		|ПОМЕСТИТЬ ВТДанныеОбОтветственномЛице
		|ИЗ
		|	ВТОтветственныеЛица КАК ОтветственныеЛицаСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.удуФИОФизЛиц.СрезПоследних(
		|				&ДатаДокумента,
		|				ФизЛицо В
		|					(ВЫБРАТЬ
		|						Ответственные.Физлицо
		|					ИЗ
		|						ВТОтветственныеЛица КАК Ответственные)) КАК ФИОФизЛицСрезПоследних
		|		ПО ОтветственныеЛицаСрезПоследних.Физлицо = ФИОФизЛицСрезПоследних.ФизЛицо
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ГрафикОтпусковОрганизаций.Номер КАК НомерДок,
		|	ГрафикОтпусковОрганизаций.Дата КАК ДатаДок,
		|	ГрафикОтпусковОрганизаций.Организация.ОКПО КАК КодПоОКПО,
		|	ВЫРАЗИТЬ(ГрафикОтпусковОрганизаций.Организация.ПолноеНаименование КАК СТРОКА(300)) КАК НазваниеОрганизации,
		|	ВТДанныеОбОтветственномЛице.Должность КАК ДолжностьРуководителя,
		|	ВТДанныеОбОтветственномЛице.НаименованиеОтветственногоЛица КАК ФИОРуководителя,
		|	ГрафикОтпусковОрганизаций.Организация.Префикс,
		|	ГрафикОтпусковОрганизаций.Ссылка КАК Ссылка,
		|	ВТДанныеОбОтветственномЛице.ОтветственноеЛицо
		|ИЗ
		|	Документ.удуГрафикОтпусковОрганизаций КАК ГрафикОтпусковОрганизаций
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОбОтветственномЛице КАК ВТДанныеОбОтветственномЛице
		|		ПО ГрафикОтпусковОрганизаций.Организация = ВТДанныеОбОтветственномЛице.Организация
		|ГДЕ
		|	ГрафикОтпусковОрганизаций.Ссылка = &ДокументСсылка";
		
	ИначеЕсли Режим = "ПоТабличнойЧастиДокумента" Тогда
				
		Запрос.Текст ="ВЫБРАТЬ
		              |	ГрафикОтпусковОрганизацииРаботникиОрганизации.Сотрудник КАК Сотрудник,
		              |	ГрафикОтпусковОрганизацииРаботникиОрганизации.Сотрудник.Физлицо КАК Физлицо,
		              |	ГрафикОтпусковОрганизацииРаботникиОрганизации.Сотрудник.Код,
		              |	ГрафикОтпусковОрганизацииРаботникиОрганизации.Сотрудник.Наименование,
		              |	ГрафикОтпусковОрганизацииРаботникиОрганизации.ДатаНачала,
		              |	СУММА(КалендарьДнейВсего.КалендарныеДни) КАК Продолжительность,
		              |	ГрафикОтпусковОрганизацииРаботникиОрганизации.ДатаОкончания,
		              |	ГрафикОтпусковОрганизацииРаботникиОрганизации.НомерСтроки
		              |ПОМЕСТИТЬ ВТДанныеДокумента
		              |ИЗ
		              |	Документ.удуГрафикОтпусковОрганизаций.РаботникиОрганизации КАК ГрафикОтпусковОрганизацииРаботникиОрганизации
		              |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.удуРегламентированныйПроизводственныйКалендарь КАК КалендарьДнейВсего
		              |		ПО (КалендарьДнейВсего.ВидДня <> ЗНАЧЕНИЕ(Перечисление.удуВидыДнейПроизводственногоКалендаря.Праздник))
		              |			И (КалендарьДнейВсего.ДатаКалендаря МЕЖДУ ГрафикОтпусковОрганизацииРаботникиОрганизации.ДатаНачала И ГрафикОтпусковОрганизацииРаботникиОрганизации.ДатаОкончания)
		              |ГДЕ
		              |	ГрафикОтпусковОрганизацииРаботникиОрганизации.Ссылка = &ДокументСсылка
		              |
		              |СГРУППИРОВАТЬ ПО
		              |	ГрафикОтпусковОрганизацииРаботникиОрганизации.Сотрудник,
		              |	ГрафикОтпусковОрганизацииРаботникиОрганизации.Сотрудник.Физлицо,
		              |	ГрафикОтпусковОрганизацииРаботникиОрганизации.Сотрудник.Код,
		              |	ГрафикОтпусковОрганизацииРаботникиОрганизации.ДатаНачала,
		              |	ГрафикОтпусковОрганизацииРаботникиОрганизации.ДатаОкончания,
		              |	ГрафикОтпусковОрганизацииРаботникиОрганизации.НомерСтроки,
		              |	ГрафикОтпусковОрганизацииРаботникиОрганизации.Сотрудник.Наименование
		              |
		              |ИНДЕКСИРОВАТЬ ПО
		              |	Сотрудник
		              |;
		              |
		              |////////////////////////////////////////////////////////////////////////////////
		              |ВЫБРАТЬ
		              |	РаботникиОрганизацииСрезПоследних.Сотрудник КАК Сотрудник,
		              |	РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
		              |	РаботникиОрганизацииСрезПоследних.Должность КАК Должность,
		              |	РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизации.Наименование КАК ПодразделениеОрганизацииНаименование,
		              |	РаботникиОрганизацииСрезПоследних.Должность.Наименование КАК ДолжностьНаименование
		              |ПОМЕСТИТЬ ВТДанныеОРаботниках
		              |ИЗ
		              |	РегистрСведений.удуРаботникиОрганизаций.СрезПоследних(
		              |			&ДатаДокумента,
		              |			Сотрудник В
		              |				(ВЫБРАТЬ
		              |					ГрафикОтпусковОрганизацииРаботникиОрганизации.Сотрудник
		              |				ИЗ
		              |					ВТДанныеДокумента КАК ГрафикОтпусковОрганизацииРаботникиОрганизации)) КАК РаботникиОрганизацииСрезПоследних
		              |
		              |ИНДЕКСИРОВАТЬ ПО
		              |	Сотрудник
		              |;
		              |
		              |////////////////////////////////////////////////////////////////////////////////
		              |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		              |	ОтветственныеЛицаСрезПоследних.ФизЛицо.Наименование КАК ФизическоеЛицоНаименование,
		              |	ОтветственныеЛицаСрезПоследних.Должность.Наименование КАК Должность,
		              |	ОтветственныеЛицаСрезПоследних.ОтветственноеЛицо,
		              |	ОтветственныеЛицаСрезПоследних.ФизЛицо КАК ФизическоеЛицо,
		              |	ОтветственныеЛицаСрезПоследних.Учреждение КАК Организация,
		              |	ФизическиеЛица.Комментарий
		              |ПОМЕСТИТЬ ВТДанныеОбОтветственномЛице
		              |ИЗ
		              |	РегистрСведений.удуОтветственныеЛицаУчреждения.СрезПоследних(
		              |			&ДатаДокумента,
		              |			Учреждение В
		              |					(ВЫБРАТЬ
		              |						РаботникиОрганизации.ПодразделениеОрганизации.Организация
		              |					ИЗ
		              |						ВТДанныеОРаботниках КАК РаботникиОрганизации)
		              |				И ОтветственноеЛицо = ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.РуководительКадровойСлужбы)) КАК ОтветственныеЛицаСрезПоследних
		              |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
		              |		ПО ОтветственныеЛицаСрезПоследних.ФизЛицо = ФизическиеЛица.Ссылка
		              |
		              |ИНДЕКСИРОВАТЬ ПО
		              |	ФизическоеЛицо
		              |;
		              |
		              |////////////////////////////////////////////////////////////////////////////////
		              |ВЫБРАТЬ
		              |	ГрафикОтпусковОрганизацииРаботники.НомерСтроки КАК НомерСтроки,
		              |	ЕСТЬNULL(ФИОФизЛиц.Фамилия + "" "" + ФИОФизЛиц.Имя + "" "" + ФИОФизЛиц.Отчество, ГрафикОтпусковОрганизацииРаботники.СотрудникНаименование) КАК Работник,
		              |	ГрафикОтпусковОрганизацииРаботники.ДатаНачала,
		              |	ГрафикОтпусковОрганизацииРаботники.ДатаОкончания,
		              |	ГрафикОтпусковОрганизацииРаботники.Продолжительность,
		              |	ГрафикОтпусковОрганизацииРаботники.СотрудникКод КАК ТабельныйНомер,
		              |	ОтветственныеЛицаОрганизацийСрезПоследних.Должность КАК ДолжностьРуководителя,
		              |	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия + "" "" + ФИОФизЛицСрезПоследних.Имя + "" "" + ФИОФизЛицСрезПоследних.Отчество, ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицоНаименование) КАК ФИОРуководителя,
		              |	РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизации,
		              |	РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизацииНаименование,
		              |	РаботникиОрганизацииСрезПоследних.ДолжностьНаименование,
		              |	РаботникиОрганизацииСрезПоследних.Должность,
		              |	ГрафикОтпусковОрганизацииРаботники.Сотрудник,
		              |	РаботникиОрганизацииСрезПоследних.Сотрудник.Физлицо.Пол КАК ФизлицоПол
		              |ИЗ
		              |	ВТДанныеДокумента КАК ГрафикОтпусковОрганизацииРаботники
		              |		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОРаботниках КАК РаботникиОрганизацииСрезПоследних
		              |			ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОбОтветственномЛице КАК ОтветственныеЛицаОрганизацийСрезПоследних
		              |				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.удуФИОФизЛиц.СрезПоследних(
		              |						&ДатаДокумента,
		              |						ФизЛицо В
		              |							(ВЫБРАТЬ
		              |								Ответственные.ФизическоеЛицо
		              |							ИЗ
		              |								ВТДанныеОбОтветственномЛице КАК Ответственные)) КАК ФИОФизЛицСрезПоследних
		              |				ПО ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо = ФИОФизЛицСрезПоследних.ФизЛицо
		              |			ПО РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизации.Организация = ОтветственныеЛицаОрганизацийСрезПоследних.Организация
		              |		ПО ГрафикОтпусковОрганизацииРаботники.Сотрудник = РаботникиОрганизацииСрезПоследних.Сотрудник
		              |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.удуФИОФизЛиц.СрезПоследних(
		              |				&ДатаДокумента,
		              |				Физлицо В
		              |					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		              |						ГрафикОтпусковОрганизацииРаботникиОрганизации.Физлицо
		              |					ИЗ
		              |						ВТДанныеДокумента КАК ГрафикОтпусковОрганизацииРаботникиОрганизации)) КАК ФИОФизЛиц
		              |		ПО ГрафикОтпусковОрганизацииРаботники.Физлицо = ФИОФизЛиц.ФизЛицо
		              |
		              |СГРУППИРОВАТЬ ПО
		              |	ГрафикОтпусковОрганизацииРаботники.НомерСтроки,
		              |	ГрафикОтпусковОрганизацииРаботники.ДатаНачала,
		              |	ГрафикОтпусковОрганизацииРаботники.ДатаОкончания,
		              |	ОтветственныеЛицаОрганизацийСрезПоследних.Должность,
		              |	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия + "" "" + ФИОФизЛицСрезПоследних.Имя + "" "" + ФИОФизЛицСрезПоследних.Отчество, ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицоНаименование),
		              |	РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизацииНаименование,
		              |	РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизации,
		              |	РаботникиОрганизацииСрезПоследних.ДолжностьНаименование,
		              |	РаботникиОрганизацииСрезПоследних.Должность,
		              |	ГрафикОтпусковОрганизацииРаботники.Сотрудник,
		              |	ГрафикОтпусковОрганизацииРаботники.СотрудникКод,
		              |	ГрафикОтпусковОрганизацииРаботники.Продолжительность,
		              |	ЕСТЬNULL(ФИОФизЛиц.Фамилия + "" "" + ФИОФизЛиц.Имя + "" "" + ФИОФизЛиц.Отчество, ГрафикОтпусковОрганизацииРаботники.СотрудникНаименование),
		              |	РаботникиОрганизацииСрезПоследних.Сотрудник.Физлицо.Пол
		              |
		              |УПОРЯДОЧИТЬ ПО
		              |	НомерСтроки";

	Иначе
		Возврат Неопределено;
	КонецЕсли;

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросДляПечати()

// Функция формирует табличный документ с печатной формой,
	//
	// Возвращаемое значение:
	//  Табличный документ - печатная форма
	//
Функция СформироватьПечатнуюФормуТ7(МассивОбъектов, ОбъектыПечати,ИмяМакета)
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ПолеСлева = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_удуГрафикОтпусковОрганизаций_Т7";
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;

	ПервыйДокумент = Истина;
	
	Для Каждого  ДокументДляПечати из МассивОбъектов цикл
	
	// получаем данные для печати
	ВыборкаДляШапки = СформироватьЗапросДляПечати("ПоРеквизитамДокумента",ДокументДляПечати).Выбрать();
	ВыборкаРаботники= СформироватьЗапросДляПечати("ПоТабличнойЧастиДокумента",ДокументДляПечати).Выбрать();
	
	// Макет необходимо получать для каждого документа, т.к. размеры колонок изменяются динамически.
	Макет = УправлениеПечатью.ПолучитьМакет(ИмяМакета);
	
	Если Не ПервыйДокумент Тогда
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЕсли;
	
	ПервыйДокумент    = Ложь;
	НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
			
	// Подсчитываем количество страниц документа - для корректного разбиения на страницы.
	ВсегоСтрокДокумента = ВыборкаРаботники.Количество();

	// запоминаем области макета
	ОбластьМакетаШапка = Макет.ПолучитьОбласть("Шапка"); // Шапка документа.
	ПовторятьПриПечатиСтроки = Макет.ПолучитьОбласть("ПовторятьПриПечати"); // повторяющаяся шапка страницы
	ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал");// Подвал документа
	ОбластьМакета = Макет.ПолучитьОбласть("СтрокаРаботник"); // строка работника

	// массив с двумя строками - для разбиения на страницы
	ВыводимыеОбласти = Новый Массив();
	ВыводимыеОбласти.Добавить(ОбластьМакета);
	
	// выводим данные о руководителях организации
	Если ВыборкаДляШапки.Следующий() Тогда
		
		ОбластьМакетаШапка.Параметры.НазваниеОрганизации = ВыборкаДляШапки.НазваниеОрганизации;
			ОбластьМакетаШапка.Параметры.КодПоОКПО 			= ВыборкаДляШапки.КодПоОКПО;
			ОбластьМакетаШапка.Параметры.НомерДок 			= ВыборкаДляШапки.НомерДок;
			ОбластьМакетаШапка.Параметры.ДатаДок 			= ВыборкаДляШапки.ДатаДок;
			
			ВыборкаДляШапки.Сбросить();
			Если ВыборкаДляШапки.НайтиСледующий(Перечисления.ОтветственныеЛицаОрганизаций.РуководительОрганизации,"ОтветственноеЛицо") Тогда
				ОбластьМакетаШапка.Параметры.Заполнить(ВыборкаДляШапки);
			КонецЕсли;
			
			// Для подвала
			ВыборкаДляШапки.Сбросить();
			Если ВыборкаДляШапки.НайтиСледующий(Перечисления.ОтветственныеЛицаОрганизаций.РуководительКадровойСлужбы,"ОтветственноеЛицо") Тогда
				ОбластьМакетаПодвал.Параметры.Заполнить(ВыборкаДляШапки);
			КонецЕсли;
	КонецЕсли;

	// Начинаем формировать выходной документ
	ТабДокумент.Вывести(ОбластьМакетаШапка); // Шапка документа.

	ВыведеноСтрок = 0;
	// выводим строки по работникам
	Пока ВыборкаРаботники.Следующий() Цикл

		// Данные по работнику.
		ОбластьМакета.Параметры.Заполнить(ВыборкаРаботники);
		
		
		// разбиение на страницы
		ВыведеноСтрок = ВыведеноСтрок + 1;
		
		// Проверим, уместится ли строка на странице или надо открывать новую страницу
		ВывестиПодвалЛиста = Не удуОбщегоНазначенияСервер.ПроверитьВыводТабличногоДокумента(ТабДокумент, ВыводимыеОбласти);
		Если Не ВывестиПодвалЛиста и ВыведеноСтрок = ВсегоСтрокДокумента Тогда
			ВыводимыеОбласти.Добавить(ОбластьМакетаПодвал);
			ВывестиПодвалЛиста = Не удуОбщегоНазначенияСервер.ПроверитьВыводТабличногоДокумента(ТабДокумент, ВыводимыеОбласти);
		КонецЕсли;
		Если ВывестиПодвалЛиста Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ТабДокумент.Вывести(ПовторятьПриПечатиСтроки);
		КонецЕсли;
		
		ТабДокумент.Вывести(ОбластьМакета);
		
	КонецЦикла;

	// если не было ни одного работника - выводим пустой бланк
	ВыводимыеОбласти = Новый Массив();
	ВыводимыеОбласти.Добавить(ОбластьМакета);
	ВыводимыеОбласти.Добавить(ОбластьМакетаПодвал);
	Для Сч = 1 По ОбластьМакета.Параметры.Количество() Цикл
		ОбластьМакета.Параметры.Установить(Сч - 1,""); 
	КонецЦикла;
	ОбластьМакета.Параметры.Работник = " " + Символы.ПС + " ";
	Пока удуОбщегоНазначенияСервер.ПроверитьВыводТабличногоДокумента(ТабДокумент, ВыводимыеОбласти, Ложь) Цикл
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла;

	// выводим предварительно подготовленный Подвал документа.
	ТабДокумент.Вывести(ОбластьМакетаПодвал);

	УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДляШапки.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДокумент;
	
КонецФункции // СформироватьПечатнуюФормуАвансовогоОтчета()




 // Процедура печати документа.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечатьТ7") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ПечатьТ7",
			"Форма Т-7",
			СформироватьПечатнуюФормуТ7(МассивОбъектов, ОбъектыПечати,"Документ.удуГрафикОтпусковОрганизаций.ПФ_MXL_Т7_от_5_1_2004")
		);
		
	КонецЕсли;
	
КонецПроцедуры // Печать()
