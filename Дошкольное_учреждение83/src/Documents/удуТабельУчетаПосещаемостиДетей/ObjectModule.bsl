
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ 

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
		
	// Проверка наличия существующих записей в регистре
	Движения.удуСведенияОПосещаемостиДОУ.Очистить();
	Движения.удуСведенияОПосещаемостиДОУ.Записать();
	Запрос =  Новый Запрос("ВЫБРАТЬ
	|	КОЛИЧЕСТВО(*) КАК КоличествоЗаписей
	|ИЗ
	|	РегистрСведений.удуСведенияОПосещаемостиДОУ КАК удуСведенияОПосещаемостиДОУ
	|ГДЕ
	|	удуСведенияОПосещаемостиДОУ.Период МЕЖДУ &ПериодРегистрации И КОНЕЦПЕРИОДА(&ПериодРегистрации, МЕСЯЦ)
	|	И удуСведенияОПосещаемостиДОУ.Учреждение = &Учреждение
	|	И удуСведенияОПосещаемостиДОУ.Группа = &Группа");
	Запрос.УстановитьПараметр("ПериодРегистрации",ПериодРегистрации);
	Запрос.УстановитьПараметр("Учреждение",Учреждение);
	Запрос.УстановитьПараметр("Группа",ГруппаДетскогоУчреждения);
	Результат = Запрос.Выполнить().Выбрать();
	Результат.Следующий();
	
	Если Результат.КоличествоЗаписей <> 0 Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "За период " + Формат(ПериодРегистрации, "ДФ='ММММ гггг'") + " для выбранной группы уже введены сведения о посещении.
		| Сначала необходимо удалить все документы """"Табель посещаемости"""" в данном периоде, по выбранной группе.";
		Сообщение.Сообщить();
	КонецЕсли;	
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Календарь.Дата,
	|	Календарь.ОсновноеЗначение
	|ПОМЕСТИТЬ ВТ_РабочиеДни
	|ИЗ
	|	РегистрСведений.удуГрафикиРаботыПоВидамВремени КАК Календарь
	|ГДЕ
	|	Календарь.Дата МЕЖДУ &ПериодРегистрации И КОНЕЦПЕРИОДА(&ПериодРегистрации, МЕСЯЦ)
	|	И Календарь.ГрафикРаботы = &График
	|	И Календарь.Месяц = &ПериодРегистрации
	|	И Календарь.План
	|	И Календарь.ВидУчетаВремени = ЗНАЧЕНИЕ(Перечисление.удуВидыУчетаВремени.ПоДням)
	|	И Календарь.ОсновноеЗначение > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	удуТабельУчетаПосещаемостиДетейНевыходы.Ребенок,
	|	удуТабельУчетаПосещаемостиДетейНевыходы.ДатаНевыхода КАК ДатаНевыхода,
	|	удуТабельУчетаПосещаемостиДетейНевыходы.ПричиныНепосещения
	|ПОМЕСТИТЬ ВТ_ДниНевыхода
	|ИЗ
	|	Документ.удуТабельУчетаПосещаемостиДетей.Невыходы КАК удуТабельУчетаПосещаемостиДетейНевыходы
	|ГДЕ
	|	удуТабельУчетаПосещаемостиДетейНевыходы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоставГруппОбученияСрезПоследних.Ребенок КАК Ребенок,
	|	НАЧАЛОПЕРИОДА(&ПериодРегистрации, МЕСЯЦ) КАК Период,
	|	ИСТИНА КАК Актуальность
	|ПОМЕСТИТЬ ВТ_ПериодыИзмененийСостава
	|ИЗ
	|	РегистрСведений.удуСведенияОЗачисленииРебенкаВГруппу.СрезПоследних(НАЧАЛОПЕРИОДА(&ПериодРегистрации, МЕСЯЦ), Группа = &Группа) КАК СоставГруппОбученияСрезПоследних
	|ГДЕ
	|	СоставГруппОбученияСрезПоследних.СостояниеУчетаВГруппе = ЗНАЧЕНИЕ(Перечисление.удуСостояниеРебенкаНаУчетеВГруппе.ПринятВГруппу)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СоставГруппОбучения.Ребенок,
	|	СоставГруппОбучения.Период,
	|	ВЫБОР
	|		КОГДА СоставГруппОбучения.СостояниеУчетаВГруппе = ЗНАЧЕНИЕ(Перечисление.удуСостояниеРебенкаНаУчетеВГруппе.ПринятВГруппу)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|ИЗ
	|	РегистрСведений.удуСведенияОЗачисленииРебенкаВГруппу КАК СоставГруппОбучения
	|ГДЕ
	|	СоставГруппОбучения.Период МЕЖДУ НАЧАЛОПЕРИОДА(&ПериодРегистрации, МЕСЯЦ) И КОНЕЦПЕРИОДА(&ПериодРегистрации, МЕСЯЦ)
	|	И СоставГруппОбучения.Группа = &Группа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВлЗап.НачалоПериода,
	|	КОНЕЦПЕРИОДА(ВлЗап.КонецПериода, ДЕНЬ) КАК КонецПериода,
	|	ВТ_ПериодыИзмененийСостава.Ребенок
	|ПОМЕСТИТЬ ВТ_Дети
	|ИЗ
	|	ВТ_ПериодыИзмененийСостава КАК ВТ_ПериодыИзмененийСостава
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ВТ_ПериодыИзмененийСостава.Период КАК НачалоПериода,
	|			МИНИМУМ(ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ВТ_ПериодыИзмененийСостава1.Период, ДЕНЬ, -1), НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(&ПериодРегистрации, МЕСЯЦ), ДЕНЬ))) КАК КонецПериода,
	|			ВТ_ПериодыИзмененийСостава.Ребенок КАК Ребенок
	|		ИЗ
	|			ВТ_ПериодыИзмененийСостава КАК ВТ_ПериодыИзмененийСостава
	|				ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодыИзмененийСостава КАК ВТ_ПериодыИзмененийСостава1
	|				ПО ВТ_ПериодыИзмененийСостава.Период < ВТ_ПериодыИзмененийСостава1.Период
	|					И ВТ_ПериодыИзмененийСостава.Ребенок = ВТ_ПериодыИзмененийСостава1.Ребенок
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ВТ_ПериодыИзмененийСостава.Период,
	|			ВТ_ПериодыИзмененийСостава.Ребенок) КАК ВлЗап
	|		ПО ВТ_ПериодыИзмененийСостава.Период = ВлЗап.НачалоПериода
	|			И ВТ_ПериодыИзмененийСостава.Ребенок = ВлЗап.Ребенок
	|ГДЕ
	|	ВТ_ПериодыИзмененийСостава.Актуальность
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Учреждение,
	|	&Воспитатель,
	|	&Группа,
	|	ВложенныйЗапрос.Дата КАК Период,
	|	ВложенныйЗапрос.Ребенок,
	|	ЕСТЬNULL(ВТ_ДниНевыхода.ПричиныНепосещения, ЗНАЧЕНИЕ(Справочник.удуПричиныПропускаЗанятий.ПустаяСсылка)) КАК ПричинаПропускаЗанятия,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_ДниНевыхода.ПричиныНепосещения, ЗНАЧЕНИЕ(Справочник.удуПричиныПропускаЗанятий.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.удуПричиныПропускаЗанятий.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПризнакПрисутствия
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ_РабочиеДни.Дата КАК Дата,
	|		ВТ_Дети.Ребенок КАК Ребенок
	|	ИЗ
	|		ВТ_РабочиеДни КАК ВТ_РабочиеДни
	|			ПОЛНОЕ СОЕДИНЕНИЕ ВТ_Дети КАК ВТ_Дети
	|			ПО (ВТ_РабочиеДни.Дата МЕЖДУ ВТ_Дети.НачалоПериода И ВТ_Дети.КонецПериода)) КАК ВложенныйЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДниНевыхода КАК ВТ_ДниНевыхода
	|		ПО ВложенныйЗапрос.Дата = ВТ_ДниНевыхода.ДатаНевыхода
	|			И ВложенныйЗапрос.Ребенок = ВТ_ДниНевыхода.Ребенок";
	
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("Воспитатель", Воспитатель);	
	Запрос.УстановитьПараметр("График",ГруппаДетскогоУчреждения.ГрафикРаботы);
	Запрос.УстановитьПараметр("Группа",ГруппаДетскогоУчреждения);
	Запрос.УстановитьПараметр("Учреждение",Учреждение);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Движения.удуСведенияОПосещаемостиДОУ.Записывать = Истина;
	Движения.удуСведенияОПосещаемостиДОУ.Загрузить(РезультатЗапроса.Выгрузить());
	
	
КонецПроцедуры

