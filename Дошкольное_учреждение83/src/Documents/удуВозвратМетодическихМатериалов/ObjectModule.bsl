
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.удуМетодическиеМатериалыНаРуках.Записывать = Истина;	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	удуВозвратМетодическихМатериалов.Читатель КАК Читатель,
	|	удуВозвратМетодическихМатериаловУчетЕдКВозврату.КоличествоКСдаче КАК Количество,
	|	удуВозвратМетодическихМатериаловУчетЕдКВозврату.ДатаВозврата КАК ПлановаяДатаВозврата,
	|	удуВозвратМетодическихМатериаловУчетЕдКВозврату.УчетнаяЕдиница,
	|	удуВозвратМетодическихМатериаловУчетЕдКВозврату.ИнвентарныйНомер,
	|	удуВозвратМетодическихМатериаловУчетЕдКВозврату.ПоПричине КАК ПричинаВыдачи,
	|	удуВозвратМетодическихМатериалов.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	удуВозвратМетодическихМатериаловУчетЕдКВозврату.НомерСтроки
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	Документ.удуВозвратМетодическихМатериалов.УчетЕдКВозврату КАК удуВозвратМетодическихМатериаловУчетЕдКВозврату
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.удуВозвратМетодическихМатериалов КАК удуВозвратМетодическихМатериалов
	|		ПО удуВозвратМетодическихМатериаловУчетЕдКВозврату.Ссылка = удуВозвратМетодическихМатериалов.Ссылка
	|ГДЕ
	|	удуВозвратМетодическихМатериалов.Ссылка = &Ссылка
	|	И удуВозвратМетодическихМатериаловУчетЕдКВозврату.КоличествоКСдаче > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ.Читатель,
	|	ВТ.Количество,
	|	ВТ.ПлановаяДатаВозврата,
	|	ВТ.УчетнаяЕдиница,
	|	ВТ.ИнвентарныйНомер,
	|	ВТ.ПричинаВыдачи,
	|	ВТ.Период,
	|	ВТ.ВидДвижения
	|ИЗ
	|	ВТ КАК ВТ";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Результат = Запрос.Выполнить();
	Движения.удуМетодическиеМатериалыНаРуках.Загрузить(Результат.Выгрузить());
	Движения.удуМетодическиеМатериалыНаРуках.БлокироватьДляИзменения = Истина;
	Движения.Записать();
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВТ.УчетнаяЕдиница,
	               |	ВТ.ИнвентарныйНомер,
	               |	ВТ.Читатель,
	               |	ВТ.ПлановаяДатаВозврата,
	               |	ВЫБОР
	               |		КОГДА ВТ.ПричинаВыдачи = ЗНАЧЕНИЕ(Справочник.удуПричиныВыдачиМетодическихМатериалов.ПустаяСсылка)
	               |			ТОГДА ""<НетПричины>""
	               |		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ВТ.ПричинаВыдачи)
	               |	КОНЕЦ КАК ПричинаВыдачи,
	               |	-ЕСТЬNULL(удуМетодическиеМатериалыНаРукахОстатки.КоличествоОстаток, 0) КАК Излишек,
	               |	ВТ.НомерСтроки КАК НомерСтроки
	               |ИЗ
	               |	ВТ КАК ВТ
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.удуМетодическиеМатериалыНаРуках.Остатки(
	               |				&Период,
	               |				(Читатель, УчетнаяЕдиница, ПричинаВыдачи, ИнвентарныйНомер, ПлановаяДатаВозврата) В
	               |					(ВЫБРАТЬ
	               |						ВТ.Читатель,
	               |						ВТ.УчетнаяЕдиница,
	               |						ВТ.ПричинаВыдачи,
	               |						ВТ.ИнвентарныйНомер,
	               |						ВТ.ПлановаяДатаВозврата
	               |					ИЗ
	               |						ВТ КАК ВТ)) КАК удуМетодическиеМатериалыНаРукахОстатки
	               |		ПО ВТ.Читатель = удуМетодическиеМатериалыНаРукахОстатки.Читатель
	               |			И ВТ.УчетнаяЕдиница = удуМетодическиеМатериалыНаРукахОстатки.УчетнаяЕдиница
	               |			И ВТ.ИнвентарныйНомер = удуМетодическиеМатериалыНаРукахОстатки.ИнвентарныйНомер
	               |			И ВТ.ПлановаяДатаВозврата = удуМетодическиеМатериалыНаРукахОстатки.ПлановаяДатаВозврата
	               |			И ВТ.ПричинаВыдачи = удуМетодическиеМатериалыНаРукахОстатки.ПричинаВыдачи
	               |ГДЕ
	               |	удуМетодическиеМатериалыНаРукахОстатки.КоличествоОстаток < 0";
	Запрос.УстановитьПараметр("Период",
	?(Режим = РежимПроведенияДокумента.Оперативный, Неопределено, Новый Граница(МоментВремени(),ВидГраницы.Включая)));
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Выборка = результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			Сообщение.Текст = "В строке " + Выборка.НомерСтроки + " указано больше литературы, чем выдавалось 
			|по причине " + Выборка.ПричинаВыдачи + ", на "
			+ СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(Выборка.Излишек,"единицу,единицы,единиц");
			
			Сообщение.Поле = "УчетЕдКВозврату[" + (Выборка.НомерСтроки-1) + "].УчетнаяЕдиница";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
		КонецЦикла;
	КонецЕсли;	
	
	

КонецПроцедуры

