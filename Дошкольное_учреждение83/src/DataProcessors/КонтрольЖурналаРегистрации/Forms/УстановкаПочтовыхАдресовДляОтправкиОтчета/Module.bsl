////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	EMailАдресаПолучателейОтчета = КонтрольЖурналаРегистрацииСлужебный.ПолучитьАдресатовПолученияОтчетаПоЖурналуРегистрации();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура КнопкаОК(Команда)
	
	Перем СообщениеОбОшибке;
	
	// Пытается сохранить введенную строку почтовых адресов
	Результат = СохранитьEMailАдресаПолучателейОтчета(EMailАдресаПолучателейОтчета, СообщениеОбОшибке);
	
	// Закрывает форму если сохранение прошло успешно
	Если Не Результат Тогда
		Если Не ПустаяСтрока(СообщениеОбОшибке) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, , "EMailАдресаПолучателейОтчета");
		КонецЕсли;
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервереБезКонтекста
Функция СохранитьEMailАдресаПолучателейОтчета(EMailАдресаПолучателейОтчета, СообщениеОбОшибке = "")
	
	Если ЗначениеЗаполнено(EMailАдресаПолучателейОтчета) Тогда
		Попытка
			ОбщегоНазначенияКлиентСервер.РазобратьСтрокуСПочтовымиАдресами(EMailАдресаПолучателейОтчета);
		Исключение
			СообщениеОбОшибке = НСтр("ru = 'Не верно задан формат почтового адреса'");
			Возврат Ложь;
		КонецПопытки;
	КонецЕсли;
	
	Попытка
		КонтрольЖурналаРегистрацииСлужебный.УстановитьАдресатовПолученияОтчетаПоЖурналуРегистрации(EMailАдресаПолучателейОтчета);
	Исключение
		ОшибкаИнфо = ИнформацияОбОшибке();
		Если ОшибкаИнфо.Причина = Неопределено Тогда
			СообщениеОбОшибке =ОшибкаИнфо.Описание;
		Иначе
			СообщениеОбОшибке = ОшибкаИнфо.Причина.Описание;
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке);
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

