////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Инициализация реквизитов периода.
	ОкончаниеПериодаВыборки = ТекущаяДатаСеанса();
	НачалоПериодаВыборки    = ОкончаниеПериодаВыборки - 24*60*60;
	СформироватьОтчетНаСервере();
	ИдентификаторРегламентногоЗадания = РегламентныеЗадания.НайтиПредопределенное(
		Метаданные.РегламентныеЗадания.КонтрольЖурналаРегистрации).УникальныйИдентификатор;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОбновитьОтчетНаФорме(Команда)
	
	ОчиститьСообщения();
	ФормироватьОтчет = Истина;
	
	Если Не ЗначениеЗаполнено(НачалоПериодаВыборки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не заполнено начало периода выборки.'"),
			,
			"НачалоПериодаВыборки");
		ФормироватьОтчет = Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ОкончаниеПериодаВыборки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не заполнено окончание периода выборки.'"),
			,
			"ОкончаниеПериодаВыборки");
		ФормироватьОтчет = Ложь;
	КонецЕсли;
	
	Если ФормироватьОтчет Тогда
		СформироватьОтчетНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УказатьПолучателейОтчета(Команда)
	
	ОткрытьФорму("Обработка.КонтрольЖурналаРегистрации.Форма.УстановкаПочтовыхАдресовДляОтправкиОтчета");
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьРасписаниеРегламентногоЗадания(Команда)
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(
		РегламентныеЗаданияКлиент.ПолучитьРасписаниеРегламентногоЗадания(ИдентификаторРегламентногоЗадания) );
	
	Если Диалог.ОткрытьМодально() Тогда
		РегламентныеЗаданияКлиент.УстановитьРасписаниеРегламентногоЗадания(ИдентификаторРегламентногоЗадания, Диалог.Расписание);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура СформироватьОтчетНаСервере()
	
	РезультатФормированияОтчета = Обработки.КонтрольЖурналаРегистрации.СформироватьОтчет(
		НачалоПериодаВыборки, ОкончаниеПериодаВыборки);
	
	Отчет = РезультатФормированияОтчета.Отчет;
	
КонецПроцедуры


