
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипДокумента = Параметры.ТипДокумента;
	Если ТипДокумента = "ВходящийДокумент" Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВходящиеДокументы.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ВходящиеДокументы КАК ВходящиеДокументы
		|ГДЕ
		|	ВходящиеДокументы.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыВходящихДокументов.ПустаяСсылка)";
		
		Элементы.Декорация.Заголовок = 
			НСТР("ru = 'Выполняется включение использования видов входящих документов.'") + Символы.ПС + 
			НСТР("ru = 'Укажите вид документа для заполнения существующих документов с незаполненным видом.'");
			
		ВидДокумента = Справочники.ВидыВходящихДокументов.ПустаяСсылка();
			
	ИначеЕсли ТипДокумента = "ИсходящийДокумент" Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ИсходящиеДокументы.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ИсходящиеДокументы КАК ИсходящиеДокументы
		|ГДЕ
		|	ИсходящиеДокументы.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыИсходящихДокументов.ПустаяСсылка)";
		
		Элементы.Декорация.Заголовок = 
			НСТР("ru = 'Выполняется включение использования видов исходящих документов.'") + Символы.ПС + 
			НСТР("ru = 'Укажите вид документа для заполнения существующих документов с незаполненным видом.'");
			
		ВидДокумента = Справочники.ВидыИсходящихДокументов.ПустаяСсылка();
			
	ИначеЕсли ТипДокумента = "ВнутреннийДокумент" Тогда	
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВнутренниеДокументы.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ВнутренниеДокументы КАК ВнутренниеДокументы
		|ГДЕ
		|	ВнутренниеДокументы.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыВнутреннихДокументов.ПустаяСсылка)";
		
		Элементы.Декорация.Заголовок = 
			НСТР("ru = 'Выполняется включение использования видов внутренних документов.'") + Символы.ПС + 
			НСТР("ru = 'Укажите вид документа для заполнения существующих документов с незаполненным видом.'");
			
		ВидДокумента = Справочники.ВидыВнутреннихДокументов.ПустаяСсылка();
		
	КонецЕсли;	
	
	Запрос = Новый Запрос(ТекстЗапроса);
	ЗначениеВРеквизитФормы(Запрос.Выполнить().Выгрузить(), "СписокДокументов");
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Если Не ЗначениеЗаполнено(ВидДокумента) Тогда 
		Предупреждение(НСтр("ru= 'Не указан ""Вид документа""'"));
		Возврат;
	КонецЕсли;	
	
	ЗаполнитьДокументы();
	Закрыть(КодВозвратаДиалога.ОК);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДокументы()
	
	НачатьТранзакцию();
	Попытка 
		Для Каждого Строка Из СписокДокументов Цикл
			ДокументОбъект = Строка.Ссылка.ПолучитьОбъект();
			ДокументОбъект.ВидДокумента = ВидДокумента;
			ДокументОбъект.Записать();
		КонецЦикла;	
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры