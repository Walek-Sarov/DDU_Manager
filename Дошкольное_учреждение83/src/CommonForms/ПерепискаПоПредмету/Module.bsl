
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьДерево();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Для Каждого ЭлементДерева Из ДеревоДокументов.ПолучитьЭлементы() Цикл
		Элементы.ДеревоДокументов.Развернуть(ЭлементДерева.ПолучитьИдентификатор(), Истина);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПодчиненныеДокументы(СтрокаДерева)
	
	Ссылка = СтрокаДерева.Ссылка;
	
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.ВходящиеДокументы") Тогда 
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИсходящиеДокументы.Ссылка КАК Ссылка,
		|	ИсходящиеДокументы.Заголовок КАК Заголовок,
		|	ИсходящиеДокументы.РегистрационныйНомер КАК РегистрационныйНомер,
		|	ИсходящиеДокументы.ДатаРегистрации КАК ДатаРегистрации,
		|	1 КАК ИндексКартинки
		|ИЗ
		|	Справочник.ИсходящиеДокументы КАК ИсходящиеДокументы
		|ГДЕ
		|	ИсходящиеДокументы.ВОтветНа = &Ссылка
		|	И ИсходящиеДокументы.ПредметПереписки = &ПредметПереписки
		|	И (НЕ ИсходящиеДокументы.ПометкаУдаления)
		|	И ИсходящиеДокументы.РегистрационныйНомер <> """"";
		//|	И ИсходящиеДокументы.Отправлен";
		
	ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.ИсходящиеДокументы") Тогда 
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВходящиеДокументы.Ссылка КАК Ссылка,
		|	ВходящиеДокументы.Заголовок КАК Заголовок,
		|	ВходящиеДокументы.РегистрационныйНомер КАК РегистрационныйНомер,
		|	ВходящиеДокументы.ДатаРегистрации КАК ДатаРегистрации,
		|	0 КАК ИндексКартинки
		|ИЗ
		|	Справочник.ВходящиеДокументы КАК ВходящиеДокументы
		|ГДЕ
		|	ВходящиеДокументы.ВОтветНа = &Ссылка
		|	И ВходящиеДокументы.ПредметПереписки = &ПредметПереписки
		|	И (НЕ ВходящиеДокументы.ПометкаУдаления)";
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ПредметПереписки", Параметры.ПредметПереписки);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = СтрокаДерева.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка); 
		
		ЗаполнитьПодчиненныеДокументы(НоваяСтрока);
	КонецЦикла;	
	
КонецПроцедуры	

&НаСервере
Функция ЗаполнитьДерево()
	
	Дерево = РеквизитФормыВЗначение("ДеревоДокументов");
	Дерево.Строки.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВходящийДокумент.Ссылка КАК Ссылка,
	|	ВходящийДокумент.ДатаРегистрации,
	|	ВходящийДокумент.РегистрационныйНомер,
	|	ВходящийДокумент.Заголовок
	|ИЗ
	|	Справочник.ВходящиеДокументы КАК ВходящийДокумент
	|ГДЕ
	|	ВходящийДокумент.ПредметПереписки = &ПредметПереписки
	|	И (ВходящийДокумент.ВОтветНа = ЗНАЧЕНИЕ(Справочник.ИсходящиеДокументы.ПустаяСсылка)
	|			ИЛИ ВходящийДокумент.ВОтветНа.ПредметПереписки <> &ПредметПереписки)
	|	И (НЕ ВходящийДокумент.ПометкаУдаления)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИсходящийДокумент.Ссылка,
	|	ИсходящийДокумент.ДатаРегистрации,
	|	ИсходящийДокумент.РегистрационныйНомер,
	|	ИсходящийДокумент.Заголовок
	|ИЗ
	|	Справочник.ИсходящиеДокументы КАК ИсходящийДокумент
	|ГДЕ
	|	ИсходящийДокумент.ПредметПереписки = &ПредметПереписки
	|	И (ИсходящийДокумент.ВОтветНа = ЗНАЧЕНИЕ(Справочник.ВходящиеДОкументы.ПустаяСсылка)
	|			ИЛИ ИсходящийДокумент.ВОтветНа.ПредметПереписки <> &ПредметПереписки)
	|	И (НЕ ИсходящийДокумент.ПометкаУдаления)
	|	И ИсходящийДокумент.РегистрационныйНомер <> """"";
	//|	И ИсходящийДокумент.Отправлен";
	Запрос.УстановитьПараметр("ПредметПереписки", Параметры.ПредметПереписки);
	
	ТаблДокументов = Запрос.Выполнить().Выгрузить();
	ТаблДокументов.Сортировать("ДатаРегистрации");
	
	Для Каждого Строка Из ТаблДокументов Цикл
		
		НоваяСтрока = Дерево.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка); 
		
		Если ТипЗнч(НоваяСтрока.Ссылка) = Тип("СправочникСсылка.ВходящиеДокументы") Тогда 
			НоваяСтрока.ИндексКартинки = 0;
		ИначеЕсли ТипЗнч(НоваяСтрока.Ссылка) = Тип("СправочникСсылка.ИсходящиеДокументы") Тогда 
			НоваяСтрока.ИндексКартинки = 1;
		КонецЕсли;
		
		ЗаполнитьПодчиненныеДокументы(НоваяСтрока);
		
	КонецЦикла;	
	
	// 
	ЗначениеВРеквизитФормы(Дерево, "ДеревоДокументов");
	
КонецФункции

&НаКлиенте
Процедура ДеревоДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ДеревоДокументов.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда 
		ОткрытьЗначение(ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ЗаполнитьДерево();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ДокументИзменен" Тогда
		Если ТипЗнч(Параметр) = Тип("СправочникСсылка.ВходящиеДокументы") 
		 Или ТипЗнч(Параметр) = Тип("СправочникСсылка.ИсходящиеДокументы") Тогда 
			Если ОбщегоНазначения.ПолучитьЗначениеРеквизита(Параметр, "ПредметПереписки") = Параметры.ПредметПереписки Тогда 
				
				ЗаполнитьДерево(); 
				
				Для Каждого ЭлементДерева Из ДеревоДокументов.ПолучитьЭлементы() Цикл
					Элементы.ДеревоДокументов.Развернуть(ЭлементДерева.ПолучитьИдентификатор(), Истина);
				КонецЦикла;
				
			КонецЕсли;	
		КонецЕсли; 
	КонецЕсли;	
	
КонецПроцедуры
