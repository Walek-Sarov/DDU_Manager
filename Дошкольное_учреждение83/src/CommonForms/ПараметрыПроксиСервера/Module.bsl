////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	НастройкаПроксиНаКлиенте = Параметры.НастройкаПроксиНаКлиенте;
	Если НЕ Параметры.НастройкаПроксиНаКлиенте
		И НЕ Пользователи.ЭтоПолноправныйПользователь(, Истина) Тогда
		ВызватьИсключение НСтр("ru = 'Недостаточно прав доступа.
			|
			|Настройка прокси-сервера выполняется администратором.'");
		КонецЕсли;

	Если НастройкаПроксиНаКлиенте Тогда
		НастройкаПроксиСервера = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НастройкаПроксиСервера");
	Иначе
		НастройкаПроксиСервера = ПолучениеФайловИзИнтернета.ПолучитьНастройкиПроксиНаСервере1СПредприятие();
	КонецЕсли;
	
	Если ТипЗнч(НастройкаПроксиСервера) = Тип("Соответствие") Тогда
		ИспользоватьПрокси = НастройкаПроксиСервера.Получить("ИспользоватьПрокси");
		Сервер       = НастройкаПроксиСервера.Получить("Сервер");
		Пользователь = НастройкаПроксиСервера.Получить("Пользователь");
		Пароль       = НастройкаПроксиСервера.Получить("Пароль");
		Порт         = НастройкаПроксиСервера.Получить("Порт");
		НеИспользоватьПроксиДляЛокальныхАдресов = НастройкаПроксиСервера.Получить("НеИспользоватьПроксиДляЛокальныхАдресов");
		ИспользоватьСистемныеНастройки = НастройкаПроксиСервера.Получить("ИспользоватьСистемныеНастройки");
	КонецЕсли;
	
	ВариантИспользованияПроксиСервера = ?(ИспользоватьПрокси, ?(ИспользоватьСистемныеНастройки = Истина, 1, 2), 0);
	Если Не НастройкаПроксиНаКлиенте Тогда
		НастройкиПрокси = Неопределено;
		// Варианты настройки прокси-сервера:
		// 0 - Не использовать прокси-сервер (по умолчанию, соответствует Новый ИнтернетПрокси(Ложь))
		// 1 - Использовать системные настройки прокси-сервера (соответствует Новый ИнтернетПрокси(Истина))
		// 2 - Использовать свои настройки прокси-сервера (соответствует ручной настройке параметров прокси-сервера)
		// Для последнего становятся достуно ручное изменение параметров прокси-сервера
		Если ВариантИспользованияПроксиСервера = 0 Тогда
			НастройкиПрокси = ПустыеНастройкиПроксиСервера();
		ИначеЕсли ВариантИспользованияПроксиСервера = 1 Тогда
			НастройкиПрокси = СистемныеНастройкиПроксиСервераНаСервере();
		КонецЕсли;
		ИнициализироватьЭлементыФормы(ЭтаФорма, НастройкиПрокси);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НастройкаПроксиНаКлиенте Тогда
#Если ВебКлиент Тогда
		Предупреждение(НСтр("ru = 'В веб-клиенте параметры прокси-сервера необходимо задавать в настройках браузера.'"));
		Отказ = Истина;
		Возврат;
#КонецЕсли		
		НастройкиПрокси = Неопределено;
		// Варианты настройки прокси-сервера:
		// 0 - Не использовать прокси-сервер (по умолчанию, соответствует Новый ИнтернетПрокси(Ложь))
		// 1 - Использовать системные настройки прокси-сервера (соответствует Новый ИнтернетПрокси(Истина))
		// 2 - Использовать свои настройки прокси-сервера (соответствует ручной настройке параметров прокси-сервера)
		// Для последнего становятся достуно ручное изменение параметров прокси-сервера
		Если ВариантИспользованияПроксиСервера = 0 Тогда
			НастройкиПрокси = ПустыеНастройкиПроксиСервера();
		ИначеЕсли ВариантИспользованияПроксиСервера = 1 Тогда
			НастройкиПрокси = СистемныеНастройкиПроксиСервераНаКлиенте();
		КонецЕсли;
		ИнициализироватьЭлементыФормы(ЭтаФорма, НастройкиПрокси);
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ИспользоватьСистемныеНастройкиПроксиСервераПриИзменении(Элемент)
	
	ИспользоватьПрокси = ВариантИспользованияПроксиСервера > 0;
	ИспользоватьСистемныеНастройки = ВариантИспользованияПроксиСервера = 1;
	
	НастройкиПрокси = Неопределено;
	// Варианты настройки прокси-сервера:
	// 0 - Не использовать прокси-сервер (по умолчанию, соответствует Новый ИнтернетПрокси(Ложь))
	// 1 - Использовать системные настройки прокси-сервера (соответствует Новый ИнтернетПрокси(Истина))
	// 2 - Использовать свои настройки прокси-сервера (соответствует ручной настройке параметров прокси-сервера)
	// Для последнего становятся достуно ручное изменение параметров прокси-сервера
	Если ВариантИспользованияПроксиСервера = 0 Тогда
		НастройкиПрокси = ПустыеНастройкиПроксиСервера();
	ИначеЕсли ВариантИспользованияПроксиСервера = 1 Тогда
		НастройкиПрокси = ?(НастройкаПроксиНаКлиенте, СистемныеНастройкиПроксиСервераНаКлиенте(), СистемныеНастройкиПроксиСервераНаСервере());
	КонецЕсли;
	ИнициализироватьЭлементыФормы(ЭтаФорма, НастройкиПрокси);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура КнопкаОК(Команда)
	
	// Закрывает форму, передавая в качестве возвращаемого результата параметры прокси.
	НастройкаПроксиСервера = Новый Соответствие;
	НастройкаПроксиСервера.Вставить("ИспользоватьПрокси", ИспользоватьПрокси);
	НастройкаПроксиСервера.Вставить("Пользователь", Пользователь);
	НастройкаПроксиСервера.Вставить("Пароль", Пароль);
	НастройкаПроксиСервера.Вставить("Порт", Порт);
	НастройкаПроксиСервера.Вставить("Сервер", Сервер);
	НастройкаПроксиСервера.Вставить("НеИспользоватьПроксиДляЛокальныхАдресов", НеИспользоватьПроксиДляЛокальныхАдресов);
	НастройкаПроксиСервера.Вставить("ИспользоватьСистемныеНастройки", ИспользоватьСистемныеНастройки);
	
	СохранитьНастройкиПроксиСервера(НастройкаПроксиНаКлиенте, НастройкаПроксиСервера);
	Закрыть(НастройкаПроксиСервера);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервереБезКонтекста
Процедура СохранитьНастройкиПроксиСервера(НастройкаПроксиНаКлиенте, НастройкаПроксиСервера)
	Если НастройкаПроксиНаКлиенте Тогда
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("НастройкаПроксиСервера", , НастройкаПроксиСервера);
		ОбновитьПовторноИспользуемыеЗначения();
	Иначе
		ПолучениеФайловИзИнтернета.СохранитьНастройкиПроксиНаСервере1СПредприятие(НастройкаПроксиСервера);
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ИнициализироватьЭлементыФормы(Форма, НастройкиПрокси)
	
	Если НастройкиПрокси <> Неопределено Тогда	
		Форма.Сервер       = НастройкиПрокси.Сервер;
		Форма.Порт         = НастройкиПрокси.Порт;
		Форма.Пользователь = НастройкиПрокси.Пользователь;
		Форма.Пароль       = НастройкиПрокси.Пароль;
		Форма.НеИспользоватьПроксиДляЛокальныхАдресов = НастройкиПрокси.НеИспользоватьПроксиДляЛокальныхАдресов;
	КонецЕсли;
	
	// Изменяем доступность группы редактирования параметров прокси в зависимости от варианта использования прокси-сервера
	Форма.Элементы.ПараметрыПрокси.Доступность = (Форма.ВариантИспользованияПроксиСервера = 2);
	
КонецПроцедуры

&НаКлиенте
Функция СистемныеНастройкиПроксиСервераНаКлиенте()
	
	Прокси = Новый ИнтернетПрокси(Истина);
	
	Результат = Новый Структура();
	Результат.Вставить("Сервер", Прокси.Сервер());
	Результат.Вставить("Порт", Прокси.Порт());
	Результат.Вставить("Пользователь", Прокси.Пользователь);
	Результат.Вставить("Пароль", Прокси.Пароль);
	Результат.Вставить("НеИспользоватьПроксиДляЛокальныхАдресов", Прокси.НеИспользоватьПроксиДляЛокальныхАдресов);
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция СистемныеНастройкиПроксиСервераНаСервере()
	
	Прокси = Новый ИнтернетПрокси(Истина);
	
	Результат = Новый Структура();
	Результат.Вставить("Сервер", Прокси.Сервер());
	Результат.Вставить("Порт", Прокси.Порт());
	Результат.Вставить("Пользователь", Прокси.Пользователь);
	Результат.Вставить("Пароль", Прокси.Пароль);
	Результат.Вставить("НеИспользоватьПроксиДляЛокальныхАдресов", Прокси.НеИспользоватьПроксиДляЛокальныхАдресов);
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПустыеНастройкиПроксиСервера()
	
	Результат = Новый Структура();
	Результат.Вставить("Сервер", "");
	Результат.Вставить("Порт", 0);
	Результат.Вставить("Пользователь", "");
	Результат.Вставить("Пароль", "");
	Результат.Вставить("НеИспользоватьПроксиДляЛокальныхАдресов", Ложь);
	Возврат Результат;
	
КонецФункции
