
////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ОбработкаОжидания()
	
	Если Элементы.СтруктураПредприятия.ТекущаяСтрока <> Неопределено Тогда 
		СписокПользователи.Параметры.УстановитьЗначениеПараметра("Подразделение", Элементы.СтруктураПредприятия.ТекущаяСтрока);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура Включить(Команда)
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.Пользователи Тогда 
		ВыборПользователя(Элементы.СписокПользователи.ТекущаяСтрока);
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.Роли Тогда 
		ВыборРоли(Элементы.СписокРоли.ТекущаяСтрока);
				
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборПользователя(ВыбранноеЗначение)
	
	ПараметрыОтбора = Новый Структура("Исполнитель", ВыбранноеЗначение);
	Если Выбранные.НайтиСтроки(ПараметрыОтбора).Количество() > 0 Тогда
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Пользователь ""%1"" уже выбран!'"),
			Строка(ВыбранноеЗначение));
		
		Предупреждение(ТекстСообщения);
		
	Иначе
		НоваяСтрока = Выбранные.Добавить();
		НоваяСтрока.Исполнитель = ВыбранноеЗначение;
		Элементы.Выбранные.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборРоли(ВыбранноеЗначение)
	
	Если Не ОбщегоНазначения.ПолучитьЗначениеРеквизита(ВыбранноеЗначение, "ИспользуетсяСОбъектамиАдресации") Тогда 
		РезультатВыбора = Новый Структура("РольИсполнителя, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации", ВыбранноеЗначение, Неопределено, Неопределено);
	Иначе	
		РезультатВыбора = ОткрытьФормуМодально("ОбщаяФорма.ВыборРолиИсполнителя", Новый Структура("РольИсполнителя, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации", ВыбранноеЗначение, Неопределено, Неопределено), ЭтаФорма);
	КонецЕсли;	
	
	Если ТипЗнч(РезультатВыбора) = Тип("Структура") Тогда
		
		ПараметрыОтбора = Новый Структура("Исполнитель, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации", 
			РезультатВыбора.РольИсполнителя, 
			РезультатВыбора.ОсновнойОбъектАдресации, 
			РезультатВыбора.ДополнительныйОбъектАдресации);
			
		Если Выбранные.НайтиСтроки(ПараметрыОтбора).Количество() > 0 Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		                НСтр("ru = 'Роль ""%1"" с указанными параметрами адресации уже выбрана!'"), 
						Строка(РезультатВыбора.РольИсполнителя));
			
			Предупреждение(ТекстСообщения);
			
		Иначе
			НоваяСтрока = Выбранные.Добавить();
			НоваяСтрока.Исполнитель = РезультатВыбора.РольИсполнителя;
			НоваяСтрока.ОсновнойОбъектАдресации = РезультатВыбора.ОсновнойОбъектАдресации;
			НоваяСтрока.ДополнительныйОбъектАдресации = РезультатВыбора.ДополнительныйОбъектАдресации;
			Элементы.Выбранные.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ПоместитьИсполнителейВоВременноеХранилище()

	ПоместитьВоВременноеХранилище(Выбранные.Выгрузить(), АдресВременногоХранилища);
	
КонецПроцедуры	

&НаСервере
Процедура УстановитьИерархию(Отметка)
	
	Если Отметка = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Элементы.ИспользоватьИерархию.Пометка = Отметка;
	Если Отметка = Истина Тогда 
		Элементы.СтруктураПредприятия.Видимость = Истина;
	Иначе
		Элементы.СтруктураПредприятия.Видимость = Ложь;
	КонецЕсли;
	СписокПользователи.Параметры.УстановитьЗначениеПараметра("ИспользоватьИерархию", Отметка);
	
КонецПроцедуры	


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АдресВременногоХранилища = Параметры.АдресВременногоХранилища;
	Выбранные.Загрузить(ПолучитьИзВременногоХранилища(АдресВременногоХранилища));
	
	ВыбранныеКоличество = Выбранные.Количество();
	Для Инд = 1 По ВыбранныеКоличество Цикл
		СтрокаВыбранные = Выбранные[ВыбранныеКоличество - Инд];
		Если Не ЗначениеЗаполнено(СтрокаВыбранные.Исполнитель) И 
			 Не ЗначениеЗаполнено(СтрокаВыбранные.ОсновнойОбъектАдресации) И
			 Не ЗначениеЗаполнено(СтрокаВыбранные.ДополнительныйОбъектАдресации) Тогда 
		Выбранные.Удалить(СтрокаВыбранные);
		КонецЕсли;
	КонецЦикла;	
	
	СписокПользователи.Параметры.УстановитьЗначениеПараметра("Подразделение", Элементы.СтруктураПредприятия.ТекущаяСтрока);
	
	ИспользоватьИерархию = Истина;
	УстановитьИерархию(ИспользоватьИерархию);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	УстановитьИерархию(Настройки["ИспользоватьИерархию"]);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура СтруктураПредприятияПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбработкаОжидания", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПользователиВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыборПользователя(Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРолиВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыборРоли(Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура Исключить(Команда)
	
	ТекущаяСтрока = Элементы.Выбранные.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	СтрокаПоИдентификатору = Выбранные.НайтиПоИдентификатору(ТекущаяСтрока);
	Выбранные.Удалить(СтрокаПоИдентификатору);
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ПоместитьИсполнителейВоВременноеХранилище();
	ОповеститьОВыборе(АдресВременногоХранилища);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("СправочникСсылка.Пользователи") Тогда 
		ВыборПользователя(ПараметрыПеретаскивания.Значение); 
		
	ИначеЕсли ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("СправочникСсылка.РолиИсполнителей") Тогда 
		ВыборРоли(ПараметрыПеретаскивания.Значение);
			
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьИерархию(Команда)
	
	ИспользоватьИерархию = Не ИспользоватьИерархию;
	Если ИспользоватьИерархию И (Элементы.СписокПользователи.ТекущиеДанные <> Неопределено) Тогда 
		Элементы.СтруктураПредприятия.ТекущаяСтрока = Элементы.СписокПользователи.ТекущиеДанные.Подразделение;
		СписокПользователи.Параметры.УстановитьЗначениеПараметра("Подразделение", Элементы.СтруктураПредприятия.ТекущаяСтрока);
	КонецЕсли;	
	УстановитьИерархию(ИспользоватьИерархию);
	
КонецПроцедуры
