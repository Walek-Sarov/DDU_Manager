
////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ОбработкаОжидания()
	
	Если Элементы.СтруктураПредприятия.ТекущаяСтрока <> Неопределено Тогда 
		СписокПользователи.Параметры.УстановитьЗначениеПараметра("Подразделение", Элементы.СтруктураПредприятия.ТекущаяСтрока);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.Пользователи Тогда 
		ОповеститьОВыборе(Элементы.СписокПользователи.ТекущаяСтрока);
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.Роли Тогда 
		ВыборРоли(Элементы.СписокРоли.ТекущаяСтрока);
				
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Функция ВыборРоли(ВыбранноеЗначение)
	
	Если Не ОбщегоНазначения.ПолучитьЗначениеРеквизита(ВыбранноеЗначение, "ИспользуетсяСОбъектамиАдресации") Тогда 
		Результат = Новый Структура("РольИсполнителя, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации", ВыбранноеЗначение, Неопределено, Неопределено);
	Иначе	
		Результат = ОткрытьФормуМодально("ОбщаяФорма.ВыборРолиИсполнителя", Новый Структура("РольИсполнителя, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации", ВыбранноеЗначение, Неопределено, Неопределено), ЭтаФорма);
	КонецЕсли;	
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		ОповеститьОВыборе(Результат);
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура УстановитьИерархию(Отметка)
	
	Если Отметка = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Элементы.ИспользоватьИерархию.Пометка = Отметка;
	Если Отметка = Истина Тогда 
		Элементы.СтруктураПредприятия.Видимость = Истина;
	Иначе
		Элементы.СтруктураПредприятия.Видимость = Ложь;
	КонецЕсли;
	СписокПользователи.Параметры.УстановитьЗначениеПараметра("ИспользоватьИерархию", Отметка);
	
КонецПроцедуры	


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	

	Исполнитель = Параметры.Исполнитель;
	Если ЗначениеЗаполнено(Исполнитель) Тогда
		
		Если ТипЗнч(Исполнитель) = Тип("СправочникСсылка.Пользователи") Тогда 
		
			Подразделение = РаботаСПользователями.ПолучитьПодразделение(Исполнитель);
			Если Не Подразделение.Пустая() Тогда
				Элементы.СтруктураПредприятия.ТекущаяСтрока = Подразделение;
				Элементы.СписокПользователи.ТекущаяСтрока = Исполнитель;
			КонецЕсли;
			Элементы.Страницы.ТекущаяСтраница = Элементы.Пользователи;
			
		ИначеЕсли ТипЗнч(Исполнитель) = Тип("СправочникСсылка.РолиИсполнителей") Тогда 
			Элементы.СписокРоли.ТекущаяСтрока = Исполнитель;
			Элементы.Страницы.ТекущаяСтраница = Элементы.Роли;
						
		КонецЕсли;	
		
	КонецЕсли;	
	
	СписокПользователи.Параметры.УстановитьЗначениеПараметра("Подразделение", Элементы.СтруктураПредприятия.ТекущаяСтрока);
	
	ИспользоватьИерархию = Истина;
	УстановитьИерархию(ИспользоватьИерархию);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	УстановитьИерархию(Настройки["ИспользоватьИерархию"]);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура СтруктураПредприятияПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбработкаОжидания", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПользователиВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОповеститьОВыборе(Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРолиВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыборРоли(Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьИерархию(Команда)
	
	ИспользоватьИерархию = Не ИспользоватьИерархию;
	Если ИспользоватьИерархию И (Элементы.СписокПользователи.ТекущиеДанные <> Неопределено) Тогда 
		Элементы.СтруктураПредприятия.ТекущаяСтрока = Элементы.СписокПользователи.ТекущиеДанные.Подразделение;
		СписокПользователи.Параметры.УстановитьЗначениеПараметра("Подразделение", Элементы.СтруктураПредприятия.ТекущаяСтрока);
	КонецЕсли;	
	УстановитьИерархию(ИспользоватьИерархию);
	
КонецПроцедуры
