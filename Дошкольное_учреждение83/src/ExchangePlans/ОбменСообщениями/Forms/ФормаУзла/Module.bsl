
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоЭтотУзел = (Объект.Ссылка = ОбменСообщениямиВнутренний.ЭтотУзел());
	
	Элементы.ГруппаИнформационныхСообщений.Видимость = Не ЭтоЭтотУзел;
	
	Если Не ЭтоЭтотУзел Тогда
		
		Если Объект.Ведущая Тогда
			Элементы.ИнформационноеСообщение.Заголовок 
				= НСтр("ru = 'Эта конечная точка является ведущей, т.е. инициирует отправку и получение сообщений обмена для текущей информационной системы.'");
			//
		Иначе
			Элементы.ИнформационноеСообщение.Заголовок 
				= НСтр("ru = 'Эта конечная точка является ведомой, т.е. выполняет отправку и получение сообщений обмена только по требованию текущей информационной системы.'");
			//
		КонецЕсли;
		
		Элементы.СделатьЭтуКонечнуюТочкуВедомой.Видимость = Объект.Ведущая;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Оповестить(ОбменСообщениямиКлиент.ИмяСобытияЗакрытаФормаКонечнойТочки());
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = ОбменСообщениямиКлиент.ИмяСобытияУстановленаВедущаяКонечнаяТочка() Тогда
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура СделатьЭтуКонечнуюТочкуВедомой(Команда)
	
	ПараметрыФормы = Новый Структура("КонечнаяТочка", Объект.Ссылка);
	
	ОткрытьФорму("ОбщаяФорма.УстановкаВедущейКонечнойТочки", ПараметрыФормы, ЭтаФорма, Объект.Ссылка);
	
КонецПроцедуры


